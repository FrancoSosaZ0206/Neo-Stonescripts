// *******************************************************
//                       6-Mine.txt                       
// *******************************************************

// Script for Boiling Mine



// IMPORTS

var uimkr = new MyScripts/Lib/UI_Maker
var u = new MyScripts/Lib/Utilities
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat


/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter

var somPanel = som.mkUI(0, 1, u.colorToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, u.colorToHex("red"), true, false)

/***************************************************************

var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED

dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/



// VARIABLES

var runCount = 0
var keepCultistMask = true

var smiteScreen = 1
var voidweaverScreen = 4

var canDebuffIce
var canDebuffPoison
var canDebuffFire

var keepFireTalisman = false

?foe = "boss"
  keepFireTalisman = true

var rangedDebuffCount = 0

?loc.loop
  keepCultistMask = false
  keepFireTalisman = false
  rangedDebuffCount = 0

  ?totaltime = 1
    runCount++

    ?loc.stars > 5 & runCount >= 2 & smiteScreen ! 3
      smiteScreen++



// FUNCTIONS

func ldtF(mode) // loadouts

  ?mode = "default"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound2@
  :?mode = "engage"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldAetherDefense@
  :?mode = "escape"
    equipL mind
    ?!keepCultistMask
      equipR @a.shieldAetherDefense@
  :?mode = "magnet"
    equipL star
    ?!keepCultistMask
      equipR triskelion

  :?mode = "extend"
    equipL triskelion
    equipR mask

  :?mode = "unmake"
    equip @a.staffAetherDebuff@

  :?mode = "rangedDmg"
    equip @a.bowAetherDmg@
  :?mode = "rangedP"
    equip @a.bowPoisonDebuff@
  :?mode = "rangedF"
    equip @a.bowFireDebuff@

  :?mode = "magic"
    ?keepCultistMask
      ?canDebuffIce
        equipL @a.wandIceDebuff@
      :?canDebuffPoison
        equipL @a.wandPoisonDebuff@
      :?canDebuffFire
        equipL @a.wandFireDebuff@

    :
      ?canDebuffIce
        equip @a.staffIceDebuff@
      :?canDebuffPoison
        equip @a.staffPoisonDebuff@
      :?canDebuffFire
        equip @a.staffFireDebuff@

  :
    >`0,2,#red,ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight1v1()

  /* How to apply debuffs:
  - apply all except the ones the foe is immune to.
  Finish when:
  - all possible debuffs have been applied. */
  canDebuffIce = (foe.debuffs.string ! "chill:6" & foe ! "immune_to_debuff_chill")
  canDebuffPoison = (foe.debuffs.string ! "damage" & foe ! "immune_to_debuff_damage")
  canDebuffFire = (foe.debuffs.string ! "dot" & foe ! "immune_to_debuff_dot")

  ?canDebuffIce | canDebuffPoison | canDebuffFire
    ?keepCultistMask
      ?canDebuffIce
        equipL @a.swordIceDebuff@
      :?canDebuffPoison
        equipL @a.swordPoisonDebuff@
      :?canDebuffFire
        equipL @a.swordFireDebuff@
    :
      //Left hand:
      ?canDebuffPoison
        equipL @a.swordPoisonDebuff@
      :?canDebuffFire
        equipL @a.swordFireDebuff@
      :?canDebuffIce
        equipL @a.wandIceDebuff@

      //Right hand:
      ?canDebuffIce
        equipR @a.swordIceDebuff@

  :?hp ! maxhp
    ?keepCultistMask
      equipL @a.swordVigorBuff@
    :
      equipL moon
      equipR @a.swordVigorBuff@

  :
    ?keepCultistMask
      equipL Void Slayer
    :
      ?buffs.string = "berserk"
        c.doMoondial(Void Slayer, a.swordAetherDmg, 2)
      :
        c.doMoondial(Void Slayer, a.swordAetherDmg, 0)

  return



// PROCEDURE:


c.doAAC("sword 0*", a.shieldCompound1)

?loc.begin // AUTO BREWING:

  ?res.stone >= 20
    ?item.potion ! "strength"
      brew stone
  :
    loc.Leave()

?keepCultistMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer"
    equipR mask

?loc.begin | loc.loop
  equipL triskelion
  equipR @a.shieldCompound1@

:?c.canUseAetherTalisman() & !keepFireTalisman
  ?keepCultistMask
    c.useAetherTalisman("l")
  :
    equipL triskelion
    c.useAetherTalisman("r")

:?c.canUseFireTalisman() & keepFireTalisman
  ?keepCultistMask
    c.useFireTalisman("l")
  :
    equipL triskelion
    c.useFireTalisman("r")

:?c.canUsePotion() & keepCultistMask
  activate potion

:?pickup.distance < 15
  ldtF("magnet")
:
  ?c.canUseBFG_DS(smiteScreen)
    c.useBFG_DS()

  /****************************/
  ?c.canUseVoidweaver()
    ?screen.i = voidweaverScreen & foe.distance < 15
    ^& keepCultistMask
      c.useVoidweaver()
  // **************************/

  ?foe.distance > 35

    ?c.canUseQuarterstaff() & ai.enabled
    ^& item.right ! "bardiche" & item.right ! Kubikiribocho
    ^& item.right ! "heavy hammer"
      c.useQuarterstaff(a.myQuarterstaff)

    :?14 <= armor & armor <= 29
      ldtF("default")
    :?armor < 14
      ldtF("armorRegen")

    :
      equipL triskelion
      equipR mask
  :

    ?foe ! "bronze_guardian"
      ?foe.distance > 15
        ?c.canUseQuarterstaff() & ai.enabled
        ^& item.right ! "bardiche" &
        ^  item.right ! Kubikiribocho &
        ^  item.right ! "heavy hammer" &
        ^(foe ! "explode" & foe.distance > 17)
          c.useQuarterstaff(a.myQuarterstaff)

        :
          ldtF("engage")

          var isAbtToDash
          isAbtToDash = (foe.distance <= 17)
          ?keepCultistMask & isAbtToDash
            equipR @a.shieldAetherDefense@

          canDebuffIce = (foe.debuffs.string ! "chill" &
          ^f ! "immune_to_debuff_chill")

          ?!keepCultistMask & canDebuffIce & isAbtToDash &
          ^i.getFoeTotalHP() > a.smiteDmg
            equipR @a.shieldIceDebuff@

      :?c.canUseBashingShield()
        c.useBashingShield(Void Slayer)
      :?c.canUseDashingShield()
        c.useDashingShield(Void Slayer,a.shieldDashing)

      :?foe = "explode" // minecart miniboss
        ?armor < 80 // we don't need to escape
          ldtF("escape") // if we can tank the damage.

      :?c.canUseQuarterstaff() & ai.enabled
      ^& item.right ! "bardiche"
      ^& item.right ! Kubikiribocho
      ^& item.right ! "heavy hammer"
        c.useQuarterstaff(a.myQuarterstaff)

      :
        ?c.canUseHeavyHammer() & keepCultistMask
        ^& foe.distance < 15 &
        ^((smiteScreen = 1 & screen.i = 3) |
        ^ smiteScreen = 3 & screen.i = 2    )
          c.useHeavyHammer(a.myHeavyHammer)

        :?foe.count = 1
          fight1v1()

        :
          canDebuffIce = (foe.debuffs.string ! "chill:6" & foe ! "immune_to_debuff_chill")
          canDebuffPoison = (foe.debuffs.string ! "damage" & foe ! "immune_to_debuff_damage")
          canDebuffFire = (foe.debuffs.string ! "dot" & foe ! "immune_to_debuff_dot")

          ?canDebuffIce | canDebuffPoison | canDebuffFire
            ldtF("magic")
          :
            ldtF("unmake")


    : // foe = "bronze_guardian"
      ?foe.state = 4 // dying
        ldtF("magnet")

      :?foe.state ! 1 // boss appearing cinematic
        ?c.canUseCinderwisp() & keepCultistMask
          var currentIgnitionLvl = 0
          currentIgnitionLvl = i.getDebuffLvl("ignition", true)

          ?foe.debuffs.string = a.maxIgnitions_Default |
          ^(currentIgnitionLvl > 3 &
          ^c.canKillCinderwisp(i.getFoeTotalHP(),
          ^              currentIgnitionLvl,
          ^              a.cinderwispDmg_Default, 0))
            c.useCinderwisp()

        ?c.canUsePotionDmg() |
        ^c.canUsePotionSpecial("strength",0)
          activate potion
        :?c.canUseHeavyHammer() // & keepCultistMask
          c.useHeavyHammer(a.myHeavyHammer)
        :?c.canUseCultistMask() // & keepCultistMask
          c.useCultistMask()

        :?foe.state = 32 & foe.time >= u.sec // boss's hammer attack
          ldtF("escape")

        :?foe.state = 33 & foe.time < 6 * u.sec // boss's hammer's stuck

          ?foe.time > 5 & c.canUseQuarterstaff()
            c.useQuarterstaff(a.myQuarterstaff)

          :?foe.distance > 15
            equipL triskelion // get close fast to boss
            ?!keepCultistMask
              equipR Void Slayer
          :?c.canUseBashingShield()
            c.useBashingShield(Void Slayer)
          :?c.canUseDashingShield()
            useDashingShield(VoidSlayer,a.shieldDashing)

          :
            ?c.canUseBardiche() & keepCultistMask
              c.useBardiche(Kubikiribocho)
            :
              fight1v1()

        :
          ?keepCultistMask
            ?item.left.state = 3
              rangedDebuffCount++

            ?rangedDebuffCount = 0
              equipL @a.wandFireDebuff@
            :?rangedDebuffCount = 1
              equipL @a.wandPoisonDebuff@
            :
              equipL @a.bowAlternate@

          :
            ?item.right.state = 3
              rangedDebuffCount++

            ?rangedDebuffCount = 0
              equip @a.staffFireDebuff@
            :?rangedDebuffCount = 1
              equip @a.staffPoisonDebuff@
            :
              ldtF("rangedDmg")

          /* :
            ?foe.armor < 1
              ?c.canUseCultistMask() & !keepCultistMask
                c.useCultistMask()
              :?c.canUseHeavyHammer() & !keepCultistMask & item.potion = "strength"
                c.useHeavyHammer(a.myHeavyHammer)
              :
                var pBowCount = rangedDebuffCount + 1
                ?foe.debuffs.string ! "damage" & pBowCount ! rangedDebuffCount
                ^& item.potion = "strength"
                  ldtF("rangedP")

                :?keepCultistMask
                  equipL @a.bowAlternate@
                :
                  ldtF("rangedDmg")
          */


// *******************************************************
//                   END OF "6-Mine.txt"                  
// *******************************************************