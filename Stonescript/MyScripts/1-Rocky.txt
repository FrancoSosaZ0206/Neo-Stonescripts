// *******************************************************
//                   START OF "1-Rocky"                   
// *******************************************************

// Script for Rocky Plateau



// /////////////////////////////////////////// //

// IMPORTS

var uimkr = new MyScripts/Lib/UI_Maker
var u = new MyScripts/Lib/Utilities
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat
c.Init()



// /////////////////////////////////////////// //

// VARIABLES

var canDash
var trueDmg

var keepCultistMask = true
var moondialMode = -1
var smiteScreen = null

?loc.loop
  keepCultistMask = false

  ?totaltime = 1 & smiteScreen ! 4
    smiteScreen++

?buffs.string = "berserk"
  moondialMode = 2
:
  moondialMode = 0

// Calculate the amount of damage the foe will deal
?loc.stars <= 15
  trueDmg = c.getFoeDamage(a.weaken_default, a.mask_feebleCount)
:
  trueDmg = c.getFoeDamage(a.weaken_boosted, a.mask_feebleCount)


// /////////////////////////////////////////// //

// FUNCTIONS

func harvFunc(btn)
  btnPressed = true
  ?btn = yesBtn
    choice = true
  : // btn = noBtn
    choice = false

  return


func ldtF(mode)

  ?mode = "default"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound2@
  :?mode = "engage"
    ?buffs.string ! "buff_inf_speed" & // infernal staff
    ^totaltime % 2 = 0 // every even frame
      equipL @a.hammerFireDefense@
    :
      equipL triskelion

    ?!keepCultistMask
      ?c.canApplyChill() & encounter.eliteMod ! "tenacious"
        equipR @a.shieldIceDebuff@
      :
        equipR @a.shieldFireDefense@

  :?mode = "bash"
    equipL Vantum Phoenix
    equipR bashing shield
  :?mode = "dash"
    equipL Vantum Phoenix
    equipR @a.shieldDashing@

  :?mode = "escape"
    equipL mind
    ?!keepCultistMask
      equipR @a.shieldFireDefense@
  :?mode = "magnet"
    equipL star
    ?!keepCultistMask
      equipR triskelion
  :?mode = "harvest"
    equip shovel

  :?mode = "defenseHeal" // heal when hit
    equipL @a.hammerVigorBuff@
    equipR @a.shieldVigorBuff@
  :?mode = "defenseUnmake" // unmake when hit
    equipL @a.swordAetherDebuff@
    equipR @a.shieldAetherDebuff@

  :?mode = "meleeDmg"
    ?keepCultistMask
      ?mode = "meleeDmgPoison"
        equipL Mamba Negra
      :?mode = "meleeDmgVigor"
        equipL Dothaneel
      :?mode = "meleeDmgAether"
        equipL Void Slayer
      :?mode = "meleeDmgFire"
        equipL Vantum Phoenix
      :?mode = "meleeDmgIce"
        equipL Bifrost
    :
      ?mode = "meleeDmgPoison"
        c.doMoondial(Mamba Negra, a.swordPoisonDmg, moondialMode)
      :?mode = "meleeDmgVigor"
        c.doMoondial(Dothaneel, a.swordVigorDmg, moondialMode)
      :?mode = "meleeDmgAether"
        c.doMoondial(Void Slayer, a.swordAetherDmg, moondialMode)
      :?mode = "meleeDmgFire"
        c.doMoondial(Vantum Phoenix, a.swordFireDmg, moondialMode)
      :?mode = "meleeDmgIce"
        c.doMoondial(Bifrost, a.swordIceDmg, moondialMode)
  :?mode = "meleeDebuff"
    ?keepCultistMask
      ?c.canApplyChill()
        equipL @a.swordIceDebuff@
      :?c.canApplyWeaken()
        ?loc.stars <= 15 | foe = "phase3"
          equipL @a.swordPoisonDebuff@
        :
          equipL Death's Kiss

      :?c.canApplyBurn()
        equipL @a.swordFireDebuff@
    :
      //Left hand:
      ?c.canApplyWeaken()
        ?loc.stars <= 15 | foe = "phase3"
          equipL @a.swordPoisonDebuff@
        :
          equipL Death's Kiss
      :?c.canApplyBurn()
        equipL @a.swordFireDebuff@
      :?c.canApplyChill()
        equipL @a.wandIceDebuff@

      //Right hand:
      ?c.canApplyChill()
        equipR @a.swordIceDebuff@
  :?mode = "meleeHeal"
    ?keepCultistMask
      equipL Soulfetcher
    :
      c.doMoondial(Vantum Phoenix, Soulfetcher, moondialMode)

  :?mode = "rangedAOEUnmake"
    equip @a.staffAetherDebuff@
    /* ?keepCultistMask
      equipL @a.bigSwordAetherDebuff@
    : */
  :?mode = "meleeUnmake"
    equip @a.staffAetherDebuff@
    /* ?keepCultistMask
      equipL @a.swordAetherDebuff@
    :
      // put a 2nd dU sword when we have it
      c.doMoondial(a.swordAetherDebuff, Vantum Phoenix, moondialMode)*/

  :?mode = "rangedAOEDmg"
    equipL @a.wandFireDmg@
    ?!keepCultistMask
      ?foe = "dysangelos"
        equipR @a.bowAetherDmg@
      :
        equipR @a.staffFireDebuff@ // @a.bowFireDmg@
  :?mode = "rangedAOEDebuff"
    ?keepCultistMask
      ?c.canApplyChill()
        equipL @a.wandIceDebuff@
      :?c.canApplyWeaken()
        equipL @a.wandPoisonDebuff@
      :?c.canApplyBurn()
        equipL @a.wandFireDebuff@

    :
      ?c.canApplyChill()
        equip @a.staffIceDebuff@
      :?c.canApplyWeaken()
        equip @a.staffPoisonDebuff@
      :?c.canApplyBurn()
        equip @a.staffFireDebuff@
  :?mode = "rangedAOEHeal"
    ?keepCultistMask
      equipL @a.wandVigorBuff@
    :
      equipL moon
      equipR @a.wandVigorBuff@
      // equip @a.staffVigorBuff@ // @a.bowVigorBuff@

  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight(mode)

  ?c.canDebuffAny() & // foe can be debuffed
  ^encounter.eliteMod ! "tenacious" // foe isn't immune to debuffs
    ?mode = "melee"
      ldtF("meleeDebuff")
    :?mode = "rangedAOE"
      ldtF("rangedAOEDebuff")

  : // all possible debuffs applied
    ?hp ! maxhp // heal up
      ?mode = "melee"
        ldtF("meleeHeal")
      :?mode = "rangedAOE"
        ldtF("rangedAOEHeal")

    : // deal max damage
      ?mode = "melee"
        ldtF("meleeDmgFire")
      :?mode = "rangedAOE"
        ldtF("rangedAOEDmg")

  return



// /////////////////////////////////////////// //

// PROCEDURE

>`1,15,#magenta,
^baseDmg = @foe.damage@\n
^trueDmg = @trueDmg@\n
^dist    = @foe.distance@\n

disable npcDialog

var choice = false
var btnPressed = false

var yesBtn
var noBtn

var dontShowAgain = false


?!btnPressed
  ?!yesBtn
    yesBtn = uimkr.mkBtn(
    ^-5,9,null,2,null,null, // x, y, w, h, anchor, dock,
    ^"Yes","#green", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound

  ?!noBtn
    noBtn = uimkr.mkBtn(
    ^5,9,8,2,null,null, // x, y, w, h, anchor, dock,
    ^"No","red", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound


  u.showStrCtr(6,30,
  ^"Harvest boulders?","#yellow",
  ^false)
:
  ?yesBtn
    yesBtn.Recycle()
    yesBtn = null
  ?noBtn
    noBtn.Recycle()
    noBtn = null


  ?!dontShowAgain
    var aux = totaltime + 3*u.sec
    ?totaltime < aux
      ?choice
        u.showStrCtr(6, 30,
        ^"Harvest boulders is enabled","#green",
        ^false)
      :
        u.showStrCtr(6, 30,
        ^"Harvest boulders is disabled","#red",
        ^false)
    :
      ?!dontShowAgain
        dontShowAgain = true


c.doAAC(a.hammerFireDefense, a.shieldFireDefense)

// Auto Brewing
?c.canBrewPotion("lucky")
  c.brewPotion("lucky")

?loc.begin // initial equipment
  equipL triskelion
  equipR @a.shieldCompound1@
:?keepCultistMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer" &
  ^item.right ! "quarterstaff"
    equipL triskelion
    equipR mask


?c.canUseBFG_DS(smiteScreen) & loc.stars > 15
  c.useBFG_DS()


?foe ! "boss" & !c.isDead() &
^c.canUseAetherTalisman()
  ?keepCultistMask
    c.useAetherTalisman("l")
  :
    equipL triskelion
    c.useAetherTalisman("r")
:?(foe = "boss" | loc.stars <= 15) &
^c.canUseFireTalisman()
  ?keepCultistMask
    c.useFireTalisman("l")
  :
    equipL triskelion
    c.useFireTalisman("r")


:?c.canUsePotionDmg() & keepCultistMask
  activate potion
:?c.canUseStaffHidden("infernal") // speed up
  c.useStaffHidden(a.staffFireHidden, "infernal")


:?choice & harvest.distance < 5
  ldtF("harvest")
:?pickup.distance < 15
  ldtF("magnet")


// About to attack
:?/*(*/foe.state = 32 & (
^(foe = "acronian_scout" & foe.time > 50) |
^(foe = "phase1" & foe.time > 60) |
^(foe = "phase2" & foe.time > 95) |
^(foe = "phase3" &
^   ((foe.time > 85 & foe.damage ! 1) |
^    (foe.time > 25 & foe.damage = 1))))/* |
^(foe = "phase3" & foe.state = 155 & foe.time > )*/
  >c0,4,#red,ABOUT TO ATTACK

  ?armor < trueDmg
    ?foe = "phase3" // & foe.time > 85 // Dysangelos's massive beam
      ?c.canUseMindstone()
        ldtF("escape")
      :?c.canUseStaffHidden("acrobatic") & foe.distance <= c.getRange("sword")
        c.useStaffHidden(a.staffStoneHidden, "acrobatic")
    :?c.canUseStaffHidden("eternity")
        c.useStaffHidden(a.staffIceHidden, "eternity")

    /*:?foe = "phase3" & foe.state = 107 & foe.time >  & // prevent the stun
    ^c.canUseStaffHidden("prevention")
      c.useStaffHidden("prevention")*/

    :?foe.distance > c.getRange("sword")
      fight("rangedAOE")
    :?foe ! "phase2" // heal to reduce it even more
      ldtF("defenseHeal")
    : // normal phase2 behaviour
      ?foe = "poison"
        ldtF("meleeDmgIce")
      :?foe = "vigor"
        ?foe.buffs.string = "buff_protection" & // against debuffs
        ^c.canUseWandHidden("reset")
          c.useWandHidden(a.wandVigorHidden, "reset", "l")
        :
          ldtF("meleeDmgPoison")
      :?foe = "aether"
        ldtF("meleeDmgVigor")
      :?foe = "fire"
        ldtF("meleeDmgAether")
      :?foe = "ice"
        ldtF("meleeDmgFire")


  :?armor >= trueDmg
    ?buffs.string ! "buff_damage" & foe ! "phase2"
      equipR @a.shieldPoisonBuff@
    :?foe = "phase2" & foe = "fire" & foe.time > 95
      ldtF("defenseUnmake") // unmake Dysangelos's fire arm


:?ai.walking &
^(foe.distance < u.dash_range_min |
^foe.distance > u.dash_range_max) &
^c.canUseQuarterstaff()
  c.useQuarterstaff(a.myQuarterstaff)

:?ai.paused |
^(ai.idle & foe.distance > c.getRange("sword")) | // if we're waiting
^c.isDead() |
^foe.state = 124 | foe.state = 125 | // dysangelos phase 1 to 2 states
^(foe.state = 107 & foe.damage = 3) | // dsangelos' flying animation
^foe.state = 108 | // dysangelos phase 2 to 3 states
^foe.state = 116 | // dysangelos phase 3 death animation state
^foe.distance > u.dash_range_max // or we can't engage in a fight
  ?!ai.walking & foe = "dysangelos" &
  ^(c.canUseHeavyHammer() | c.canUseBardiche() |
  ^(foe.distance <= u.bardiche_ability_range + u.grasping_buff
  ^& c.canUseStaffHidden("grasping")))
    ?c.canUseHeavyHammer()
      c.useHeavyHammer("heavy hammer *max -shiny +6") // fast speed hammer
    
    :?c.canUseStaffHidden("grasping")
      c.useStaffHidden(a.staffAetherHidden, "grasping")
    :?c.canUseBardiche()
      c.useBardiche(Kubikiribocho)

  :?armor > a.compoundMaxArmor_boosted |
  ^(foe ! "phase" & foe.distance < u.shield_rune_range)
    ldtF("engage")
  :?a.compoundMaxArmor_default <= armor &
  ^armor <= a.compoundMaxArmor_boosted
    ldtF("default")
  :
    ldtF("armorRegen")


:?encounter.eliteMod = "monarch"
  ?foe.count > 1 // eliminate the other foes
    ?c.canUseBFG()
      c.useBFG()
    :?c.canUseWandHidden("explosive")
      c.useWandHidden(a.wandFireHidden, "explosive", "l")

    :?foe.distance < 7 & // if too close, try to escape
    ^(c.canUseMindstone() |
    ^c.canUseStaffHidden("acrobatic"))
      ?c.canUseMindstone()
        ldtF("escape")
      :
        c.useStaffHidden(a.staffStoneHidden, "acrobatic")

    :?c.canUseHeavyHammer() // if close, use hammer
      c.useHeavyHammer(Mjölnir)
    :
      fight("rangedAOE")

  :?c.canUseBashingShield()
    ldtF("bash")
  :?c.canUseDashingShield()
    ldtF("dash")

  : // 1v1 with the monarch
    ?foe.hp + foe.armor >= a.bardicheRDmg &
    ^c.canUseBardiche()
      c.useBardiche(Kubikiribocho)
    :
      fight("melee")

: // fighting range
  ?c.canUseBashingShield() & !canDash
    ldtF("bash")
  :?c.canUseDashingShield() & !canDash
    ldtF("dash")

  :?foe = "acronian_scout"
    fight("melee")

  : // Dysangelos
    ?foe = "phase1"
      fight("melee")

    :?foe = "phase2"
      ?hp ! maxhp
        ldtF("meleeHeal")
      :?c.canDebuffAny() & foe.buffs.string ! "buff_protection"
        ldtF("meleeDebuff")

      :?foe = "poison"
        ldtF("meleeDmgIce")
      :?foe = "vigor"
        ?foe.buffs.string = "buff_protection" & // against debuffs
        ^c.canUseWandHidden("reset")
          c.useWandHidden(a.wandVigorHidden, "reset", "l")
        :
          ldtF("meleeDmgPoison")
      :?foe = "aether"
        ldtF("meleeDmgVigor")
      :?foe = "fire"
        ldtF("meleeDmgAether")
      :?foe = "ice"
        ldtF("meleeDmgFire")

    : // ?foe = phase3
      ?c.canUsePotionDmg()
        activate potion
      :?c.canUseCultistMask()
        c.useCultistMask()
      :?c.canUseStaffHidden("berserker")
        c.useStaffHidden(a.staffPoisonHidden, "berserker")

      ?c.canUseCinderwisp() &
      ^foe.debuffs.string = a.maxIgnitions_Default
        c.useCinderwisp()

      :
        ?hp ! maxhp
          ldtF("meleeHeal")
        :?c.canDebuffAny()
          ldtF("meleeDebuff")

        :?c.canUseHeavyHammer() & foe.armor > 0
          c.useHeavyHammer("Guardian's Bane")

        :?foe.buffs.string ! "defense_fire"
          ldtF("meleeDmgFire")
        :
          ldtF("meleeDmgIce")



// *******************************************************
//                    END OF "1-Rocky"                    
// *******************************************************