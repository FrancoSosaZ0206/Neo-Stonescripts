// *******************************************************
//                   START OF "1-Rocky"                   
// *******************************************************

// Script for Rocky Plateau



// IMPORTS

var u = new MyScripts/Lib/Utilities
var uimkr = new MyScripts/Lib/UI_Maker
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo

var c = new MyScripts/Lib/Combat
c.Main()

/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter
var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED
var bosTmr
bosTmr = import MyScripts/Borrowed/BossfightTimer2

var somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)


dpsm.ENABLED = true
dpsm.refreshRate = 5

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/


// VARIABLES

var dontDash
var trueDmg

var nRuns = 0

var dysanToPhase3Done = false
var dysanToPhase3Timer = -1

var keepMask = true

var resetApsTimer = true
var restAps = false

?loc.loop
  dysanToPhase3Timer = -1
  dysanToPhase3Done = false

  keepMask = false

  resetApsTimer = true
  restAps = false

  ?totaltime = 1
    nRuns++

var moondialingMode = -1


// FUNCTIONS

func harvFunc(btn)
  btnPressed = true
  ?btn = yesBtn
    choice = true
  : // btn = noBtn
    choice = false

  return

func bossFight()

  ?foe = "phase1"
    ?!keepMask & hp = maxhp
      c.doMoondial("Vantum Phoenix", a.fsword, moondialingMode)
    :
      equipL Vantum Phoenix

  :?foe = "phase2"
    ?foe = "poison"
      ?!keepMask & hp = maxhp
        c.doMoondial("Bifrost", a.isword, moondialingMode)
      :
        equipL Bifrost
    :?foe = "vigor"
      ?!keepMask & hp = maxhp
        c.doMoondial("Mamba Negra", a.psword, moondialingMode)
      :
        equipL Mamba Negra
    :?foe = "aether"
      ?!keepMask & hp = maxhp
        c.doMoondial("Dothaneel", a.vsword, moondialingMode)
      :
        equipL Dothaneel
    :?foe = "fire"
      ?!keepMask & hp = maxhp
        c.doMoondial("Void Slayer", a.aesword, moondialingMode)
      :
        equipL Void Slayer
    :?foe = "ice"
      ?!keepMask & hp = maxhp
        c.doMoondial("Vantum Phoenix", a.fsword, moondialingMode)
      :
        equipL Vantum Phoenix

  : // ?foe = phase3
    ?c.canUsePotDmg()
      activate potion
    :?c.canUseMask()
      c.useMask()

    ?c.canUseCind() & dpsm.DPS
      ?foe.debuffs.string = a.maxIgnitions_Default |
      ^c.canKillCind(i.getFoeTotalHP(),
      ^              i.getDebuffLvl("ignition", true),
      ^              a.cindDmg_Default, dpsm.DPS + a.cindDmg_Default)
        c.useCind()

    :?c.canUseCind() & !dpsm.DPS
      ?foe.debuffs.string = a.maxIgnitions_Default
        c.useCind()

    foe.time = foe.time

    ?((foe.state = 115 & foe.time < 70) |
    ^(foe.state = 32 & foe.damage = 1))
    ^& foe.time >= 42 // putting up shield
      dontDash = true

      ?c.canUseMind()
        ldtF("escape")
      :?foe.distance > 7
        equip @a.aebow@
      :?c.canUseMask()
        c.useMask()

    :
      dontDash = false
      ?c.canUseHamm() & foe.armor > 0 & foe.distance < 11
        c.useHamm(a.myHamm)
      :?c.canUseQstaff() & foe.distance > 6 & foe.distance < 11
        c.useQstaff(a.myqstaff)
      :
        ?foe.buffs.string ! "defense_fire"
          ?!keepMask & hp = maxhp
            ?c.apsTimer < 10
              c.doAps("Vantum Phoenix", a.fsword,
              ^       "Bifrost", a.isword,
              ^       "Dothaneel", a.vsword,
              ^       "Mamba Negra", a.psword,
              ^       "Void Slayer", a.aesword)

            :
              c.doMoondial("Vantum Phoenix", a.fsword, moondialingMode)

          :
            equipL Vantum Phoenix
        :
          ?!keepMask & hp = maxhp
            c.doMoondial("Bifrost", a.isword, moondialingMode)
          :
            equipL Bifrost


  ?hp < maxhp
  ^& !c.canBash() & !c.canDash()
  ^& !keepMask
    equipL moon
    equipR @a.dLsword@


  return



// LOADOUTS

func ldtF(mode)
  ?mode = "default"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "armorRecover"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield2@
  :?mode = "escape"
    equipL mind
    equipR @a.cshield2@

    //p.mindDelay = totaltime + p.mindCd
  :?mode = "magnet"
    equipL star
    ?!keepMask
      equipR triskelion


  :?mode = "harv"
    equip shovel


  //buffs/debuffs (oriented to duration)
  :?mode = "pDeb1"
    ?keepMask
      equipL @a.dPsword@
      equipR mask
    :
      equipL moon
      equipR @a.dPsword@
  :?mode = "pDeb2"
    ?keepMask
      equipL Death's Kiss
      equipR mask
    :
      equipL moon
      equipR Death's Kiss
  :?mode = "aidActive"
    equipL @a.ahHammer@
    equipR @a.ahShield@
  :?mode = "pDebActive" // idk what to name it lol
    equipL @a.dPsword@
    equipR @a.apShield@

  :?mode = "fDeb"
    equipL @a.dFsword@
    equipR mask
  :?mode = "iDeb"
    equipL @a.dIsword@
    equipR mask

  : // print error message on screen
    i.showStrCentered(
    ^"ERROR: ldtF() recieved incorrect mode: " + mode,
    ^0, 30, "yellow")


  return 


// PROCEDURE

disable npcDialog

var choice = false
var btnPressed = false

var yesBtn
var noBtn

var dontShowAgain = false



?!btnPressed
  ?!yesBtn
    yesBtn = uimkr.mkBtn(
    ^-5,9,null,2,null,null, // x, y, w, h, anchor, dock,
    ^"Yes","#green", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound

  ?!noBtn
    noBtn = uimkr.mkBtn(
    ^5,9,8,2,null,null, // x, y, w, h, anchor, dock,
    ^"No","red", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound


  i.showStrCtr(6,30,
  ^"Harvest boulders?","#yellow",
  ^false)

:
  ?yesBtn
    yesBtn.Recycle()
    yesBtn = null
  ?noBtn
    noBtn.Recycle()
    noBtn = null


  ?!dontShowAgain
    var aux = totaltime + 3*u.sec
    ?totaltime < aux
      ?choice
        i.showStrCtr(6, 30,
        ^"Harvest boulders is enabled","#green",
        ^false)
      :
        i.showStrCtr(6, 30,
        ^"Harvest boulders is disabled","#red",
        ^false)
    :
      ?!dontShowAgain
        dontShowAgain = true



?item.right.state ! a.apShield &
^item.left.state ! a.ahHammer & item.right.state ! a.ahShield
  c.doAAC("sword 0*", a.cshield1)

?debuffs.string
  loc.Leave()

?loc.begin // AUTO BREWING

  ?res.bronze >= 10
    ?res.stone >= 10
      ?item.potion ! "lucky"
        brew stone + bronze

      moondialingMode = 0

    :?res.wood >= 10
      ?item.potion ! "berserk"
        brew wood + bronze

      moondialingMode = 2
    :
      loc.Leave()
  :
    loc.Leave()

?keepMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer"
    equipR mask

?loc.begin | loc.loop
  /* ldtF("default") but w/o considering keepMask*/
  equipL triskelion
  equipR @a.cshield1@

:?c.canUseFTali()
  equipL triskelion
  c.useFTali("r")

:?c.canUsePotDmg() & keepMask
  activate potion

:?choice & harvest.distance < 5
  ldtF("harv")

:?pickup.distance < 15
  ldtF("magnet")

  ?screen.i ! 3 & c.canUseQstaff()
    c.useQstaff(a.myqstaff)

:
  ?foe = "dysangelos" & foe.state = 108
  ^& dysanToPhase3Timer = -1 & !keepMask
    dysanToPhase3Timer = 0

  ?dysanToPhase3Timer ! -1 & dysanToPhase3Timer < 90
    dysanToPhase3Timer++


  ?c.canUseQstaff() & screen.i < 3 & foe.distance > 15
    c.useQstaff(a.myqstaff)

  :?foe = "dysangelos" & foe.distance <= 22
  ^& c.canUseBard2() &
  ^(foe.state = 100 | foe.state = 101 |
  ^foe.state = 102 | foe.state = 126) // Dysan's pre-dialog strat
    ?c.canUseHamm2()
      c.useHamm(a.myHamm)
    :?c.canUseQstaff()
      c.useQstaff(a.myqstaff)
    :?c.canDash()
      c.dash("Vantum Phoenix", a.dshield)
    :
      buffs.string = buffs.string
      ?buffs.string = "berserk"
        c.useBard("Kubikiribocho")
      :
        c.useBard(a.fastBard)

  :?foe = "dysangelos" & foe.state = 127 & !restAps & !keepMask // "reloading" aps strat
    ?c.apsTimer >= 10
      c.apsTimer = 0

    : // ?c.apsTimer < 10
      c.doAps("Vantum Phoenix", a.fsword,
      ^       "Bifrost", a.isword,
      ^       "Dothaneel", a.vsword,
      ^       "Mamba Negra", a.psword,
      ^       "Void Slayer", a.aesword)

      ?c.apsTimer = 9
        restAps = true

  :?c.canUseQstaff() & screen.i = 4 &
  ^foe.distance > 15 & foe.distance <= 18 & ai.enabled
    c.useQstaff(a.myqstaff)

  :?dysanToPhase3Timer >= 0 & !dysanToPhase3Done // Dysan's phase3 transformation cutscene strat

    ?c.canUseQstaff()
      c.useQstaff(a.myqstaff)

    :?c.canBash()
      c.bash(a.dLsword)
    :?c.canDash()
      c.dash(a.dLsword, a.dshield)

    :?c.canUseBard()
      c.useBard("Kubikiribocho")
    :?c.canUseHamm()
      c.useHamm(a.myHamm)
    :
      dysanToPhase3Done = true



  :?foe.distance > 15 | !ai.enabled |
  ^(foe.state = 124 | foe.state = -1 |
  ^(foe.state = 107 & foe.damage = 3)) // Dysan's flying animation (after putting shield on phase3)
  ^| (foe = "dysangelos" & foe.state = 4)
  ^| foe.state = 116

    ?14 <= armor & armor <= a.cMaxArmor
      ldtF("default")
    :
      ldtF("armorRecover")

  :?ai.enabled & foe.state ! -1
    // fighting range

    ?foe = "acronian_scout"

      ?c.canBash()
        c.bash("Vantum Phoenix")

      :?foe.state = 32 & foe.time > 40
        ldtF("pDebActive")

      :?keepMask
        ?foe.debuffs.string ! "chill:6"
          ldtF("iDeb")
        :?foe.debuffs.string ! "damage"
          ldtF("pDeb1") // 2
        :?foe.debuffs.string ! "dot"
          ldtF("fDeb")
        :
          equipL Vantum Phoenix

      :?hp ! maxhp
        c.doMoondial(a.dLsword, "Vantum Phoenix", moondialingMode)

      :?c.apsTimer < 10
        c.doAps("Vantum Phoenix", a.fsword,
        ^       "Bifrost", a.isword,
        ^       "Dothaneel", a.vsword,
        ^       "Mamba Negra", a.psword,
        ^       "Void Slayer", a.aesword)
      :
        c.doMoondial("Vantum Phoenix", a.fsword, moondialingMode)



    :?foe = "dysangelos" &
    ^foe.state ! 124 &
    ^!(foe.state = 107 & foe.damage = 3)

      trueDmg = foe.damage
      ?foe.debuffs.string = "damage"
        ?foe = "phase3"
          trueDmg -= 4 // a.boostedPdeb
        :
          trueDmg -= 4

      ?foe.debuffs.string = "feeble"
        trueDmg -= 2 * i.getDebuffLvl("feeble", true)

      ?resetApsTimer
        c.apsTimer = 0
        resetApsTimer = false

      ?c.canBash() & !dontDash
        c.bash("Vantum Phoenix")
      :?c.canDash() & !dontDash
        c.dash("Vantum Phoenix", a.dshield)


      :?foe.state = 32 & (
      ^(foe = "phase1" &
      ^  ((foe.debuffs.string = "chill" & foe.time > 58) // about to attack, slowed down by chill debuff
      ^ | (foe.debuffs.string ! "chill" & 23 < foe.time & foe.time < 30)      )) | // same, but w/o chill (sometimes it runs out right when attacking)
      ^(foe = "phase3" &
      ^  ((foe.debuffs.string = "chill" & foe.time > 80) // final phase, about to normal attack
      ^ | (foe.debuffs.string ! "chill" & 48 < foe.time & foe.time < 55)      )) ) // same, but w/o chill

        ?armor < trueDmg
          ldtF("aidActive")
        :
          ldtF("pDebActive")


      :?foe ! "immune_to_debuff_damage"
      ^& foe.buffs.string ! "buff_protection"
      ^& loc.stars > 5
        ?keepMask
          ?foe.debuffs.string ! "chill:6"
            ldtF("iDeb")

          :?foe.debuffs.string ! "damage"
            ?foe = "phase3"
              ldtF("pDeb1") // 2
            :
              ldtF("pDeb1") // 2

          :?foe.debuffs.string ! "dot"
            ldtF("fDeb")
          :
            bossFight()
        :
          ?foe.debuffs.string ! "damage"
            ?foe = "phase3"
              ldtF("pDeb1") // 2
            :
              ldtF("pDeb1")
          :
            bossFight()
      :
        bossFight()



// *******************************************************
//                    END OF "1-Rocky"                    
// *******************************************************