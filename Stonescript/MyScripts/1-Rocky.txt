// *******************************************************
//                   START OF "1-Rocky"                   
// *******************************************************

// Script for Rocky Plateau



// /////////////////////////////////////////// //

// IMPORTS

var uimkr = new MyScripts/Lib/UI_Maker
var u = new MyScripts/Lib/Utilities
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat
c.Init()



// /////////////////////////////////////////// //

// VARIABLES

var canDash
var trueDmg

var nRuns = 0

var keepCultistMask = true
var moondialMode = -1
var smiteScreen = null

var resetBurstingTimer = true
var restBursting = false

?loc.loop
  keepCultistMask = false

  resetBurstingTimer = true
  restBursting = false

  ?totaltime = 1
    nRuns++

?buffs.string = "berserk"
  moondialMode = 2
:
  moondialMode = 0



// /////////////////////////////////////////// //

// FUNCTIONS

func harvFunc(btn)
  btnPressed = true
  ?btn = yesBtn
    choice = true
  : // btn = noBtn
    choice = false

  return


func ldtF(mode)

  ?mode = "default"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound2@
  :?mode = "engage"
    ?buffs.string ! "buff_inf_speed" & // infernal staff
    ^totaltime % 2 = 0 // every even frame
      equipL @a.hammerFireDefense@
    :
      equipL triskelion

    ?!keepCultistMask
      equipR @a.shieldFireDefense@

  :?mode = "bash"
    equipL Vantum Phoenix
    equipR bashing shield
  :?mode = "dash"
    equipL Vantum Phoenix
    equipR @a.shieldDashing@

  :?mode = "escape"
    equipL mind
    ?!keepCultistMask
      equipR @a.shieldFireDefense@
  :?mode = "magnet"
    equipL star
    ?!keepCultistMask
      equipR triskelion
  :?mode = "harvest"
    equip shovel


  :?mode = "weakenLess" // normal weaken
    ?keepCultistMask
      equipL @a.swordPoisonDebuff@
    :
      equipL moon
      equipR @a.swordPoisonDebuff@
  :?mode = "weakenMore" // boosted weaken
    ?keepCultistMask
      equipL "Death's Kiss"
    :
      equipL moon
      equipR "Death's Kiss"

  :?mode = "aidActive" // heal when hit
    equipL @a.hammerVigorBuff@
    equipR @a.shieldVigorBuff@
  :?mode = "empowerActive" // weaken when hit
    equipL @a.swordPoisonDebuff@
    equipR @a.shieldPoisonBuff@


  :?mode = "meleeDmg"
    ?keepCultistMask
      ?mode = "meleeDmgPoison"
        equipL Mamba Negra
      :?mode = "meleeDmgVigor"
        equipL Dothaneel
      :?mode = "meleeDmgAether"
        equipL Void Slayer
      :?mode = "meleeDmgFire"
        equipL Vantum Phoenix
      :?mode = "meleeDmgIce"
        equipL Bifrost
    :
      ?mode = "meleeDmgPoison"
        c.doMoondial(Mamba Negra, a.swordPoisonDmg, moondialMode)
      :?mode = "meleeDmgVigor"
        c.doMoondial(Dothaneel, a.swordVigorDmg, moondialMode)
      :?mode = "meleeDmgAether"
        c.doMoondial(Void Slayer, a.swordAetherDmg, moondialMode)
      :?mode = "meleeDmgFire"
        c.doMoondial(Vantum Phoenix, a.swordFireDmg, moondialMode)
      :?mode = "meleeDmgIce"
        c.doMoondial(Bifrost, a.swordIceDmg, moondialMode)


  :?mode = "meleeDebuff"
    ?keepCultistMask
      ?c.canDebuffChill()
        equipL @a.swordIceDebuff@
      :?c.canDebuffWeaken()
        equipL @a.swordPoisonDebuff@
      :?c.canDebuffBurn()
        equipL @a.swordFireDebuff@
    :
      //Left hand:
      ?c.canDebuffWeaken()
        equipL @a.swordPoisonDebuff@
      :?c.canDebuffBurn()
        equipL @a.swordFireDebuff@
      :?c.canDebuffChill()
        equipL @a.wandIceDebuff@

      //Right hand:
      ?c.canDebuffChill()
        equipR @a.swordIceDebuff@
  :?mode = "meleeHeal"
    ?keepCultistMask
      equipL Soulfetcher
    :
      c.doMoondial(Vantum Phoenix, Soulfetcher, moondialMode)

  :?mode = "rangedAOEUnmake"
    equip @a.staffAetherDebuff@
    /* ?keepCultistMask
      equipL @a.bigSwordAetherDebuff@
    : */
  :?mode = "meleeUnmake"
    equip @a.staffAetherDebuff@
    /* ?keepCultistMask
      equipL @a.swordAetherDebuff@
    :
      // put a 2nd dU sword when we have it
      c.doMoondial(a.swordAetherDebuff, Vantum Phoenix, moondialMode)*/

  :?mode = "rangedAOEDmg"
    equipL @a.wandFireDmg@
    ?!keepCultistMask
      equipR @a.staffFireDebuff@ // @a.bowFireDmg@
  :?mode = "rangedAOEDebuff"
    ?keepCultistMask
      ?c.canDebuffChill()
        equipL @a.wandIceDebuff@
      :?c.canDebuffWeaken()
        equipL @a.wandPoisonDebuff@
      :?c.canDebuffBurn()
        equipL @a.wandFireDebuff@

    :
      ?c.canDebuffChill()
        equip @a.staffIceDebuff@
      :?c.canDebuffWeaken()
        equip @a.staffPoisonDebuff@
      :?c.canDebuffBurn()
        equip @a.staffFireDebuff@
  :?mode = "rangedAOEHeal"
    ?keepCultistMask
      equipL @a.wandVigorBuff@
    :
      equipL moon
      equipR @a.wandVigorBuff@
      // equip @a.staffVigorBuff@ // @a.bowVigorBuff@

  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight(mode)
  ?mode ! "boss"
    ?c.canDebuffAny() & // foe can be debuffed
    ^encounter.eliteMod ! "tenacious" // foe isn't immune to debuffs
      ?mode = "melee"
        ldtF("meleeDebuff")
      :?mode = "rangedAOE"
        ldtF("rangedAOEDebuff")

    : // all possible debuffs applied
      ?hp ! maxhp // heal up
        ?mode = "melee"
          ldtF("meleeHeal")
        :?mode = "rangedAOE"
          ldtF("rangedAOEHeal")

      : // deal max damage
        ?mode = "melee"
          ldtF("meleeDmgFire")
        :?mode = "rangedAOE"
          ldtF("rangedAOEDmg")
  
  : // mode = "boss"
    ?foe = "phase1"
      fight("melee")

    :?foe = "phase2"
      ?hp ! maxhp
        ldtF("meleeHeal")

      :?c.canDebuffAny() & foe.buffs.string ! "buff_protection"
        ldtF("meleeDebuff")

      :?foe = "poison"
        ldtF("meleeDmgIce")
      :?foe = "vigor"
        ?foe.buffs.string = "buff_protection" & // against debuffs
        ^c.canUseWandHidden("reset")
          c.useWandHidden(a.wandVigorHidden, "reset", "l")
        :
          ldtF("meleeDmgPoison")
      :?foe = "aether"
        ldtF("meleeDmgVigor")
      :?foe = "fire"
        ldtF("meleeDmgAether")
      :?foe = "ice"
        ldtF("meleeDmgFire")

    : // ?foe = phase3
      ?c.canUsePotionDmg()
        activate potion
      :?c.canUseCultistMask()
        c.useCultistMask()
      :?c.canUseStaffHidden("berserker")
        c.useStaffHidden(a.staffPoisonHidden, "berserker")

      ?c.canUseCinderwisp() &
      ^foe.debuffs.string = a.maxIgnitions_Default
        c.useCinderwisp()

      ?((foe.state = 115 & foe.time < 70) |
      ^(foe.state = 32 & foe.damage = 1)) &
      ^foe.time >= 42 // putting up shield
        >c0,0,SHIELD
        canDash = true

        ?c.canUseMindstone()
          ldtF("escape")
        :?c.canUseStaffHidden("acrobatic")
          c.useStaffHidden(a.staffStoneHidden, "acrobatic")
        :?foe.distance > 7
          equip @a.bowAetherDmg@
        :?c.canUseCultistMask()
          c.useCultistMask()
        :
          fight("melee")

      :
        >c0,0,NOT SHIELD
        canDash = false

        ?hp ! maxhp
          ldtF("meleeHeal")
        :?c.canDebuffAny()
          ldtF("meleeDebuff")

        :?c.canUseHeavyHammer() & foe.armor
          c.useHeavyHammer(Mjölnir)
        :
          ?foe.buffs.string ! "defense_fire"
            ?c.burstingTimer < 10 & !keepCultistMask
              c.doBursting("Vantum Phoenix"  , a.swordFireDmg,
              ^            "Bifrost"         , a.swordIceDmg,
              ^            "Dothaneel"       , a.swordVigorDmg,
              ^            "Mamba Negra"     , a.swordPoisonDmg,
              ^            "Void Slayer"     , a.swordAetherDmg)

            :
              ldtF("meleeDmgFire")

          :
            ldtF("meleeDmgIce")


  return



// /////////////////////////////////////////// //

// PROCEDURE

>c0,-1,#magenta,foe damage = @foe.damage@\n
^foe distance = @foe.distance@\n

disable npcDialog

var choice = false
var btnPressed = false

var yesBtn
var noBtn

var dontShowAgain = false


?!btnPressed
  ?!yesBtn
    yesBtn = uimkr.mkBtn(
    ^-5,9,null,2,null,null, // x, y, w, h, anchor, dock,
    ^"Yes","#green", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound

  ?!noBtn
    noBtn = uimkr.mkBtn(
    ^5,9,8,2,null,null, // x, y, w, h, anchor, dock,
    ^"No","red", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound


  u.showStrCtr(6,30,
  ^"Harvest boulders?","#yellow",
  ^false)
:
  ?yesBtn
    yesBtn.Recycle()
    yesBtn = null
  ?noBtn
    noBtn.Recycle()
    noBtn = null


  ?!dontShowAgain
    var aux = totaltime + 3*u.sec
    ?totaltime < aux
      ?choice
        u.showStrCtr(6, 30,
        ^"Harvest boulders is enabled","#green",
        ^false)
      :
        u.showStrCtr(6, 30,
        ^"Harvest boulders is disabled","#red",
        ^false)
    :
      ?!dontShowAgain
        dontShowAgain = true


c.doAAC(a.hammerFireDefense, a.shieldFireDefense)

// Auto Brewing
?c.canBrewPotion("lucky")
  c.brewPotion("lucky")

?loc.begin // initial equipment
  equipL triskelion
  equipR @a.shieldCompound1@
:?keepCultistMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer" &
  ^item.right ! "quarterstaff"
    equipL triskelion
    equipR mask


/*?c.canUseBFG_DS(smiteScreen)
  c.useBFG_DS()*/


?foe ! "boss" &
^foe.state ! 4 & foe.state ! -1 &
^c.canUseAetherTalisman()
  ?keepCultistMask
    c.useAetherTalisman("l")
  :
    equipL triskelion
    c.useAetherTalisman("r")
:?(foe = "boss" & loc.stars <= 15) |
^c.canUseFireTalisman()
  ?keepCultistMask
    c.useFireTalisman("l")
  :
    equipL triskelion
    c.useFireTalisman("r")


:?c.canUsePotionDmg() & keepCultistMask
  activate potion
:?c.canUseStaffHidden("infernal") // speed up
  c.useStaffHidden(a.staffFireHidden, "infernal")


:?choice & harvest.distance < 5
  ldtF("harvest")
:?pickup.distance < 15
  ldtF("magnet")

:?ai.walking &
^(foe.distance < 11 | foe.distance > 15) &
^c.canUseQuarterstaff()
  c.useQuarterstaff(a.myQuarterstaff)

:?ai.paused |
^(ai.idle & foe.distance > c.getRange("sword")) | // if we're waiting
^foe.state = -1 | foe.state = 4 | // foe death states
^foe.state = 124 | foe.state = 125 | // dysangelos phase 1 to 2 states
//^(foe.state = 107 & foe.damage = 1) | // dsangelos' flying animation
^foe.state = 108 | // dysangelos phase 2 to 3 states
^foe.state = 116 | // dysangelos phase 3 death animation state
^foe.distance > u.dash_range_max // or we can't engage in a fight
  ?!ai.walking & foe = "dysangelos" &
  ^(c.canUseHeavyHammer() | c.canUseBardiche() |
  ^(foe.distance <= u.bardiche_ability_range + u.grasping_buff
  ^& c.canUseStaffHidden("grasping")))
    ?c.canUseHeavyHammer()
      c.useHeavyHammer(Mjölnir)
    
    :?c.canUseStaffHidden("grasping")
      c.useStaffHidden(a.staffAetherHidden, "grasping")
    :?c.canUseBardiche()
      c.useBardiche(Kubikiribocho)

  :?armor > 29 |
  ^(foe ! "phase" & foe.distance < 40)
    ldtF("engage")
  :?14 <= armor & armor <= 29
    ldtF("default")
  :
    ldtF("armorRegen")


// normal foes
:?encounter.eliteMod = "monarch"
  ?foe.count > 1 // eliminate the other foes
    ?c.canUseBFG()
      c.useBFG()
    :?c.canUseWandHidden("explosive")
      c.useWandHidden(a.wandFireHidden, "explosive", "l")

    :?foe.distance < 7 & // if too close, try to escape
    ^(c.canUseMindstone() |
    ^c.canUseStaffHidden("acrobatic"))
      ?c.canUseMindstone()
        ldtF("escape")
      :
        c.useStaffHidden(a.staffStoneHidden, "acrobatic")

    :?c.canUseHeavyHammer() // if close, use hammer
      c.useHeavyHammer(Mjölnir)
    :
      fight("rangedAOE")

  :?c.canUseBashingShield()
    ldtF("bash")
  :?c.canUseDashingShield()
    ldtF("dash")

  : // 1v1 with the monarch
    ?foe.hp + foe.armor >= a.bardicheRDmg &
    ^c.canUseBardiche()
      c.useBardiche(Kubikiribocho)
    :
      fight("melee")


:
  ?foe = "dysangelos" & foe.state = 127 & !restBursting & !keepCultistMask // "reloading" bursting strat
    ?c.burstingTimer >= 10
      c.burstingTimer = 0
    : // c.burstingTimer < 10
      c.doBursting("Vantum Phoenix"  , a.swordFireDmg,
      ^            "Bifrost"         , a.swordIceDmg,
      ^            "Dothaneel"       , a.swordVigorDmg,
      ^            "Mamba Negra"     , a.swordPoisonDmg,
      ^            "Void Slayer"     , a.swordAetherDmg)

      ?c.burstingTimer = 9
        restBursting = true


  : // fighting range
    ?foe = "acronian_scout"
      ?c.canUseBashingShield()
        ldtF("bash")
      :?c.canUseDashingShield()
        ldtF("dash")

      :?foe.state = 32 & foe.time > 40 // about to attack
        ?!keepCultistMask
          ldtF("empowerActive")
        :?loc.stars > 15 & c.canUseStaffHidden("eternity")
          c.useStaffHidden(a.staffIceHidden, "eternity")
        :
          fight("melee")

      :?c.burstingTimer < 10 & !keepCultistMask
        c.doBursting("Vantum Phoenix"  , a.swordFireDmg,
        ^            "Bifrost"         , a.swordIceDmg,
        ^            "Dothaneel"       , a.swordVigorDmg,
        ^            "Mamba Negra"     , a.swordPoisonDmg,
        ^            "Void Slayer"     , a.swordAetherDmg)

      :
        fight("melee")

    :?foe = "dysangelos"

      trueDmg = foe.damage
      ?foe.debuffs.string = "damage"
        ?foe = "phase3"
          trueDmg -= 4 // a.boostedWeaken
        :
          trueDmg -= 4

      ?foe.debuffs.string = "feeble"
        trueDmg -= 2 * i.getDebuffLvl("feeble", true)

      ?resetBurstingTimer
        c.burstingTimer = 0
        resetBurstingTimer = false

      ?c.canUseBashingShield() & !canDash
        ldtF("bash")
      :?c.canUseDashingShield() & !canDash
        ldtF("dash")

      :?foe.state = 32 & (
      ^(foe = "phase1" &
      ^  ((foe.debuffs.string = "chill" & foe.time > 58) // about to attack, slowed down by chill debuff
      ^ | (foe.debuffs.string ! "chill" & 23 < foe.time & foe.time < 30)      )) | // same, but w/o chill (sometimes it runs out right when attacking)
      ^(foe = "phase3" &
      ^  ((foe.debuffs.string = "chill" & foe.time > 80) // final phase, about to normal attack
      ^ | (foe.debuffs.string ! "chill" & 48 < foe.time & foe.time < 55)      )) ) // same, but w/o chill

        ?!keepCultistMask
          ?armor < trueDmg
            ldtF("aidActive")
          :
            ldtF("empowerActive")


      :
        fight("boss")



// *******************************************************
//                    END OF "1-Rocky"                    
// *******************************************************