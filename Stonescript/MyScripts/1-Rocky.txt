// *******************************************************
//                   START OF "1-Rocky"                   
// *******************************************************

// Script for Rocky Plateau



// IMPORTS

var uimkr = new MyScripts/Lib/UI_Maker
var u = new MyScripts/Lib/Utilities
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo

var c = new MyScripts/Lib/Combat
c.Init()

/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter

var somPanel = som.mkUI(0, 1, u.colorToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, u.colorToHex("red"), true, false)

/***************************************************************/

var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED

dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/

import UI/FoeStateTracker


// VARIABLES

var dontDash
var trueDmg

var nRuns = 0

var dysanToPhase3Done = false
var dysanToPhase3Timer = -1

var keepCultistMask = true

var resetApsTimer = true
var restAps = false

?loc.loop
  dysanToPhase3Timer = -1
  dysanToPhase3Done = false

  keepCultistMask = false

  resetApsTimer = true
  restAps = false

  ?totaltime = 1
    nRuns++

var moondialMode = -1

?buffs.string = "buff_inf_speed" |
^item.potion = "berserk"
  moondialMode = 2
:
  moondialMode = 0

// FUNCTIONS

func harvFunc(btn)
  btnPressed = true
  ?btn = yesBtn
    choice = true
  : // btn = noBtn
    choice = false

  return

func bossFight()

  ?foe = "phase1"
    ?!keepCultistMask & hp = maxhp
      c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)
    :
      equipL Vantum Phoenix

  :?foe = "phase2"
    ?foe = "poison"
      ?!keepCultistMask & hp = maxhp
        c.doMoondial("Bifrost", a.swordIceDmg, moondialMode)
      :
        equipL Bifrost
    :?foe = "vigor"
      ?!keepCultistMask & hp = maxhp
        c.doMoondial("Mamba Negra", a.swordPoisonDmg, moondialMode)
      :
        equipL Mamba Negra
    :?foe = "aether"
      ?!keepCultistMask & hp = maxhp
        c.doMoondial("Dothaneel", a.swordVigorDmg, moondialMode)
      :
        equipL Dothaneel
    :?foe = "fire"
      ?!keepCultistMask & hp = maxhp
        c.doMoondial("Void Slayer", a.swordAetherDmg, moondialMode)
      :
        equipL Void Slayer
    :?foe = "ice"
      ?!keepCultistMask & hp = maxhp
        c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)
      :
        equipL Vantum Phoenix

  : // ?foe = phase3
    ?c.canUsePotionDmg()
      activate potion
    :?c.canUseCultistMask()
      c.useCultistMask()
    :?c.canUseStaffBerserker()
      equip @a.staffPoisonHidden@
      activate R

    ?c.canUseCinderwisp() & dpsm.DPS
      ?foe.debuffs.string = a.maxIgnitions_Default |
      ^c.canKillCinderwisp(i.getFoeTotalHP(),
      ^              i.getDebuffLvl("ignition", true),
      ^              a.cinderwispDmg_Default, dpsm.DPS + a.cinderwispDmg_Default)
        c.useCinderwisp()

    :?c.canUseCinderwisp() & !dpsm.DPS
      ?foe.debuffs.string = a.maxIgnitions_Default
        c.useCinderwisp()

    ?((foe.state = 115 & foe.time < 70) |
    ^(foe.state = 32 & foe.damage = 1))
    ^& foe.time >= 42 // putting up shield
      dontDash = true

      ?c.canUseMindstone()
        ldtF("escape")
      :?foe.distance > 7
        equip @a.bowAetherDmg@
      :?c.canUseCultistMask()
        c.useCultistMask()

    :
      dontDash = false
      ?c.canUseHeavyHammer() & foe.armor > 0 & foe.distance < 11
        c.useHeavyHammer(a.myHeavyHammer)
      :?c.canUseQuarterstaff() & foe.distance > 6 & foe.distance < 11
        c.useQuarterstaff(a.myQuarterstaff)
      :
        ?foe.buffs.string ! "defense_fire"
          ?!keepCultistMask & hp = maxhp
            ?c.apsTimer < 10
              c.doAps("Vantum Phoenix", a.swordFireDmg,
              ^       "Bifrost", a.swordIceDmg,
              ^       "Dothaneel", a.swordVigorDmg,
              ^       "Mamba Negra", a.swordPoisonDmg,
              ^       "Void Slayer", a.swordAetherDmg)

            :
              c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)

          :
            equipL Vantum Phoenix
        :
          ?!keepCultistMask & hp = maxhp
            c.doMoondial("Bifrost", a.swordIceDmg, moondialMode)
          :
            equipL Bifrost


  ?hp < maxhp
  ^& !c.canUseBashingShield() & !c.canUseDashingShield()
  ^& !keepCultistMask
    equipL moon
    equipR @a.swordVigorBuff@


  return



// LOADOUTS

func ldtF(mode)
  ?mode = "default"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound1@
  :?mode = "armorRecover"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound2@
  :?mode = "escape"
    equipL mind
    equipR @a.shieldCompound2@

  :?mode = "magnet"
    equipL star
    ?!keepCultistMask
      equipR triskelion
    :
      equipR mask


  :?mode = "harv"
    equip shovel


  //buffs/debuffs (oriented to duration)
  :?mode = "pDeb1"
    ?keepCultistMask
      equipL @a.swordPoisonDebuff@
      equipR mask
    :
      equipL moon
      equipR @a.swordPoisonDebuff@
  :?mode = "pDeb2"
    ?keepCultistMask
      equipL "Death's Kiss"
      equipR mask
    :
      equipL moon
      equipR "Death's Kiss"
  :?mode = "aidActive"
    equipL @a.hammerVigorBuff@
    equipR @a.shieldVigorBuff@
  :?mode = "pDebActive" // idk what to name it lol
    equipL @a.swordPoisonDebuff@
    equipR @a.shieldPoisonBuff@

  :?mode = "fDeb"
    equipL @a.swordFireDebuff@
    equipR mask
  :?mode = "iDeb"
    equipL @a.swordIceDebuff@
    equipR mask

  : // print error message on screen
    i.showStrCentered(
    ^"ERROR: ldtF() recieved incorrect mode: " + mode,
    ^0, 30, "yellow")


  return 


// PROCEDURE

disable npcDialog

var choice = false
var btnPressed = false

var yesBtn
var noBtn

var dontShowAgain = false



?!btnPressed
  ?!yesBtn
    yesBtn = uimkr.mkBtn(
    ^-5,9,null,2,null,null, // x, y, w, h, anchor, dock,
    ^"Yes","#green", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound

  ?!noBtn
    noBtn = uimkr.mkBtn(
    ^5,9,8,2,null,null, // x, y, w, h, anchor, dock,
    ^"No","red", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound


  u.showStrCtr(6,30,
  ^"Harvest boulders?","#yellow",
  ^false)

:
  ?yesBtn
    yesBtn.Recycle()
    yesBtn = null
  ?noBtn
    noBtn.Recycle()
    noBtn = null


  ?!dontShowAgain
    var aux = totaltime + 3*u.sec
    ?totaltime < aux
      ?choice
        u.showStrCtr(6, 30,
        ^"Harvest boulders is enabled","#green",
        ^false)
      :
        u.showStrCtr(6, 30,
        ^"Harvest boulders is disabled","#red",
        ^false)
    :
      ?!dontShowAgain
        dontShowAgain = true



c.doAAC("sword 0*", a.shieldCompound1)

?debuffs.string
  loc.Leave()

?loc.begin
  // initial equipment
  equipL triskelion
  equipR @a.shieldCompound1@

  // ///////////////////////////// //
  // AUTO BREWING
  ?res.bronze >= 10
    ?res.stone >= 10
      ?item.potion ! "lucky"
        brew stone + bronze

    :?res.wood >= 10
      ?item.potion ! "berserk"
        brew wood + bronze

    :
      loc.Leave()
  :
    loc.Leave()
  // ///////////////////////////// //

:?keepCultistMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer" &
  ^item.right ! "quarterstaff"
    equipR mask


?c.canUsePotionDmg() & keepCultistMask
  activate potion

// speed up
:?c.canUseStaffInfernal() &
^screen.i ! 4 & foe.state ! -1
  equip @a.staffFireHidden@
  activate R

?c.canUseFireTalisman()
  equipL triskelion
  c.useFireTalisman("r")

:?choice & harvest.distance < 5
  ldtF("harv")

:?pickup.distance < 15
  ldtF("magnet")

:?screen.i < 3 & foe.distance > 15 &
^screen.i > 3 & foe.distance > 15 &
^c.canUseQuarterstaff()
  >c0,0,#red,using qstaff
  loc.pause()
  c.useQuarterstaff(a.myQuarterstaff)

:?ai.paused |
^(ai.idle & foe.distance > 11) | // if we're waiting
^foe.state = -1 | foe.state = 4 | // foe death states
^foe.state = 124 | foe.state = 125 | // dysangelos phase 1 to 2 states
^(foe.state = 107 & foe.damage = 3) | // dsangelos' flying animation
^foe.state = 108 | // dysangelos phase 2 to 3 states
^foe.state = 116 | // dysangelos phase 3 death animation state
^foe.distance > 15 // or we can't engage in a fight
  ?14 <= armor & armor <= a.compoundMaxArmor
    ldtF("default")
  :
    ldtF("armorRecover")


:
  ?foe = "dysangelos" & foe.state = 108
  ^& dysanToPhase3Timer = -1 & !keepCultistMask
    dysanToPhase3Timer = 0

  ?dysanToPhase3Timer ! -1 & dysanToPhase3Timer < 90
    dysanToPhase3Timer++


  ?foe = "dysangelos" & foe.distance <= 22
  ^& c.canUseBardiche() &
  ^(foe.state = 100 | foe.state = 101 |
  ^foe.state = 102 | foe.state = 126) // Dysan's pre-dialog strat
    ?c.canUseHeavyHammer()
      c.useHeavyHammer(a.myHeavyHammer)
    :?c.canUseQuarterstaff()
      c.useQuarterstaff(a.myQuarterstaff)
    :?c.canUseDashingShield()
      c.useDashingShield("Vantum Phoenix", a.shieldDashing)
    :
      ?buffs.string = "berserk"
        c.useBardiche("Kubikiribocho")
      :
        c.useBardiche(a.fastBardiche)

  :?foe = "dysangelos" & foe.state = 127 & !restAps & !keepCultistMask // "reloading" aps strat
    ?c.apsTimer >= 10
      c.apsTimer = 0

    : // c.apsTimer < 10
      c.doAps("Vantum Phoenix", a.swordFireDmg,
      ^       "Bifrost", a.swordIceDmg,
      ^       "Dothaneel", a.swordVigorDmg,
      ^       "Mamba Negra", a.swordPoisonDmg,
      ^       "Void Slayer", a.swordAetherDmg)

      ?c.apsTimer = 9
        restAps = true

  
    ?dysanToPhase3Timer >= 0 & !dysanToPhase3Done // Dysan's phase3 transformation cutscene strat

    ?c.canUseQuarterstaff()
      c.useQuarterstaff(a.myQuarterstaff)

    :?c.canUseBashingShield()
      c.useBashingShield(a.swordVigorBuff)
    :?c.canUseDashingShield()
      c.useDashingShield(a.swordVigorBuff, a.shieldDashing)

    :?c.canUseBardiche()
      c.useBardiche("Kubikiribocho")
    :?c.canUseHeavyHammer()
      c.useHeavyHammer(a.myHeavyHammer)
    :
      dysanToPhase3Done = true


  :?ai.enabled // & foe.state ! -1

    // fighting range
    ?foe = "acronian_scout"

      ?c.canUseBashingShield()
        c.useBashingShield("Vantum Phoenix")

      :?foe.state = 32 & foe.time > 40 & !keepCultistMask
        ldtF("pDebActive")

      :?keepCultistMask
        ?foe.debuffs.string ! "chill:6"
          ldtF("iDeb")
        :?foe.debuffs.string ! "damage"
          ldtF("pDeb1") // 2
        :?foe.debuffs.string ! "dot"
          ldtF("fDeb")
        :
          equipL Vantum Phoenix

      :?hp ! maxhp
        c.doMoondial(a.swordVigorBuff, "Vantum Phoenix", moondialMode)

      :?c.apsTimer < 10
        c.doAps("Vantum Phoenix", a.swordFireDmg,
        ^       "Bifrost", a.swordIceDmg,
        ^       "Dothaneel", a.swordVigorDmg,
        ^       "Mamba Negra", a.swordPoisonDmg,
        ^       "Void Slayer", a.swordAetherDmg)
      :
        c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)



    :?foe = "dysangelos" &
    ^foe.state ! 124 &
    ^!(foe.state = 107 & foe.damage = 3)

      trueDmg = foe.damage
      ?foe.debuffs.string = "damage"
        ?foe = "phase3"
          trueDmg -= 4 // a.boostedPdeb
        :
          trueDmg -= 4

      ?foe.debuffs.string = "feeble"
        trueDmg -= 2 * i.getDebuffLvl("feeble", true)

      ?resetApsTimer
        c.apsTimer = 0
        resetApsTimer = false

      ?c.canUseBashingShield() & !dontDash
        c.useBashingShield("Vantum Phoenix")
      :?c.canUseDashingShield() & !dontDash
        c.useDashingShield("Vantum Phoenix", a.shieldDashing)


      :?foe.state = 32 & (
      ^(foe = "phase1" &
      ^  ((foe.debuffs.string = "chill" & foe.time > 58) // about to attack, slowed down by chill debuff
      ^ | (foe.debuffs.string ! "chill" & 23 < foe.time & foe.time < 30)      )) | // same, but w/o chill (sometimes it runs out right when attacking)
      ^(foe = "phase3" &
      ^  ((foe.debuffs.string = "chill" & foe.time > 80) // final phase, about to normal attack
      ^ | (foe.debuffs.string ! "chill" & 48 < foe.time & foe.time < 55)      )) ) // same, but w/o chill

        ?!keepCultistMask
          ?armor < trueDmg
            ldtF("aidActive")
          :
            ldtF("pDebActive")


      :?foe ! "immune_to_debuff_damage"
      ^& foe.buffs.string ! "buff_protection"
      ^& loc.stars > 5
        ?keepCultistMask
          ?foe.debuffs.string ! "chill:6"
            ldtF("iDeb")

          :?foe.debuffs.string ! "damage"
            ?foe = "phase3"
              ldtF("pDeb1") // 2
            :
              ldtF("pDeb1") // 2

          :?foe.debuffs.string ! "dot"
            ldtF("fDeb")
          :
            bossFight()
        :
          ?foe.debuffs.string ! "damage"
            ?foe = "phase3"
              ldtF("pDeb1") // 2
            :
              ldtF("pDeb1")
          :
            bossFight()
      :
        bossFight()



// *******************************************************
//                    END OF "1-Rocky"                    
// *******************************************************