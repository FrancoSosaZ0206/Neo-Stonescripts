// *******************************************************
//                     4-Mushroom.txt                     
// *******************************************************

// Script for Mushroom Forest:



// IMPORTS

var uimkr = new MyScripts/Lib/UI_Maker
var u = new MyScripts/Lib/Utilities
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat

/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter

var somPanel = som.mkUI(0, 1, u.colorToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, u.colorToHex("red"), true, false)

/***************************************************************/

var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED

dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/

var bossTimer
bossTimer = import MyScripts/Borrowed/BossfightTimer2

/***************************************************************/

import UI/FoeStateTracker



// VARIABLES

var keepCultistMask = true
var smiteScreen = 1

var canDebuffIce
var canDebuffPoison
var canDebuffFire

canDebuffIce =
^(foe.debuffs.string ! "chill:6" &
^foe ! "immune_to_debuff_chill")
canDebuffPoison =
^(foe.debuffs.string ! "damage" &
^foe ! "immune_to_debuff_damage")
canDebuffFire =
^(foe.debuffs.string ! "dot" &
^foe ! "immune_to_debuff_dot")


?loc.loop
  keepCultistMask = false

  ?totaltime = 1 & loc.stars > 5 &
  ^smiteScreen ! 3
    smiteScreen++



// FUNCTIONS:

func ldtF(mode)

  ?mode = "default"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound2@
  :?mode = "engage"
    ?keepCultistMask
      equipL @a.hammerPoisonDefense@
    :
      equipL triskelion
      equipR @a.shieldPoisonDefense@
  :?mode = "escape"
    equipL mind
    ?!keepCultistMask
      equipR @a.shieldPoisonDefense@
  :?mode = "magnet"
    equipL star
    ?!keepCultistMask
      equipR triskelion

  :?mode = "meleeDmg"
    equipL Mamba Negra
    ?!keepCultistMask
      equipR @a.swordPoisonDmg@

  :?mode = "meleeDebuff"
    ?keepCultistMask
      ?canDebuffIce
        equipL @a.swordIceDebuff@
      :?canDebuffPoison
        equipL @a.swordPoisonDebuff@
      :?canDebuffFire
        equipL @a.swordFireDebuff@
    :
      //Left hand:
      ?canDebuffPoison
        equipL @a.swordPoisonDebuff@
      :?canDebuffFire
        equipL @a.swordFireDebuff@
      :?canDebuffIce
        equipL @a.wandIceDebuff@

      //Right hand:
      ?canDebuffIce
        equipR @a.swordIceDebuff@


  :?mode = "rangedDmg"
    equip @a.bowPoisonDmg@

  :?mode = "magic"
    ?keepCultistMask
      ?canDebuffIce
        equipL @a.wandIceDebuff@
      :?canDebuffPoison
        equipL @a.wandPoisonDebuff@
      :?canDebuffFire
        equipL @a.wandFireDebuff@

    :
      ?canDebuffIce
        equip @a.staffIceDebuff@
      :?canDebuffPoison
        equip @a.staffPoisonDebuff@
      :?canDebuffFire
        equip @a.staffFireDebuff@

  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight1v1()
  /* How to apply debuffs:
  - apply all except the ones the foe is immune to.
  Finish when:
  - all possible debuffs have been applied. */
  ?canDebuffIce | canDebuffPoison | canDebuffFire
    ldtF("meleeDebuff")
  :
    ?keepCultistMask
      equipL Mamba Negra

      ?hp ! maxhp & !debuffs.string
        equipL @a.swordVigorBuff@
    :
      ?buffs.string = "berserk"
        c.doMoondial(Mamba Negra, a.swordPoisonDmg, 2)
      :
        c.doMoondial(Mamba Negra, a.swordPoisonDmg, 0)

      ?hp ! maxhp & !debuffs.string
        equipR @a.swordVigorBuff@

  return



// PROCEDURE:

c.doAAC("sword 0*", a.shieldCompound1)

?totaltime - bossTimer.endbos = 1 & loc = "harder Angry"
  activate voidweaver


?loc.begin
  // initial equipment
  equipL triskelion
  equipR @a.shieldCompound1@

  // ///////////////////////////// //
  // AUTO BREWING
  ?res.bronze >= 10
    ?res.wood >= 10
      ?item.potion ! "berserk"
        brew wood + bronze
    :?res.stone >= 10
      ?item.potion ! "lucky"
        brew stone + bronze
    :
      loc.Leave()
  :
    loc.Leave()
  // ///////////////////////////// //

:?keepCultistMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer" &
  ^item.right ! "quarterstaff"
    equipL triskelion
    equipR mask


?c.canUsePotionDmg() & keepCultistMask
  activate potion

:?c.canUseStaffInfernal()
  equip @a.staffFireHidden@
  activate R

?foe ! "boss" & foe.distance ! 9999 &
^c.canUseAetherTalisman()
  ?keepCultistMask
    c.useAetherTalisman("l")
  :
    equipL triskelion
    c.useAetherTalisman("r")

:?foe = "boss" & foe.distance <= 23 &
^c.canUseFireTalisman()
  ?keepCultistMask
    c.useFireTalisman("l")
  :
    equipL triskelion
    c.useFireTalisman("r")

:?pickup.distance < 15
  ldtF("magnet")

:?foe = "explode" & // Mr. Puff
^foe.distance <= 20 // wand range
  ?foe.distance < 11
    // try to back off
    ?c.canUseMindstone()
      ldtF("escape")
    :?c.canUseStaffAcrobatic()
      equip @a.staffStoneHidden@
      activate R

    
    : // else, try to avoid getting debuffed
      ?foe.hp < 100
        ?c.canUseWandFrost()
          equipL @a.wandIceHidden@
          activate L
        :?foe.debuffs.string ! "stun" &
        ^c.canUseStaffPrevention()
          equip @a.staffVigorHidden@
          activate R

      // else, just defeat it ASAP
      :?canDebuffIce | canDebuffPoison | canDebuffFire
        ldtF("meleeDebuff")
      :
        ldtF("meleeDmg")

  :?canDebuffIce | canDebuffPoison | canDebuffFire
    ldtF("magic")
  :?keepCultistMask
    equipL @a.wandPoisonDebuff@
  :
    ldtF("rangedDmg")

:?ai.walking &
^(foe.distance < 11 | foe.distance > 15 ) &
^c.canUseQuarterstaff()
  c.useQuarterstaff(a.myQuarterstaff)

:?(foe ! "phase" | foe = "phase2") &
^foe.distance > 15 & foe.distance <= 17
  ldtF("engage")

:?ai.paused |
^(ai.idle & foe.distance > 11) | // if we're waiting
^foe.state = -1 | foe.state = 4 | // foe death states
^foe.distance > 15 // or we can't engage in a fight
  ?armor > 29
    ldtF("engage")
  :?14 <= armor & armor <= 29
    ldtF("default")
  :
    ldtF("armorRegen")


// Boss fight - phase1
:?foe = "mushroom_boss" & foe = "phase1"

  ?c.canUsePotionDmg()
    activate potion

  :?foe.armor > foe.maxarmor / 2 &
  ^c.canUseHeavyHammer()
    c.useHeavyHammer(a.myHeavyHammer)

  :?foe.state = 32 & foe.time >= 75 // boss's punch attack
    ?foe.distance < 20
      ?c.canUseMindstone()
        ldtF("escape")
      :?c.canUseStaffAcrobatic()
        equip @a.staffStoneHidden@
        activate R
      :?c.canUseStaffEternity()
        equip @a.staffIceHidden@
        activate R
      :
        ldtF("meleeDmg")

    :
      ?keepCultistMask
        equipL @a.wandPoisonDebuff@
      :
        ldtF("rangedDmg")

  :?foe.armor = foe.maxarmor & // foe has full armor
  ^foe.distance > 10 // we haven't dashed
    // Dash or Bash
    ?c.canUseBashingShield()
      c.useBashingShield(Mamba Negra)
    :?c.canUseDashingShield()
      c.useDashingShield(Mamba Negra, a.shieldDashing)

  :
    fight1v1()


: // normal foes and boss's phase2
  ?c.canUseBFG_DS(smiteScreen)
    c.useBFG_DS()

  ?c.canUseBashingShield()
    c.useBashingShield(Mamba Negra)
  :?c.canUseDashingShield()
    c.useDashingShield(Mamba Negra, a.shieldDashing)

  ?foe = "snail" & foe = "boss" // The snail that's always with Mr. Puff
    ?c.canUseHeavyHammer()
      c.useHeavyHammer(a.myHeavyHammer)
    :?c.canUseBardiche()
      c.useBardiche(a.fastBardiche)

    :?canDebuffIce | canDebuffPoison | canDebuffFire
      ldtF("meleeDebuff")
    :
      ldtF("meleeDmg")

  :?foe ! "phase2"
    ?c.canUseHeavyHammer() & foe.count >= 5 &
    ^!c.canUseBFG() & keepCultistMask
      c.useHeavyHammer(a.myHeavyHammer)

    :?foe.count = 1
      fight1v1()
    :        
      ?canDebuffIce | canDebuffPoison | canDebuffFire
        ldtF("magic")
      :?keepCultistMask
        equipL @a.wandPoisonDebuff@
      :
        equip @a.staffAetherDebuff@


  : // foe = "phase2" (Enoki & Morel)
    /* ?foe.state ! 1 // boss appearing animation
    ^&foe.state ! 4 // dying */

    ?c.canUseWandPlague() // reduce their damage
      equipL @a.wandPoisonHidden@
      activate L

    /*:?!ai.paused & c.canUseMindstone()
      ldtF("escape")*/
    :?c.canUseCultistMask()
      c.useCultistMask()
    : 
      fight1v1()


// *******************************************************
//                 END OF "4-Mushroom.txt"                
// *******************************************************
