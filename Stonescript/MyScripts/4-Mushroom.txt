// *******************************************************
//                     4-Mushroom.txt                     
// *******************************************************

// Script for Mushroom Forest



// /////////////////////////////////////////// //

// IMPORTS

var uimkr = new MyScripts/Lib/UI_Maker
var u = new MyScripts/Lib/Utilities
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat

/***********************************************/

// import UI/FoeStateTracker

/***********************************************/

var som = new MyScripts/Lib/Speed_O_Meter

var somPanel = som.mkUI(0, 1,
^u.colorToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1,
  ^u.colorToHex("red"), true, false)

/***********************************************/

var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED

dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***********************************************/



// /////////////////////////////////////////// //

// VARIABLES

var keepCultistMask = true
var smiteScreen = 2

?loc.loop
  keepCultistMask = false

  ?totaltime = 1 &
  ^smiteScreen ! 3
    smiteScreen++

var moondialMode = -1

?buffs.string = "berserk"
  moondialMode = 2
:
  moondialMode = 0



// /////////////////////////////////////////// //

// FUNCTIONS:

func ldtF(mode)

  ?mode = "default"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound2@
  :?mode = "engage"
    ?keepCultistMask & foe.count
      equipL @a.hammerPoisonDefense@
    :?!keepCultistMask
      equipL triskelion
      equipR @a.shieldPoisonDefense@

  :?mode = "escape"
    equipL mind
    ?!keepCultistMask
      equipR @a.shieldPoisonDefense@
  :?mode = "magnet"
    equipL star
    ?!keepCultistMask
      equipR triskelion

  :?mode = "meleeDmg"
    ?keepCultistMask
      equipL Mamba Negra
    :
      c.doMoondial(Mamba Negra, a.swordPoisonDmg, moondialMode)
  :?mode = "meleeDebuff"
    ?keepCultistMask
      ?c.canDebuffChill()
        equipL @a.swordIceDebuff@
      :?c.canDebuffWeaken()
        equipL @a.swordPoisonDebuff@
      :?c.canDebuffBurn()
        equipL @a.swordFireDebuff@
    :
      //Left hand:
      ?c.canDebuffWeaken()
        equipL @a.swordPoisonDebuff@
      :?c.canDebuffBurn()
        equipL @a.swordFireDebuff@
      :?c.canDebuffChill()
        equipL @a.wandIceDebuff@

      //Right hand:
      ?c.canDebuffChill()
        equipR @a.swordIceDebuff@
  :?mode = "meleeHeal"
    ?keepCultistMask
      equipL @a.swordVigorBuff@
    :
      c.doMoondial(Mamba Negra, a.swordVigorBuff, moondialMode)

  :?mode = "unmakeAOE"
    equip @a.staffAetherDebuff@
    /* ?keepCultistMask
      equipL @a.bigSwordAetherDebuff@
    : */
  :?mode = "unmake1v1"
    equip @a.staffAetherDebuff@
    /* ?keepCultistMask
      equipL @a.swordAetherDebuff@
    :
      // put a 2nd dU sword when we have it
      c.doMoondial(a.swordAetherDebuff, Mamba Negra, moondialMode)*/

  :?mode = "rangedDmgAOE"
    ?keepCultistMask
      equipL @a.wandPoisonDmg@
    :
      equip @a.bowPoisonDmg@

  :?mode = "rangedDebuffAOE"
    ?keepCultistMask
      ?c.canDebuffChill()
        equipL @a.wandIceDebuff@
      :?c.canDebuffWeaken()
        equipL @a.wandPoisonDebuff@
      :?c.canDebuffBurn()
        equipL @a.wandFireDebuff@

    :
      ?c.canDebuffChill()
        equip @a.staffIceDebuff@
      :?c.canDebuffWeaken()
        equip @a.staffPoisonDebuff@
      :?c.canDebuffBurn()
        equip @a.staffFireDebuff@

  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight1v1()

  ?c.canDebuffAny() & // foe can be debuffed
  ^encounter.eliteMod ! "tenacious" // foe isn't immune to debuffs
    ldtF("meleeDebuff")

  : // all possible debuffs applied
    ?hp ! maxhp & !debuffs.string // heal up
      ldtF("meleeHeal")

    : // deal max damage
      ldtF("meleeDmg")

  return



// /////////////////////////////////////////// //

// PROCEDURE:

?buffs.string ! "buff_inf_speed" & // infernal staff
^totaltime % 2 = 0 & // every even frame
^item.left = "triskelion"
  equipL @a.hammerPoisonDefense@ // gain extra armor without slowing down

c.doAAC(a.hammerPoisonDefense, a.shieldPoisonDefense)

?loc.begin
  // initial equipment
  equipL triskelion
  equipR @a.shieldCompound1@

  // ///////////////////////////// //
  // AUTO BREWING
  ?res.bronze >= 10
    ?res.wood >= 10
      ?item.potion ! "berserk"
        brew wood + bronze

    :?res.stone >= 10
      ?item.potion ! "lucky"
        brew stone + bronze
    :
      loc.Leave()
  :
    loc.Leave()
  // ///////////////////////////// //

:?keepCultistMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer" &
  ^item.right ! "quarterstaff"
    equipL triskelion
    equipR mask


?c.canUsePotionDmg() & keepCultistMask
  activate potion

:?c.canUseStaffInfernal()
  c.useStaffHidden(a.staffFireHidden)
  activate R


?c.canUseBFG_DS(smiteScreen)
  c.useBFG_DS()


?foe ! "boss" & foe.distance ! 9999 &
^c.canUseAetherTalisman()
  ?keepCultistMask
    c.useAetherTalisman("l")
  :
    equipL triskelion
    c.useAetherTalisman("r")

:?foe = "boss" & foe.distance <= 23 &
^c.canUseFireTalisman()
  ?keepCultistMask
    c.useFireTalisman("l")
  :
    equipL triskelion
    c.useFireTalisman("r")


:?pickup.distance < 15
  ldtF("magnet")

:?foe = "explode" & // Mr. Puff
^foe.distance < 5 // explosion range
  ?c.canUseMindstone()
    ldtF("escape")
  :?c.canUseStaffAcrobatic()
    c.useStaffHidden(a.staffStoneHidden)
  :?c.canUseStaffPrevention()
    c.useStaffHidden(a.staffVigorHidden)
  :?c.canUseStaffEternity()
    c.useStaffHidden(a.staffIceHidden)

:?ai.walking &
^(foe.distance < 11 | foe.distance > 15 ) &
^c.canUseQuarterstaff()
  c.useQuarterstaff(a.myQuarterstaff)

:?ai.paused |
^(ai.idle & foe.distance > 11) | // if we're waiting
^foe.state = -1 | foe.state = 4 | // foe death states
^foe.distance > 15 // or we can't engage in a fight
  ?armor > 29 |
  ^(foe ! "phase1" & foe.distance < 40)
    ldtF("engage")
  :?14 <= armor & armor <= 29
    ldtF("default")
  :
    ldtF("armorRegen")


:?foe ! "phase" // not the boss

  ?encounter.eliteMod = "monarch"
    ?foe.count > 1 // eliminate the other foes
      ?c.canUseBFG()
        c.useBFG()
      :?c.canUseWandExplosive()
        c.useWandExplosive(a.wandFireHidden, "l")

      :?foe.distance < 7 & // if too close, try to escape
      ^(c.canUseMindstone() |
      ^c.canUseStaffAcrobatic())
        ?c.canUseMindstone()
          ldtF("escape")
        :
          c.useStaffHidden(a.staffStoneHidden)

      :?c.canUseHeavyHammer() // if close, use hammer
        c.useHeavyHammer(a.myHeavyHammer)
      :?c.canDebuffAny()
        ldtF("rangedDebuffAOE")
      :
        ldtF("rangedDmgAOE")

    :?c.canUseBashingShield()
      c.useBashingShield(Mamba Negra)
    :?c.canUseDashingShield()
      c.useDashingShield(Mamba Negra, a.shieldDashing)

    : // 1v1 with the monarch
      ?foe.hp + foe.armor >= a.bardicheRDmg &
      ^c.canUseBardiche()
        c.useBardiche(Kubikiribocho)
      :
        fight1v1()

  :?c.canUseBashingShield()
    c.useBashingShield(Mamba Negra)
  :?c.canUseDashingShield()
    c.useDashingShield(Mamba Negra, a.shieldDashing)

  :?foe = "snail" & foe = "boss" // The snail that's always with Mr. Puff
    ?c.canUseHeavyHammer()
      c.useHeavyHammer(a.myHeavyHammer)
    :?c.canUseBardiche()
      c.useBardiche(Kubikiribocho)

    :?c.canDebuffAny()
      ldtF("meleeDebuff")
    :
      ldtF("meleeDmg")

  :?foe.count > 1 // AOE strategy
    ?!encounter.isElite &
    ^screen.i ! smiteScreen &
    ^c.canUseVoidweaver()
      c.useVoidweaver()

    ?encounter.isElite &
    ^encounter.eliteMod ! "tenacious" &
    ^c.canUseWandGravity()
      c.useWandGravity(a.wandStoneHidden, "l")

    // Hinder regen
    ?encounter.eliteMod = "regen" &
    ^c.canUseWandExplosive()
      c.useWandExplosive(a.wandFireHidden, "l")

    // Remove armor
    :?encounter.eliteMod = "plated" &
    ^c.canUseHeavyHammer()
      c.useHeavyHammer(a.myHeavyHammer)

    :?c.canDebuffAny() &
    ^encounter.eliteMod ! "tenacious"
      ldtF("rangedDebuffAOE")
    :
      ldtF("rangedDmgAOE")

  : // 1v1 strategy
    ?encounter.eliteMod = "titan" &
    ^c.canUseBardiche()
      c.useBardiche(Kubikiribocho)
    :
      fight1v1()

// Boss fight - phase1
:?foe = "phase1"

  ?c.canUsePotionDmg()
    activate potion

  :?foe.armor > foe.maxarmor / 2 &
  ^c.canUseHeavyHammer()
    c.useHeavyHammer(a.myHeavyHammer)

  :?foe.state = 32 & foe.time >= 75 // boss's punch attack
    ?foe.distance < 20
      ?c.canUseMindstone()
        ldtF("escape")
      :?c.canUseStaffAcrobatic()
        c.useStaffHidden(a.staffStoneHidden)
      :?c.canUseStaffEternity()
        c.useStaffHidden(a.staffIceHidden)
      :
        ldtF("meleeDmg")

    :
      ldtF("rangedDmgAOE")

  : // Dash or Bash
    ?c.canUseBashingShield()
      c.useBashingShield(Mamba Negra)
    :?c.canUseDashingShield()
      c.useDashingShield(Mamba Negra, a.shieldDashing)
    :
      fight1v1()


: // Boss fight - phase2 (Enoki & Morel)
  ?c.canUseCinderwisp() &
  ^foe.debuffs.string = a.maxIgnitions_Default
    c.useCinderwisp()

  ?c.canUseWandPlague() // reduce their damage
    c.useWandPlague(a.wandPoisonHidden, "l")
  :?c.canUseStaffBerserker() // increase our damage
    c.useStaffHidden(a.staffPoisonHidden)

  :?c.canUseCultistMask()
    c.useCultistMask()

  : // get in melee range
    ?c.canUseBashingShield()
      c.useBashingShield(Mamba Negra)
    :?c.canUseDashingShield()
      c.useDashingShield(Mamba Negra, a.shieldDashing)
    :
      fight1v1()



// *******************************************************
//                 END OF "4-Mushroom.txt"                
// *******************************************************
