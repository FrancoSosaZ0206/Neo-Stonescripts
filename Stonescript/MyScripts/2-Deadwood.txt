// *******************************************************
//                     2-Deadwood.txt                     
// *******************************************************

// Script for Deadwood Canyon



// IMPORTS

var u = new MyScripts/Lib/Utilities
var uimkr = new MyScripts/Lib/UI_Maker
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat
c.Main()

/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter
var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED
var bosTmr
bosTmr = import MyScripts/Borrowed/BossfightTimer2

var somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)


dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/



// VARIABLES

var keepMask = true
var smiteScr = 1

var dontDash = false
var debCount = 0

?loc.loop
  keepMask = false
  dontDash = false
  debCount = 0

var canDebI
var canDebP
var canDebF



// FUNCTIONS:

func harvFunc(btn)
  btnPressed = true
  ?btn = yesBtn
    choice = true
  : // btn = noBtn
    choice = false

  return

func ldtF(mode)

  ?mode = "default"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield2@
  :?mode = "escape"
    equipL mind
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "magnet"
    equipL star
    ?!keepMask
      equipR triskelion

  :?mode = "extend"
    equipL triskelion
    equipR mask

  :?mode = "harv"
    ?!keepMask
      equipL moon
      equipR hatchet
    :
      equipL triskelion

  :?mode = "aidPassive"
    equipL ouroboros
    ?!keepMask
      equipR triskelion
  :?mode = "aidActive"
    equipL @a.vhammer3@
    ?!keepMask
      equipR @a.vshield2@

  :?mode = "unmkDmg"
    equip @a.dUstaff@

  :?mode = "poenaStun"
    ?!keepMask
      equipL Speedy Hammer
      equipR Bifrost
    :
      equipL Speedy Hammer

  :?mode = "magic"
    ?keepMask
      ?canDebI
        equipL @a.dIwand1@
      :?canDebP
        equipL @a.dPwand@
      :?canDebF
        equipL @a.dFwand@

    :
      ?canDebI
        equip @a.dIstaff@
      :?canDebP
        equip @a.dPstaff@
      :?canDebF
        equip @a.dFstaff@

  :?mode = "rangedDmg"
    equip @a.rbow@

  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight1v1()

  /* How to apply debuffs:
  - apply all except the ones the foe is immune to.
  Finish when:
  - all possible debuffs have been applied. */
  canDebI = (foe.debuffs.string ! "chill:6" & foe ! "immune_to_debuff_chill")
  canDebP = (foe.debuffs.string ! "damage" & foe ! "immune_to_debuff_damage")
  canDebF = (foe.debuffs.string ! "dot" & foe ! "immune_to_debuff_dot")

  ?canDebI | canDebP | canDebF
    ?keepMask
      ?canDebI
        equipL @a.dIsword@
      :?canDebP
        equipL @a.dPsword@
      :?canDebF
        equipL @a.dFsword@
    :
      //Left hand:
      ?canDebP
        equipL @a.dPsword@
      :?canDebF
        equipL @a.dFsword@
      :?canDebI
        equipL @a.dIwand1@

      //Right hand:
      ?canDebI
        equipR @a.dIsword@


  :?hp ! maxhp
    ?keepMask
      equipL @a.dLsword@
    :
      equipL moon
      equipR @a.dLsword@

  :
    equip arm
    /*?keepMask
      equipL Bifrost
    :
      ?buffs.string = "berserk"
        c.doMoondial(Bifrost, a.isword, 2)
      :
        c.doMoondial(Bifrost, a.isword, 0)*/

  return



// PROCEDURE:

var choice = false
var btnPressed = false

var yesBtn
var noBtn

var dontShowAgain = false

?!btnPressed
  ?!yesBtn
    yesBtn = uimkr.mkBtn(
    ^-5,9,null,2,null,null, // x, y, w, h, anchor, dock,
    ^"Yes","#green", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound

  ?!noBtn
    noBtn = uimkr.mkBtn(
    ^5,9,8,2,null,null, // x, y, w, h, anchor, dock,
    ^"No","red", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound


  i.showStrCtr(6,30,
  ^"Harvest trees?","#yellow",
  ^false)

:
  ?yesBtn
    yesBtn.Recycle()
    yesBtn = null
  ?noBtn
    noBtn.Recycle()
    noBtn = null


  ?!dontShowAgain
    var aux = totaltime + 3*u.sec
    ?totaltime < aux
      ?choice
        i.showStrCtr(6, 30,
        ^"Harvest trees is enabled","#green",
        ^false)
        ?keepMask
          i.showStrCtr(7, 40,
          ^"(Will start harvesting in the next run!)","#green",
          ^false)
      :
        i.showStrCtr(6, 30,
        ^"Harvest trees is disabled","#red",
        ^false)
    :
      ?!dontShowAgain
        dontShowAgain = true



c.doAAC("sword 0*", a.cshield1)

?loc.begin

  ?res.bronze >= 10

    ?res.wood >= 10
      ?item.potion ! "berserk"
        brew wood + bronze

    :?res.stone >= 10
      ?item.potion ! "lucky"
        brew stone + bronze
    :
      loc.Leave()
  :
    loc.Leave()

?keepMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer"
    equipR mask

?loc.begin | loc.loop
  equipL triskelion
  equipR @a.cshield1@

:?foe ! "boss" & c.canUseAETali()
  ?keepMask
    c.useAETali("l")
  :
    equipL triskelion
    c.useAETali("r")

:?foe = "tree_boss" & c.canUseFTali()
  ?keepMask
    c.useFTali("l")
  :
    equipL triskelion
    c.useFTali("r")

/* hide cinderwisp to avoid
getting ignition debuff reflected */
:?c.canHideFTali()
^& foe = "poena"
^& !c.canUseBard()
  ?keepMask
    c.useFTali("l")
  :
    equipL triskelion
    c.useFTali("r")

:?c.canUsePotDmg() & keepMask
  activate potion

:?choice & harvest.distance < 5
  ldtF("harv")
  ?c.canUseHatchet() & !keepMask
    c.useHatchet()

:?pickup.distance < 15
  ldtF("magnet")
:

  ?c.canUseBladeDS(smiteScr)
    c.useBladeDS()

  ?c.canUseQstaff()
  ^& foe ! "tree_boss" & foe ! "poena"
  ^& ai.enabled & item.right ! "hatchet"
  ^& item.right ! "heavy hammer"
  ^& item.right ! "bardiche"
  ^& item.right ! Kubikiribocho
  ^& ((!c.canBash() & !c.canDash())
  ^| (foe.distance < 11 | foe.distance > 15))
    c.useQstaff(a.myqstaff)

  :?((foe = "tree_boss" | foe = "poena") & foe.distance > 22)
  ^|(foe ! "tree_boss" & foe ! "poena" & foe.distance > 15)
    ?hp ! maxhp
      ldtF("aidPassive")
    :
      ?14 <= armor & armor <= 29
        ldtF("default")
      :?armor < 14
        ldtF("armorRegen")
      :
        ldtF("extend")

  :
    ?c.canBash() & !dontDash
      c.bash(a.dLsword)
    :?c.canDash() & !dontDash
      c.dash(a.dLsword, a.dshield)

    :
      ?foe ! "tree_boss" & foe ! "poena"
        ?c.canUseHamm() & keepMask &
        ^foe.GetCount(15) >= 5 & !c.canUseBlade()
          c.useHamm(a.myHamm)
        :?(foe.GetCount(15) = 1 | (foe = "immune_to_ranged" & keepMask)) & !ai.walking
          fight1v1()
        :
          canDebI = (foe.debuffs.string ! "chill:6" & foe ! "immune_to_debuff_chill")
          canDebP = (foe.debuffs.string ! "damage" & foe ! "immune_to_debuff_damage")
          canDebF = (foe.debuffs.string ! "dot" & foe ! "immune_to_debuff_dot")

          ?canDebI | canDebP | canDebF
            ldtF("magic")
          :
            ldtF("unmkDmg")

      :
        ?loc.stars > 5 & c.canUsePotDmg()
          activate potion

        :?foe = "phase1" // Xyloalgia
        ^& foe.state ! 4

          ?foe.hp > a.armDmg * 2
            fight1v1()
          :
            dontDash = true
            ?c.canUseMind()
              ldtF("escape")
            :
              ?item.right.state = 3
                debCount++

              ?debCount < 1
                equipL @a.dPwand@
                equipR @a.dFwand@
              :?debCount < 7
                equip @a.dIstaff@

              :
                dontDash = false
                ?foe.distance > 15 & c.canUseQstaff()
                  c.useQstaff(a.myqstaff)
                :?c.canBash()
                  c.bash(a.dLsword)
                :?c.canDash()
                  c.dash(a.dLsword, a.dshield)
                :
                  fight1v1()


        : // Poena
          dontDash = false

          ?foe.distance > 15 & c.canUseQstaff()
            c.useQstaff(a.myqstaff)
          :?c.canBash()
            c.bash(a.dLsword)
          :?c.canDash()
            c.dash(a.dLsword, a.dshield)

          :?foe.distance <= 9
            ?c.canUseMask()
              c.useMask()
            :?c.canUseBard() & foe.distance <= 9
              c.useBard(Kubikiribocho)

            :
              var smiteBuffs
              smiteBuffs = i.getBuffInfo("smite", false, 2)

              ?smiteBuffs >= 25
                equip arm

              :
                ?c.canUseBard() & foe.hp <= a.bardRDmg
                  c.useBard(Kubikiribocho)

                :?c.canUseHamm() &
                ^foe.hp <= a.hammDmg +
                ^(a.smiteMult * smiteBuffs)
                  c.useHamm(a.myHamm)
                :
                  ldtF("poenaStun")



// *******************************************************
//                 END OF "2-Deadwood.txt"                
// *******************************************************