// *******************************************************
//                       6-Mine.txt                       
// *******************************************************

// Script for Boiling Mine in green star levels



// IMPORTS

var uimkr = new MyScripts/Lib/UI_Maker
var u = new MyScripts/Lib/Utilities
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat

import UI/FoeStateTracker


/***************************************************************

var som = new MyScripts/Lib/Speed_O_Meter

var somPanel = som.mkUI(0, 1, u.colorToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, u.colorToHex("red"), true, false)

/***************************************************************/



// VARIABLES




// FUNCTIONS



// PROCEDURE

c.doAAC("sword 0*", a.shieldCompound1)

?pickup.distance < 12 // collect bronze or chest
  equipL star

:?ai.paused | // if we're waiting
^foe.distance > 23 // or we can't engage in a fight
	equipL triskelion // to go faster
  
  ?armor >= 14 | loc.begin
    equipR @a.shieldCompound1@
  :
    equipR @a.shieldCompound2@

:?foe = "explode" & // Bomb Cart foe
^foe.distance <= 5 & // sword range
^item.GetCooldown("mind") <= 0 // mindstone can be used
  equipL mind
  equipR @a.shieldAetherDefense@ // to get more armor on the way

:?foe = "bronze_guardian"
  ?foe.state = 32 & foe.time = 33 & // about to attack
  ^item.GetCooldown("mind") <= 0
    equipL mind
    equipR @a.shieldAetherDefense@

  :?foe.state = 33 // hammer stuck
    ?c.canUseDashingShield()
      c.useDashingShield("sword *0", a.shieldDashing)

    :?foe.distance <= 8 // heavy hammer range
      ?c.canUseHeavyHammer()
        c.useHeavyHammer(a.myHeavyHammer)
      :
        equipL moon
        equipR Speedy Hammer

    : // get close fast
      equipL triskelion
      equipR sword *0


  :
    equip @a.bowAetherDmg@

: // aether crossbow range = 23
  equip @a.bowAetherDmg@


// *******************************************************
//                   END OF "6-Mine.txt"                  
// *******************************************************