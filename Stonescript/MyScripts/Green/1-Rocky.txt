// *******************************************************
//                   START OF "1-Rocky"                   
// *******************************************************

// Script for Rocky Plateau in green stars



// IMPORTS

var a = new MyScripts/Lib/MyArsenal
var c = new MyScripts/Lib/Combat

import UI/FoeStateTracker


// VARIABLES

var moondialMode = 0

?buffs.string = "buff_inf_speed" |
^item.potion ! "berserk"
  moondialMode = 2
:
  moondialMode = 0


var keepFireTalisman = false

?loc.loop
  keepFireTalisman = true


// FUNCTIONS




// PROCEDURE

?encounter.isElite // print elite modifier
  >`1,12,#red,eliteMod=@encounter.eliteMod@

disable npcDialog
c.doAAC("sword 0*", a.shieldCompound1)

// ///////////////////////////////////////////////// //

?loc.begin
  equipL triskelion
  equipR @a.shieldCompound1@

  // AUTO BREWING
  ?res.bronze >= 10
    ?res.stone >= 10
      ?item.potion ! "lucky"
        brew stone + bronze

    :?res.wood >= 10
      ?item.potion ! "berserk"
        brew wood + bronze

    :
      loc.Leave()
  :
    loc.Leave()


// speed up
?c.canUseStaffInfernal()
  equip @a.staffFireHidden@
  activate R

:?c.canUseAetherTalisman()
  equipL triskelion
  c.useAetherTalisman("r")
:?foe = "phase" & foe.distance <= 23 &
^c.canUseFireTalisman()
  equipL triskelion
  c.useFireTalisman("r")

:?pickup.distance < 5 // collect stones or chest
  equipL star

  ?armor >= 14
    equipR @a.shieldCompound1@
  :
    equipR @a.shieldCompound2@

:?ai.paused | (ai.idle & foe.distance > 11) | // if we're waiting
^foe.state = -1 | foe.state = 4 | // foe death states
^foe.state = 124 | foe.state = 125 | // dysangelos phase 1 to 2 states
^foe.state = 107 | foe.state = 108 | // dysangelos phase 2 to 3 states
^foe.state = 116 | // dysangelos phase 3 death animation state
^foe.distance > 23 // or we can't engage in a fight
  equipL triskelion // to go faster

  ?armor >= 14
    equipR @a.shieldCompound1@
  :
    equipR @a.shieldCompound2@

// keep distance
:?encounter.eliteMod = "berserker" |
^encounter.eliteMod = "hydra"
  
  // Get away
  ?foe.distance < 11 &
  ^c.canUseMindstone()
    equipL mind
  :?foe.distance < 11 &
  ^c.canUseStaffAcrobatic()
    equip @a.stoneStaffHidden@
    activate R

  // combat ranged
  ?foe.debuffs.string ! "chill:6"
    equip @a.staffIceDebuff@
  :?foe.debuffs.string ! "damage"
    equip @a.staffPoisonDebuff@
  :?hp ! maxhp
    equipL moon
    equipR @a.wandVigorHidden@
  :
    equip @a.bowFireDebuff@

:?foe.distance > 15 & // quarterstaff dashing range
^c.canUseQuarterstaff()
  c.useQuarterstaff(a.myQuarterstaff)

// dashing and bashing shields' range
:?11 <= foe.distance & foe.distance <= 15
  ?c.canUseDashingShield()
    c.useDashingShield("sword *0", a.shieldDashing)
  :?c.canUseBashingShield()
    c.useBashingShield("sword *0")


// melee range
:?encounter.eliteMod = "mundane"
  ?foe.count > 1 // AOE
    /*?foe.armor
      equip @a.myHeavyHammer@
    :*/
    equip @a.bowRepeating@

  : // 1 on 1
    ?foe.armor
      equipL moon
      equipR "Speedy Hammer"
    :
      c.doMoondial("Vantum Phoenix", a.swordFireDmg, 0)

:?encounter.eliteMod = "metamorph" // same as dysan phase2

  ?foe.count > 1 // AOE
    ?foe.debuffs.string ! "chill:6"
      equip @a.staffIceDebuff@
    :?foe.debuffs.string ! "damage"
      equip @a.staffPoisonDebuff@
    :?foe.debuffs.string ! "dot"
      equip @a.staffFireDebuff@

    :?foe.armor
      equip @a.myHeavyHammer@
    :
      equip @a.staffFireDebuff@

  : // 1 on 1
    ?foe.debuffs.string ! "chill:6" // slow down
      c.doMoondial("Vantum Phoenix", a.swordIceDebuff, moondialMode)
    :?foe.debuffs.string ! "damage" // reduce damage
      equipL moon
      equipR @a.swordPoisonDebuff@
    :?foe.debuffs.string ! "dot" // burn
      equipL moon
      equipR @a.swordFireDebuff@

    :?foe.armor
      equipL moon
      equipR "Speedy Hammer"
    
    :?foe = "poison" // use ice
      c.doMoondial("Bifrost", a.swordIceDmg, moondialMode)
    :?foe = "vigor" // use poison
      c.doMoondial("Mamba Negra", a.swordPoisonDmg, moondialMode)
    :?foe = "aether" // use vigor
      c.doMoondial("Dothaneel", a.swordVigorDmg, moondialMode)
    :?foe = "fire" // use aether
      c.doMoondial("Void Slayer", a.swordAetherDmg, moondialMode)
    : // ice -> use fire
      c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)

:?foe = "acronian_soldier"
  ?encounter.isElite
    ?c.canUseVoidweaver()
      c.useVoidweaver()

    ?c.canUseWandCalamity()
      equipL @a.wandAetherHidden@
      activate L

    ?c.canUseBFG()
      c.useBFG()

    :?c.canUseHeavyHammer()
      c.useHeavyHammer(a.myHeavyHammer)

  /*?c.canUseWandGravity()
    c.useWandGravity(a.wandStoneHidden, "l")*/

  :?hp ! maxhp // heal up
    c.doMoondial("Dothaneel", a.swordVigorBuff, moondialMode)

  :?foe.count > 1
    ?encounter.eliteMod ! "tenacious"
      ?foe.debuffs.string ! "chill:6"
        equip @a.staffIceDebuff@
      :?foe.debuffs.string ! "damage"
        equip @a.staffPoisonDebuff@

    :?foe.armor
      equip @a.myHeavyHammer@
    :
      equip @a.staffFireDebuff@

  : // 1 on 1
    c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)

:?encounter.eliteMod ! "tenacious" &
^foe.debuffs.string ! "chill:6" // slow down
  c.doMoondial("Vantum Phoenix", a.swordIceDebuff, moondialMode)
:?encounter.eliteMod ! "tenacious" &
^foe.debuffs.string ! "damage" // reduce damage
  equipL moon
  equipR @a.swordPoisonDebuff@
:?encounter.eliteMod ! "tenacious" &
^foe.debuffs.string ! "dot" // burn
  equipL moon
  equipR @a.swordFireDebuff@

:?hp ! maxhp // heal up
  c.doMoondial("Dothaneel", a.swordVigorBuff, moondialMode)

:?foe = "acronian_scout"
  ?c.canUseBardiche()
    c.useBardiche("Kubikiribocho")
  :?c.canUseStaffBerserker()
    equip @a.staffPoisonHidden@
    activate R
  :
    // focus on damage
    c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)
  
: // Dysangelos

  ?foe = "phase1" // focus on damage
    c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)

  :?foe = "phase2"

    ?foe.buffs.string = "buff_protection" & // against debuffs
    ^item.GetCooldown("wand_vigor") <= 0 // and it's not on cooldown
      ?armor >= 1
        equipL @a.wandVigorHidden@
        activate L
      :
        equipR @a.shieldCompound2@

    ?foe = "poison" // use ice
      c.doMoondial("Bifrost", a.swordIceDmg, moondialMode)
    :?foe = "vigor" // use poison
      c.doMoondial("Mamba Negra", a.swordPoisonDmg, moondialMode)
    :?foe = "aether" // use vigor
      c.doMoondial("Dothaneel", a.swordVigorDmg, moondialMode)
    :?foe = "fire" // use aether
      c.doMoondial("Void Slayer", a.swordAetherDmg, moondialMode)
    : // ice -> use fire
      c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)

  : // phase 3
    ?c.canUsePotionDmg()
      activate potion
    :?c.canUseStaffBerserker()
      equip @a.staffPoisonHidden@
      activate R
    :?c.canUseCultistMask() // reduce damage even further
      c.useCultistMask()

    ?c.canUseCinderwisp()
      ?foe.debuffs.string = a.maxIgnitions_Default |
      ^c.canKillCinderwisp(i.getFoeTotalHP(),
      ^                    i.getDebuffLvl("ignition", true),
      ^                    a.cinderwispDmg_Default,
      ^                    0                                )
        c.useCinderwisp()

    ?foe.buffs.string ! "defense_fire" // doesn't have the element immunity shield
      c.doMoondial("Vantum Phoenix", a.swordFireDmg, moondialMode)

    : // immune to fire
      c.doMoondial("Bifrost", a.swordIceDmg, moondialMode)





// *******************************************************
//                    END OF "1-Rocky"                    
// *******************************************************