// *******************************************************
//                       5-Halls.txt                      
// *******************************************************

// Script for Haunted Halls



// /////////////////////////////////////////// //

// IMPORTS

var u = new MyScripts/Lib/Utilities
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat



// /////////////////////////////////////////// //

// VARIABLES

var keepCultistMask = true
var smiteScreen = 1
var moondialMode = -1

?loc.loop
  keepCultistMask = false

  ?totaltime = 1 & smiteScreen ! 4
    smiteScreen++

?buffs.string = "berserk"
  moondialMode = 2
:
  moondialMode = 0



// /////////////////////////////////////////// //

// FUNCTIONS

func ldtF(mode)

  ?mode = "default"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepCultistMask
      equipR @a.shieldCompound2@
  :?mode = "engage"
    ?buffs.string ! "buff_inf_speed" & // infernal staff
    ^totaltime % 2 = 0 // every even frame
      equipL @a.hammerVigorBuff@
    :
      equipL triskelion

    ?!keepCultistMask
      ?foe ! "immune_to_debuff_chill" &
      ^foe.debuffs.string ! "chill" &
      ^encounter.eliteMod ! "tenacious"
        equipR @a.shieldIceDebuff@
      :
        equipR @a.shieldVigorDefense@

  :?mode = "bash"
    equipL Dothaneel
    equipR bashing shield
  :?mode = "dash"
    equipL Dothaneel
    equipR @a.shieldDashing@

  :?mode = "escape"
    equipL mind
    ?!keepCultistMask
      equipR @a.shieldVigorDefense@
  :?mode = "magnet"
    equipL star
    ?!keepCultistMask
      equipR triskelion

  :?mode = "defenseHeal" // heal when hit
    equipL @a.hammerVigorBuff@
    equipR @a.shieldVigorBuff@
  :?mode = "defenseUnmake" // unmake when hit
    equipL @a.swordAetherDebuff@
    equipR @a.shieldAetherDebuff@

  :?mode = "meleeDmg"
    ?keepCultistMask
      equipL Dothaneel
    :
      c.doMoondial(Dothaneel, a.swordVigorDmg, moondialMode)
  :?mode = "meleeDebuff"
    ?keepCultistMask
      ?c.canApplyChill()
        equipL @a.swordIceDebuff@
      :?c.canApplyWeaken()
        equipL @a.swordPoisonDebuff@
      :?c.canApplyBurn()
        equipL @a.swordFireDebuff@
    :
      //Left hand:
      ?c.canApplyWeaken()
        equipL @a.swordPoisonDebuff@
      :?c.canApplyBurn()
        equipL @a.swordFireDebuff@
      :?c.canApplyChill()
        equipL @a.wandIceDebuff@

      //Right hand:
      ?c.canApplyChill()
        equipR @a.swordIceDebuff@
  :?mode = "meleeHeal"
    ?keepCultistMask
      equipL Soulfetcher
    :
      c.doMoondial(Dothaneel, Soulfetcher, moondialMode)

  :?mode = "rangedAOEUnmake"
    equip @a.staffAetherDebuff@
    /* ?keepCultistMask
      equipL @a.bigSwordAetherDebuff@
    : */
  :?mode = "meleeUnmake"
    equip @a.staffAetherDebuff@
    /* ?keepCultistMask
      equipL @a.swordAetherDebuff@
    :
      // put a 2nd dU sword when we have it
      c.doMoondial(a.swordAetherDebuff, Dothaneel, moondialMode)*/

  :?mode = "rangedAOEDmg"
    equipL theVwand
    ?!keepCultistMask
      equipR @a.wandVigorDmg@ // the 2nd one
  :?mode = "rangedAOEDebuff"
    ?keepCultistMask
      ?c.canApplyChill()
        equipL @a.wandIceDebuff@
      :?c.canApplyWeaken()
        equipL @a.wandPoisonDebuff@
      :?c.canApplyBurn()
        equipL @a.wandFireDebuff@
    :
      ?c.canApplyChill()
        equip @a.staffIceDebuff@
      :?c.canApplyWeaken()
        equip @a.staffPoisonDebuff@
      :?c.canApplyBurn()
        equip @a.staffFireDebuff@
  :?mode = "rangedAOEHeal"
    ?keepCultistMask
      equipL @a.wandVigorBuff@
    :
      equipL moon
      equipR @a.wandVigorBuff@
      // equip @a.staffVigorBuff@ // @a.bowVigorBuff@

  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight(mode)
  ?c.canDebuffAny() & // foe can be debuffed
  ^encounter.eliteMod ! "tenacious" // foe isn't immune to debuffs
    ?mode = "melee"
      ldtF("meleeDebuff")
    :?mode = "rangedAOE"
      ldtF("rangedAOEDebuff")

  : // all possible debuffs applied
    ?hp ! maxhp // heal up
      ?mode = "melee"
        ldtF("meleeHeal")
      :?mode = "rangedAOE"
        ldtF("rangedAOEHeal")

    : // deal max damage
      ?mode = "melee"
        ldtF("meleeDmg")
      :?mode = "rangedAOE"
        ldtF("rangedAOEDmg")

  return



// /////////////////////////////////////////// //

// PROCEDURE

c.doAAC(a.hammerVigorBuff, a.shieldVigorDefense)

// Auto Brewing
?c.canBrewPotion("berserk")
  c.brewPotion("berserk")

?loc.begin // initial equipment
  equipL triskelion
  equipR @a.shieldCompound1@
:?keepCultistMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer" &
  ^item.right ! "quarterstaff"
    equipL triskelion
    equipR mask


?c.canUseBFG_DS(smiteScreen)
  c.useBFG_DS()


?foe ! "boss" & !c.isDead() &
^c.canUseAetherTalisman()
  ?keepCultistMask
    c.useAetherTalisman("l")
  :
    equipL triskelion
    c.useAetherTalisman("r")
:?foe = "boss" &
^c.canUseFireTalisman()
  ?keepCultistMask
    c.useFireTalisman("l")
  :
    equipL triskelion
    c.useFireTalisman("r")

:?c.canUsePotionDmg() & keepCultistMask
  activate potion
:?c.canUseStaffHidden("infernal")
  c.useStaffHidden(a.staffFireHidden, "infernal")

:?pickup.distance < 15
  ldtF("magnet")

// ABOUT TO ATTACK
:?foe = "skeleton_boss" & // & foe = "boss"
^foe.state = 32 & foe.time > 63
  ?c.canUseStaffHidden("prevention") &
  ^foe = "phase2"
    c.useStaffHidden(a.staffVigorHidden, "prevention")

  :?c.canUseStaffHidden("eternity") &
  ^buffs.string ! "buff_staff_protection"
    c.useStaffHidden(a.staffIceHidden, "eternity")

  :?foe.distance <= c.getRange("sword") & // couldn't dash, try to unmake to prevent attack
  ^foe ! "phase2" &
  ^!(loc.stars > 15 & keepCultistMask)
    ldtF("defenseUnmake")

  :?armor < trueDmg
    ?c.canUseMindstone()
      ldtF("escape")
    :?c.canUseStaffHidden("acrobatic") & foe.distance <= c.getRange("sword")
      c.useStaffHidden(a.staffStoneHidden, "acrobatic")

    :?foe.distance > c.getRange("sword")
      fight("rangedAOE")
    : // heal to reduce it even more
      ldtF("defenseHeal")

  :?armor >= trueDmg
    ?buffs.string ! "buff_damage" & foe ! "phase2"
      equipR @a.shieldPoisonBuff@

    : // could dash, fight ranged
      fight("rangedAOE")


:?ai.walking &
^(foe.distance < u.dash_range_min |
^foe.distance > u.dash_range_max) &
^!(encounter.isElite &
^foe.distance <= c.getRange("wand") &
^(item.left = "wand" | item.right = "wand")) &
^c.canUseQuarterstaff()
  c.useQuarterstaff(a.myQuarterstaff)

:?foe ! "skeleton_boss" &
^!(encounter.isElite &
^foe.distance <= c.getRange("wand") &
^(item.left = "wand" | item.right = "wand")) &
^(ai.paused |
^(ai.idle & foe.distance > u.dash_range_min) | // if we're waiting
^c.isDead() |
^(encounter.isElite & foe.distance > c.getRange("wand")) |
^(!encounter.isElite & foe.distance > u.dash_range_max)) // or we can't engage in a fight
  ?armor > a.compoundMaxArmor_boosted |
  ^(foe ! "phase" & foe.distance < u.shield_rune_range)
    ldtF("engage")
  :?a.compoundMaxArmor_default <= armor &
  ^armor <= a.compoundMaxArmor_boosted
    ldtF("default")
  :
    ldtF("armorRegen")


:?foe = "skeleton_boss" // Pallas (note: sword can be unmade in phase1)
  ?c.canUseCinderwisp() &
  ^foe.debuffs.string = a.maxIgnitions_Default
    c.useCinderwisp()
  ?c.canUsePotionDmg()
    activate potion

  ?foe = "phase2" & foe.count > 1
    ?c.canUseHeavyHammer()
      c.useHeavyHammer(Mjölnir)
    :?c.canUseWandHidden("explosive")
      c.useWandHidden(a.wandFireHidden, "explosive", "l")
    :
      fight("rangedAOE")

  :?c.canUseBashingShield()
    ldtF("bash")
  :?c.canUseDashingShield()
    ldtF("dash")

  :?c.canUseWandHidden("plague") // reduce their damage
    c.useWandHidden(a.wandPoisonHidden, "plague", "l")
  :?c.canUseStaffHidden("berserker") // increase our damage
    c.useStaffHidden(a.staffPoisonHidden, "berserker")
  :?c.canUseCultistMask()
    c.useCultistMask()
  :?c.canUseWandHidden("frost")
    c.useWandHidden(a.wandIceHidden, "frost", "l")

  :
    fight("melee")


// normal foes
:?encounter.eliteMod = "monarch"
  ?foe.count > 1 // eliminate the other foes
    ?c.canUseBFG()
      c.useBFG()
    :?c.canUseWandHidden("explosive")
      c.useWandHidden(a.wandFireHidden, "explosive", "l")

    :?foe.distance < 7 & // if too close, try to escape
    ^(c.canUseMindstone() |
    ^c.canUseStaffHidden("acrobatic"))
      ?c.canUseMindstone()
        ldtF("escape")
      :
        c.useStaffHidden(a.staffStoneHidden, "acrobatic")

    :?c.canUseHeavyHammer() // if close, use hammer
      c.useHeavyHammer(Mjölnir)
    :
      fight("rangedAOE")

  :?c.canUseBashingShield()
    ldtF("bash")
  :?c.canUseDashingShield()
    ldtF("dash")

  : // 1v1 with the monarch
    ?foe.hp + foe.armor >= a.bardicheRDmg &
    ^c.canUseBardiche()
      c.useBardiche(Kubikiribocho)
    :?foe ! "immune_to_physical"
      fight("melee")
    :
      fight("rangedAOE")

:?c.canUseBashingShield() & !encounter.isElite
  ldtF("bash")
:?c.canUseDashingShield() & !encounter.isElite
  ldtF("dash")

:?foe.name = "R.I.Pieces"
  fight("melee")

:?foe.count > 1 // AOE strategy
  ?!(screen.i = smiteScreen |
  ^screen.i = smiteScreen + 1)
    ?c.canUseVoidweaver() & !encounter.isElite
      c.useVoidweaver()

    :?encounter.eliteMod ! "tenacious" &
    ^c.canUseWandHidden("gravity")
      c.useWandHidden(a.wandStoneHidden, "gravity", "l")


  // Hinder regen
  ?encounter.eliteMod = "regen" &
  ^c.canUseWandHidden("explosive")
    c.useWandHidden(a.wandFireHidden, "explosive", "l")

  // Remove armor
  :?encounter.eliteMod = "plated" &
  ^c.canUseHeavyHammer()
    c.useHeavyHammer(Mjölnir)

  :
    fight("rangedAOE")

: // 1v1 strategy
  ?foe ! "immune_to_physical"
    ?foe.hp + foe.armor >= a.bardicheRDmg &
    ^c.canUseBardiche()
      c.useBardiche(Kubikiribocho)
    :
      fight("melee")
  :
    fight("rangedAOE")



// *******************************************************
//                  END OF "5-Halls.txt"                  
// *******************************************************