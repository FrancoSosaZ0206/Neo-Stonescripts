//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄ START OF "Stats" ▀▄▀▄▀▄▀▄▀▄ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //


/* // ///////////// FILE INFO //////////////// //

Stats library
Made by IronHawk (Tom Crow)

. ................ Description ................ .

  This library contains a pack of stats from the
  game, as well as querie functions to consult
  and reference.

  It's static, so updates in-game won't be
  reflected here. It is therefore advised to
  keep values updated.

. ................. Importing ................. .

  Desktop:
    Put this file in Stonescript/UI or your
    directory of preference (inside Stonescript),
    then do this:

    var s = import <YourDirectory>/Stats

  Mobile:
    Copy-paste this script and any dependencies
    into your Mindstone.

. ................... Usage ................... .

  Example - TO DO:

  TO DO

. ............................................. .

Enjoy! */

/* HITBOXES AND SPLASH RANGES

Ranges + Hitboxes table

Link: https://discord.com/channels/423242655498240000/609453573113249924/1283482516287918153

"So here are the main hitboxes, with slashes ('/')
showing splash area to the right vs odd-width foe.

All hitbox widths are odd unless it says even,
which means splash rounds down while main hit
rounds up.

          11111111
012345678901234567
..bbbbbBbbbbb////. bardiche, bfg
...sssSsss/....... skeleton arm (has 1 aoeX)
..ccCcc////....... basic crossbow left
...ccCcc////...... basic crossbow right
....ccCcc////..... heavy crossbows, heavy hammer
..ssSss///........ sword left, staff melee, hammer left (even)
...ssSss///....... sword right, big sword left, hammer right  (even)
....ssSss///...... big sword right (even)
..wWw////......... wand left, torch left, harmonic
...rRr////........ runestone left, runestone right
....wWw////....... wand right, staff magic, torch right
G////............. grappling
tTt////........... stone throw left
.tTt////.......... stone throw right
..pPp////......... punch, kick
.hhhHhhh////...... hatchet (even) (has 5 aoeX)
.ssSss///......... shovel (even)
....ffffffffffffff fissure left (up to 63) (even)
......ffffffffffff fissure right (up to 65) (even)"

******************************************************
How splash and the table works:

Link: https://discord.com/channels/423242655498240000/609453573113249924/1378196531706073178

"The top row of the table is distance to the enemy
and the letters are where that hitbox will hit.
For example, bardiche will hit an enemy at
distance 12, but not 13.

Enemies have their own collision width extending to
the right, which is why you don't miss when
swinging at dist 1.

When a projectile hits the first enemy, it
splashes and hits the rest, using a different
calculation:

For most projectiles, it grows the projectile
hitbox by 4 units on both sides, 2 units vertically
(to the area shown with slashes), but there is a
bug that will shift the enemy hitbox left by half
their collision width with the splash collision
detection, so actually you get +1 range on top of
this if the enemy is 3-wide, +2 if its 5-wide, etc.

I don't recall how even enemy widths round, it might
depend if the projectile hitbox is even or odd, [...]."
*/

// //////////////// IMPORTS ///////////////// //

var u = import My/Lib/Utilities


// /////////////// VARIABLES //////////////// //

// .................. IDs ................... //

var cooldown_table = [ // ability cooldowns only
^   "blade",
^   "mask",
^   "arm",

^   "mind",

^   "bash",
^   "dash",

^   "bardiche",
^   "heavy_hammer",
^   "quarterstaff",

// ^   "stone_talisman",
// ^   "poison_talisman",
// ^   "vigor_talisman",
^   "aether_talisman",
^   "fire_talisman",
// ^   "ice_talisman",

// ^   "",
// ^   "",
// ^   "",
^   "voidweaver",
^   "cinderwisp",
// ^   "",

^   "wand_stone",
^   "wand_poison",
^   "wand_vigor",
^   "wand_aether",
^   "wand_fire",
^   "wand_ice",

^   "staff_stone",
^   "staff_poison",
^   "staff_vigor",
^   "staff_aether",
^   "staff_fire",
^   "staff_ice"
^]

var talisman_table = [
^   "TALISMAN ID"              , "SUMMON ID",

// ^   "poison_talisman" , ""  ,
// ^   "vigor_talisman"  , ""  ,
^   "aether_talisman" , "voidweaver"  ,
^   "fire_talisman"   , "cinderwisp" //  ,
// ^   "ice_talisman"    , ""  ,
// ^   "stone_talisman"  , ""
^]

var hidden_table = [
^   "ID"                  , "NAME"              , "REQUIRED ARMOR",

// Hidden wands
^   "wand_hidden_poison"  , "plague wand"       , 6   ,
^   "wand_hidden_vigor"   , "reset wand"        , 1   ,
^   "wand_hidden_aether"  , "calamity wand"     , 14  ,
^   "wand_hidden_fire"    , "explosive wand"    , 4   ,
^   "wand_hidden_ice"     , "frost wand"        , 3   ,
^   "wand_hidden_stone"   , "gravity wand"      , 0   ,

// Hidden staves
^   "staff_hidden_poison" , "berserker staff"   , 8   ,
^   "staff_hidden_vigor"  , "prevention staff"  , 4   ,
^   "staff_hidden_aether" , "grasping staff"    , 2   ,
^   "staff_hidden_fire"   , "infernal staff"    , 6   ,
^   "staff_hidden_ice"    , "eternity staff"    , 1   ,
^   "staff_hidden_stone"  , "acrobatic staff"   , 3
^]


// ............. Weapon Ranges .............. //

var range_table = [
^   "ID"                  , "RANGE",

^   "blade"               , 11,
^   "arm"                 , 7,

^   "bardiche"            , 9,
^   "bardiche R"          , 7,  // Ability
^   "heavy_hammer"        , 7,
^   "heavy_hammer R"      , 21, // Ability

// These count both for dashing and bashing shields:
^   "dash_min"            , 11,
^   "dash_max"            , 15,

^   "hatchet"             , 5,
^   "grappling_hook"      , 13,

// soul stones (the mindstone can be used no matter the range)
^   "star"                , 12,
^   "ki"                  , 18,
^   "experience"          , 18,
^   "triskelion"          , 10,
^   "moon"                , 18,

^   "sword"               , 5,
^   "hammer"              , 5,
^   "quarterstaff"        , 5,
^   "big_sword"           , 6,
^   "wand"                , 20,
^   "socketed_staff"      , 20,
^   "crossbow"            , 22,
^   "socketed_shield"     , 40  // Engaging range
^]

// Special stats

/* the added range that the
Grasping Staff's ability adds: */
var grasping_buff = 10
var grasping_cap = 22


// //////////////// FUNCTIONS //////////////// //

/* Returns a stat of an effect,
or null if there was an error.

Valid <source> values: "foe", "player"
Valid <effectType> values: "buff", "debuff"
Valid <effectId> values: same as in game.
  for more info, consult the Wiki: https://stonestoryrpg.miraheze.org/wiki/Status_Effects
Valid <statType> values:
  - "1", "symbol"
  - "2", "name", "id"
  - "3", "count", "stack"
  - "4", "time", "duration" */
func getEffectStat(source, effectType, effectId, statType)
  // Entry sanitization:
  source = string.ToLower(source)
  effectType = string.ToLower(effectType)
  effectId = string.ToLower(effectId)
  statType = string.ToLower(statType)

  var stat = null // the effect stat to return
  var fxStr = null // the full effects string

  ?source = "foe" & effectType = "buff"
    fxStr = foe.buffs.string
  :?source = "foe" & effectType = "debuff"
    fxStr = foe.debuffs.string

  :?source = "player" & effectType = "buff"
    fxStr = buffs.string
  :?source = "player" & effectType = "debuff"
    fxStr = debuffs.string

  :
    showStrCtr(0,30,#red,"ERROR\nFILE: Stats\nFUNCTION: getEffectStat\nCAUSE: unknown <source> or <effectType>: <" + source + ">, <" + effectType + ">", false)


  ?fxStr // there are active effects
    // all effects are separated by a "," char
    var fxArr = string.Split(fxStr, ",")

    for i = 0 .. fxArr.Count() - 1
      ?fxArr[i] = effectId // target effect is active
        var effectStats = string.Split(fxArr[i], ":")

        ?statType = "1" | statType = "symbol"
          stat = effectStats[0]
        :?statType = "2" | statType = "name" | statType = "id"
          stat = effectStats[1]
        :?statType = "3" | statType = "count" | statType = "stack"
          stat = effectStats[2]
        :?statType = "4" | statType = "time" | statType = "duration"
          stat = effectStats[3]

  return stat
/* Returns a string formatted like a table with
a given target's active effects and their stats.
Has a full and a slim version.

- Full version example: "
Foe debuffs table:

Symbol | Name | Count | Time
❄ | chill | 4 | 352
o | stun | 1 | 100
* | unstable | 1 | 12
"

- Slim version example: "
Player buffs table:

<sym> x<n>, <mm:ss f>
i x12, 00:05 24f
∞ x4, 00:07 12f
"

Valid <source> values: "foe", "player"
Valid <effectType> values: "buff", "debuff" */
func getEffectsTable(source, effectType, isSlim)
  // Entry sanitization:
  source = string.ToLower(source)
  effectType = string.ToLower(effectType)

  var fxPanel = null
  var fxStr = null
  var fxTable = null

  ?source = "foe" & effectType = "buff"
    fxStr = foe.buffs.string
    fxTable = "Foe buffs"
  :?source = "foe" & effectType = "debuff"
    fxStr = foe.debuffs.string
    fxTable = "Foe debuffs"

  :?source = "player" & effectType = "buff"
    fxStr = buffs.string
    fxTable = "Player buffs"
  :?source = "player" & effectType = "debuff"
    fxStr = debuffs.string
    fxTable = "Player debuffs"

  :
    showStrCtr(0,30,#red,"ERROR\nFILE: Stats\nFUNCTION: showEffects\nCAUSE: unknown <source> or <effectType>: <" + source + ">, <" + effectType + ">", false)


  ?fxStr // there are active effects
    /* Overview:
    1. split effects
    2. for each effect, split into an array with all its stats, and
      a. add it to the matrix, or
      b. for the slim version, add all except index 2 (the name).

    - Effects are separated by a "," char
    - Stats are separated by a ":" char */
    
    var fxArr = []
    fxTable += "table:\n\n"

    ?!isSlim
      fxTable += "Symbol | Name | Count | Time\n"
    :
      fxTable += "<sym> x<n>, <mm:ss f>"

    var fxArr = string.Split(fxStr, ",")
    for i = 0 .. fxArr.Count() - 1
      var effectStats = string.Split(fxArr[i], ":")
      var statsFormatted = null

      ?!isSlim
        statsFormatted = string.Join(" | ", fxArr)
      :
        statsFormatted = fxArr[0] + " x" + // <sym>
        ^fxArr[2] + ", " + // <n>
        ^u.myFormatSpeedrun(fxArr[3]) // <t>

      fxArr = a.Add(statsFormatted)

    fxTable += string.Join("\n", fxArr)

  return fxTable

/* Returns the sum of a given target's current hp and
armor. */
func getTotalHP(source)
  ?source = "foe"
    return foe.hp + foe.armor
  :?source = "player"
    return hp + armor
  :
    showStrCtr(0,30,#red,"ERROR\nFILE: Stats\nFUNCTION: getTotalHP\nCAUSE: unknown <source>: " + source, false)

    return null

/* Returns an armor stat from an item's ID.
Works with hidden items only. */
func getArmor(source)
  var armorI = hidden_table.IndexOf(source) +
  ^            hidden_table.IndexOf("REQUIRED ARMOR")

  ?armorI > -1
    return hidden_table[armorI]
  : // armorI <= -1 -- not found or error
    u.showStrCtr(0,30,#red,"ERROR\nFILE: Stats\nFUNCTION: getArmor\nCAUSE: unknown <source>:\n\n" + source, false)
    return null
/* Returns a talisman's name/ID from a summon's ID. */
func getTalisman(source)
  var talismanI = talisman_table.IndexOf(source) +
  ^             talisman_table.IndexOf("TALISMAN ID")

  ?talismanI > -1
    return talisman_table[talismanI]
  : // talismanI <= -1 -- not found or error
    u.showStrCtr(0,30,#red,"ERROR\nFILE: Stats\nFUNCTION: getTalisman\nCAUSE: unknown <source>:\n\n" + source, false)
    return null
/* Returns a summon's name/ID from a talisman's ID. */
func getSummon(source)
  var summonI = talisman_table.IndexOf(source) +
  ^             talisman_table.IndexOf("SUMMON ID")

  ?summonI > -1
    return talisman_table[summonI]
  : // summonI <= -1 -- not found or error
    u.showStrCtr(0,30,#red,"ERROR\nFILE: Stats\nFUNCTION: getSummon\nCAUSE: unknown <source>:\n\n" + source, false)
    return null
/* Returns each weapon's range, or -1
if there was an error.

Accounts for the Grasping Staff's
buff and Pallas's debuff.
*************************************
   REQUIRES WEAPON ID EXCLUSIVELY.
************************************* */
func getRange(source)
  var result = null
  var isIncreased = false
  var canIncrease = true
  var isReduced = false

  var rangeI = range_table.IndexOf(source) +
  ^            range_table.IndexOf("RANGE")

  ?armorI > -1
    result = range_table[rangeI]

    ?source = "crossbow" |
    ^source = "triskelion"
      canIncrease = false

    ?buffs.string = "buff_range" &
    ^canIncrease & !isIncreased
      result = result + grasping_buff
      isIncreased = true

    ?debuffs.string = "pallas_phase2_debuff" & !isReduced
      result--
      isReduced = true

    ?result >= grasping_cap & isIncreased
      result = grasping_cap
  :
    u.showStrCtr(0,30,#red,"ERROR\nFILE: Stats\nFUNCTION: getRange\nCAUSE: unknown <source>:\n\n" + source, false)
    result = false

  return result
func inRange(source)
  ?source = "dash" | source = "bash"
    return getRange("dash_min") <= foe.distance &
    ^      foe.distance <= getRange("dash_max")
  :
    return foe.distance <= getRange(source)


//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄▀ END OF "Stats" ▄▀▄▀▄▀▄▀▄▀▄ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //
