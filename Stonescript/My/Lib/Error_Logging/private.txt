/* TODO:
 - [ ] implement errorMessage object/array:
   [string: file, string: function, string: cause]
 - [ ] implement logError(errorMessage: msg).
 - [ ] implement assert(bool: condition, errorMessage: msg).
 - [ ] store logged errors inside an array (NOT errorMessage, but a global error array containing errorMessage instances). */

// imports



// variables

/* ERROR INFO

Object definition

Error = ["FILE","AT","CAUSE"]

Member definition

- FILE: error's source file.
- AT: error's source function or script body.
- CAUSE: error's cause description.
*/
var errLog = [] // logs multiple errors.

var _FILE   = 0
var _AT     = 1
var _CAUSE  = 2

var errorFormat = "ERROR\nFILE: {0}\nAT: {1}\nCAUSE: {2}."


// functions



// private



// public

func hasErrors()
  return errLog.Count() > 0

func getErrorCount()
  return errLog.Count()

func getError()
  ?hasErrors()
    return string.Format(errorFormat,
    ^                    errLog[0][_FILE],
    ^                    errLog[0][_AT],
    ^                    errLog[0][_CAUSE])
  :
    return null

func logError(_file, _at, _cause)
  var result = false

  ?Type(_file) ! "string"
    errLog.Add(["Error_Logging/private",
    ^        "logError",
    ^        "incorrect <_file> type. "+
    ^        "Expected 'string', was '"+Type(_file)+"'"])
  :?Type(_at) ! "string"
    errLog.Add(["Error_Logging/private",
    ^        "logError",
    ^        "incorrect <_at> type. "+
    ^        "Expected 'string', was '"+Type(_at)+"'"])
  :?Type(_cause) ! "string"
    errLog.Add(["Error_Logging/private",
    ^        "logError",
    ^        "incorrect <_cause> type. "+
    ^        "Expected 'string', was '"+Type(_cause)+"'"])

  :?hasErrors()
    var match = false
    for error : errLog
      ?error[_FILE] = _file &
      ^error[_AT] = _at &
      ^error[_CAUSE] = _cause
        match = true
        break
    ?!match
      errLog.Add([_file, _at, _cause])
      result = true
  :
    errLog.Add([_file, _at, _cause])
    result = true

  return result

func assert(_cond, _file, _at, _cause)
  ?Type(_cond) ! "bool"
    errLog.Add(["Error_Logging/private",
    ^        "assert",
    ^        "incorrect <_cond> type. "+
    ^        "Expected 'bool', was '"+Type(_cond)+"'"])
    return false

  :?!_cond
    return logError(_file, _at, _cause)
