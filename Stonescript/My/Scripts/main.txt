//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄ START OF "main" ▀▄▀▄▀▄▀▄▀▄ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //

// Read main-Readme.

/* //////////////// MOBILE ////////////////// //
// ↓↓↓ COPY FROM HERE ↓↓↓

// Paste your url here ↓↓↓
var mobileUrl =
^"https://raw.githubusercontent.com/
^FrancoSosaZ0206/Neo-Stonescripts/
^main/Stonescript/"

sys.SetFileUrl(mobileUrl)

// ↓↓↓ COMMENT OUT HERE ↓↓↓
import My/Scripts/main
// ↑↑↑ COMMENT OUT HERE ↑↑↑

// ↑↑↑ COPY UNTIL HERE ↑↑↑
// ////////////////////////////////////////// */

// /////////////////// PC /////////////////// //

// ················ IMPORTS ················· //

/* tracks global stats, displays them at
Deadwood Waterfall. */
import UI/BragMeter

var scrl = import UI/ScrollText
import My/Cosmetics

?sys.isMobile
  sys.cacheRemoteFiles = false // refresh between runs
  import UI/MindstoneButton

// ··············· PROCEDURE ················ //

enable npcDialog

// Watermark & version control
var author = "Tom Crow"

var scriptVer = "2.0.4"
var lastUpdated = "2025/06/28 19:00 GMT-6"
var disclaimer = "STEALING/MARKETING IS FORBIDDEN."

var dcTag = "@metaleagle26"
var dcUsername = "IronHawk, Imports Maniac"
var ytChannel = "IronHawk0206"

// Formatted watermark array
var watermarkArr = [
^"author: "+author,

^"version: "+scriptVer,
^"last updated: "+lastUpdated,
^"source: "+sys.fileUrl,
^"DISCLAIMER: "+disclaimer,

^string.Format("discord: {0} (SSRPG {1}: {2})",
^              dcTag,
^              "server username",
^              dcUsername),
^"YouTube: "+ytChannel,

^"############"]

// combine array with transparent char separators
var watermarkStr = string.Join("############", watermarkArr)
// replace remaining whitespaces for transparent characters "#"
watermarkStr = string.Split(watermarkStr)
watermarkStr = string.Join("#", watermarkStr)

// print params
var w = 16
var stepWait = 3
var c = color.Random()
// color rotation logic
?totaltime % (math.Pow(stepWait, 2)) = 0
  c = color.Random()

// print watermark with ScrollText
>`@screen.w - w@,0,@c@,
^@scrl.ScrollText(watermarkStr,w,stepWait,stepWait)@


// print identifier banner
?loc.begin
  ui.ShowBanner("Using IronHawk's Neo-Stonescripts!",
  ^             "Enjoy! ;)")

// withdrawal logic:

?!loc.isQuest & (
^loc = "cross_deadwood_river" |
^loc = "cross_bridge")
  loc.Leave()

// special locations logic
:?loc = "uulaa_shop" // Hotspring Shop
  import My/Scripts/Hotspring_Shop
:?loc = "undead_crypt_intro" // Haunted Gate
  disable npcDialog
  import My/Scripts/Gate

: // farming locations logic
  // disable hud // TO DO: uncomment this once we can print everything ourselves.

  import UI/FoeStateTracker
  var aim = import My/Meters/Ai/public
  var tm = import My/Meters/Time/public
  var dps = import My/Meters/DPS/public

  aim.main()
  tm.main(0,-1,#red,"slim ui speedrun")
  dps.main(-8,-1,#yellow,1,"slim ui")

  ?loc = "rocky_plateau"
    disable npcDialog
    import My/Scripts/1-Rocky
  :?loc = "deadwood_valley"
    import My/Scripts/2-Deadwood
  :?loc = "caustic_caves"
    import My/Scripts/3-Caves
  :?loc = "fungus_forest"
    import My/Scripts/4-Mushroom
  :?loc = "undead_crypt"
    import My/Scripts/5-Halls
  :?loc = "bronze_mine"
    import My/Scripts/6-Mine
  :?loc = "icy_ridge"
    import My/Scripts/7-Ridge
  :?loc = "temple"
    import My/Scripts/8-Temple

//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄▀ END OF "main" ▄▀▄▀▄▀▄▀▄▀▄ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //