//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀ START OF "3-Caves" ▄▀▄▀▄▀▄▀ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //

// ///////////////// IMPORTS ///////////////// //

var log = import My/Lib/Error_Logging/public

var c = import My/Lib/Combat
var s = import My/Lib/Stats
var a = import My/Lib/Arsenal

var es = import My/Lib/Effect_Stats/public
var tracker = import My/Lib/Ranged_Debuffing/public

// //////////////// VARIABLES //////////////// //

// file
var _FILE = "Scripts/3-Caves"

// flags
var canDash  = true

// values
var stunTime = null
var hand     = null

var itemSplash = 17 // bardiche hitbox + splash area

// screens
var isMinibossScr = false
var lastFoeScr = null
var isLastFoeScr = false
var bossScr = null
var isBossScr = false

// TODO: implement
var boss_cutscenes = []

// counters
// number of runs in this session
var nRuns = 1
/* time when permapot was activated,
   -1 means not activated. */
var permapotActTime = -1
// permapot time bandwidth
var permapotTbw = loc.averageTime -
^                 (loc.averageTime / 10)

var old_t = item.GetTreasureCount()
var end_t = 0

var minibossAtkTime = 0

// //////////////// FUNCTIONS //////////////// //

func fight(mode)
  ?mode = "escape"
    canDash = false

    ?c.canUseAbility("mind")
      a.ldtF("mind")
    : // c.canUseAbility("staff_stone")
      c.useAbility_TH("staff_stone", a.stSh)

  :?mode = "evade"
    canDash = false

    ?c.canUseAbility("staff_stone") &
    ^ (foe ! "ranged" |
    ^  (foe = "phase" & s.inRange("dash_min")))
      c.useAbility_TH("staff_stone", a.stSh)
    :?c.canUseAbility("mind") &
    ^ (foe ! "ranged" |
    ^  (foe = "phase" & s.inRange("dash_min")))
      a.ldtF("mind")

    /* :?c.canUseAbility("staff_ice") &
    ^ s.inRange("dash_min")
      c.useAbility_TH("staff_ice", a.stIh) */

    :?!s.inRange("sword")
      ?foe.count > 1
        fight("normal")
      :
        fight("normal")

    // Unmake
    :?es.canUnmake()
      a.ldtF("defense unmake")
    :
      a.ldtF("defense")

  :?mode = "normal"
    // ........... DEBUFFING LOGIC ........... //

    ?(tracker.canTrack("Weaken") |
    ^ tracker.canTrack("Burn")    ) &
    // AOE debuffing
    ^((!canDash |
    ^  (foe = "elite" &
    ^   foe.GetCount(itemSplash) > 1)))
      ?tracker.canTrack("Weaken")
        a.ldtF("magic debuff weaken")
      : // tracker.canTrack("Burn")
        a.ldtF("magic debuff burn")

    // 1v1 strong foe weakening
    :?es.canDebuff("Weaken") & canDash &
    ^ (foe = "monarch" | foe = "boss") &
    ^ s.inRange("sword")
      a.ldtF("sword debuff weaken")

    :?es.canMultiDebuff(["Chill","Burn"],"any") &
    ^ canDash & s.inRange("sword") &
    // Permapot 1v1 debuffing
    ^ ((c.permapotSetup & foe = "elite" &
    ^   foe.GetCount(itemSplash) = 1) |
    // 1v1 strong foe debuffing
    ^  (((loc.stars > 15 &
    ^     (foe = "monarch" | foe = "boss") &
    ^     s.getFoeDmg(a.berserkerLvl,
    ^                 a.plagueLvl   ,
    ^                 a.maskLvl      ) > 0) |
    ^    nRuns = 1)))
      a.ldtF("sword debuff auto")

    // ............ HEALING LOGIC ............ //
    :?hp ! maxhp &
    ^ ((canDash &
    ^   foe.GetCount(itemSplash) = 1) |
    ^  foe = "monarch")
      a.ldtF("sword heal")

    // ........... UNMAKING LOGIC ............ //
    :?es.canUnmake()
      ?canDash &
      ^foe.GetCount(itemSplash) = 1
        a.ldtF("sword unmake")
      : // foe.GetCount(itemSplash) > 1
        a.ldtF("big sword unmake")

    // ............ DAMAGE LOGIC ............. //
    
    :?canDash &
    ^ (foe.GetCount(itemSplash) = 1 |
    ^  foe = "monarch" | foe = "phase")
      ?c.canUseAbility("staff_poison") &
      ^!s.inRange("dash")
        c.useAbility_TH("staff_poison", a.stPh)
      :?c.canUsePotion() & foe = "phase"
        activate potion
      // reduce damage with Cultist Mask
      :?c.canUseAbility("mask") & foe = "phase"
        c.useAbility_OH("mask", "mask", "r")
      :
        a.ldtF("sword dmg")

    : // !(canDash | (...))
      ?c.permapotSetup
        a.ldtF("magic dmg")
      :
        a.ldtF("ranged dmg")

  : // invalid mode
    log.logError(_FILE,"fight",
    ^   "unknown <mode>: "+mode)

// //////////////// PROCEDURE //////////////// //

// .............. MONITOR LOGIC .............. //

// permapot time bandwidth monitor:
?permapotActTime ! -1 & nRuns = 1
  var delay = totaltime + (5 * 30)
  ?totaltime <= delay
    >o-7,6,#magenta,wait until 
    ^@time.FormatDigital(permapotTbw)@

/* // foe distance monitor
?foe.distance ! 9999
  >f0,10,#magenta,dist=@foe.distance@
*/

// bfg ds screens monitor
var bfgDsTargetScr = null
bfgDsTargetScr = c.getScreen_DS("blade",
^                               "TARGET")

?bfgDsTargetScr
  var bfgCol = #00ff00

  ?screen.i > bfgDsTargetScr
    bfgCol = #888888
  :?screen.i < bfgDsTargetScr
    bfgCol = #ffff00
  : // screen.i = bfgDsTargetScr
    bfgCol = #00ff00

  >`1,10,@bfgCol@,bfg: @screen.i@/
  ^@bfgDsTargetScr@

// item states monitor:
>`1,12,#magenta,ils: @item.left.state@ | 
^irs: @item.right.state@

var permapotCol
?c.permapotSetup
  permapotCol = #00ff00
:
  permapotCol = #ff0000
>`1,13,@permapotCol@,permapotSetup

// show effect durations
?buffs.string = "ʘ" | buffs.string = "lucky"
  var potTime
  ?buffs.string = "ʘ"
    potTime = buffs.GetTime("ʘ")
  : // buffs.string = "lucky"
    potTime = buffs.GetTime("lucky")

  >`1,15,#0066ff,@c.potionType@: @potTime@f 
  ^(@time.FormatDigital(potTime, true)@)

?debuffs.string = "damage"
  var weakenTime
  weakenTime = debuffs.GetTime("damage")

  >`1,16,#008800,∞: @weakenTime@f 
  ^(@time.FormatDigital(weakenTime, true)@)

// show any logged errors
?log.hasErrors()
  >`1,@screen.h - 2 - 5@,#ff0000,errorCount: 
  ^@log.getErrorCount()@\n
  >@log.getError()@

?foe = "cool_bat"
  >f0,6,#magenta,@minibossAtkTime@f

// ............. WITHDRAWAL LOGIC ............ //

?(foe ! "boss" & s.getTotalHP("player")
^ <=
^s.getFoeDmg(a.berserkerLvl,
^            a.plagueLvl,
^            a.maskLvl      )) |
^nRuns >= 25
  loc.Leave()

// ............... SETUP LOGIC ............... //

?loc.loop
  nRuns++

  old_t = item.GetTreasureCount()
  end_t = 0

// set up isMinibossScr
isMinibossScr =
   // yellow
^ ((loc.stars = math.Clamp(loc.stars,
^                          11, 15) &
^   screen.i = 6) |
   // green
^  (loc.stars = math.Clamp(loc.stars,
^                          16, 20) &
^   screen.i = 2))

// set up lastFoeScr and bossScr
?!(lastFoeScr | bossScr)
  ?loc.stars <= 4 // white
    lastFoeScr = 3
    bossScr = 5
  ?loc.stars = 5
    lastFoeScr = 2
    bossScr = 4
  :?loc.stars <= 10 // cyan
    lastFoeScr = 6
    bossScr = 9
  :?loc.stars <= 15 // yellow
    lastFoeScr = 5
    bossScr = 8
  :?loc.stars <= 20 // green
    lastFoeScr = 2 + loc.stars - 16
    bossScr    = 5 + loc.stars - 16
: // set up isLastFoeScr and isBossScr
  isLastFoeScr = (screen.i = lastFoeScr)
  isBossScr    = (screen.i = bossScr)

/* set up permapotSetup:
- in first run (yellow onwards), and
- once the potion has been consumed. */
c.permapotSetup =
^   (loc.stars > 10 & nRuns = 1 &
^    item.potion = "empty"      &
^    buffs.string = "lucky"      )

// Record time when permapot was activated
?c.permapotSetup & permapotActTime = -1
  permapotActTime = totaltime

// BFG logic
?lastFoeScr & (nRuns > 1 | loc.stars <= 10)
  c.screenMngr_DS("blade", 2, lastFoeScr - 1)

// set up itemSplash default value
itemSplash = 17 // bardiche's hitbox + itemSplash area

// set up canDash
?isMinibossScr & foe ! "boss"
  canDash = false
:
  canDash = true

// init Combat
c.Init()
// start up Ranged_Debuffing tracker
tracker.track()

// set up stunTime
stunTime = foe.debuffs.GetTime("stun")

// Decide hand for one-handed items
?c.permapotSetup |
^(s.inRange("dash") & canDash & ai.walking)
  hand = "left"
:
  hand = "right"

// Auto brewing
c.potionType = "lucky"
?c.canBrewPotion()
  c.brewPotion()

// track post-treasure time
?old_t ! item.GetTreasureCount()
  end_t++

// set up minibossAtkTime
?foe = "cool_bat"
  ?foe.state = 33 & foe.time = 0
    minibossAtkTime = 0
  ?foe.state ! 32
    minibossAtkTime ++

// ........................................... //

// Permapot equipment
?c.permapotSetup
  a.ldtF("permapot")

c.AAC("sword *0", "hatchet")

// Initial equipments
?loc.begin
  a.ldtF("default")

/* activate potion post-treasure
(after using Infernal Staff)
or while setting up permapot. */
:?c.canUsePotion() & loc.stars > 10 &
^ (c.permapotSetup |
^  (end_t > 10 & isBossScr &
^   !c.canUseAbility("staff_fire")))
  activate potion

// Summons
:?c.canSummon("voidweaver") &
^ screen.i <= lastFoeScr
  c.useAbility_OH("aether_talisman",
  ^   "aether_talisman", hand       )
:?c.canSummon("cinderwisp") &
^ screen.i > lastFoeScr
  c.useAbility_OH("fire_talisman",
  ^   "fire_talisman", hand       )

// ............. DEFENSIVE LOGIC ............. //

/* TODO: change Ceiling Decorator logic so that
we farm knockbacks with it until we kill it or
we are in the Bolesh's screen. */
// Ceiling Decorator and Bolesh defensive logic
:?(foe = "cool_bat" & minibossAtkTime > 25) |
^ (foe = "phase" & foe.state = 142 &
^  (foe.debuffs.string = "chill:6" & foe.time > 55))
  >c0,0,#ff0000,*** ABOUT TO ATTACK! ***

  // prevent Weaken
  ?c.canUseAbility("staff_vigor")
    c.useAbility_TH("staff_vigor", a.stVh)

  // avoid damage with Eternity Staff
  :?c.canUseAbility("staff_ice") &
  ^ foe = "phase" & s.inRange("hatchet")
    c.useAbility_TH("staff_ice", a.stIh)
  
  : // dodge hit
    fight("evade")
    >c0,1,#yellow,EVADING ATTACK!

// ............. CUTSCENES LOGIC ............. //



// .............. WALKING LOGIC .............. //

// speed up (post-treasure, after using potion)
:?c.canUseAbility("staff_fire") &
^ end_t > 10 & isBossScr &
^ loc.stars > 10
  >c0,0,#ff0000,SPEED UP (POST-TREASURE)

  c.useAbility_TH("staff_fire", a.stFh)

/* speed up (normal) when:
  - speed effect not already present
  - last foe in screen just defeated
  - too far to attack
  - save armor if it's setup run
  - not facing the miniboss */
:?c.canUseAbility("staff_fire") &
^ screen.i < bossScr &
^ buffs.string ! "buff_inf_speed" &
^ ((minibossScr & screen.i > minibossScr) |
^   screen.i = lastFoeScr + 1)
  >c0,0,#ff0000,SPEED UP (NORMAL)

  c.useAbility_TH("staff_fire", a.stFh)

// mini-dash
:?c.canUseAbility("quarterstaff") &
^ ai.walking & ai.enabled & !ai.paused &
// Cannot dash and in dashing range
^ !(canDash & a.canSmartDash()) &
^ (c.permapotSetup |
^  (!c.permapotSetup & screen.i > 0))
  c.useAbility_TH("quarterstaff", a.myQ)

// Pickup logic
:?pickup.distance < 15 &
// foe is farther than pickup
^(foe.distance = 9999 |
^ foe.distance > 15 |
// pickup is a treasure
^ pickup = "treasure")
  a.ldtF("magnet")

// regen armor (post-treasure)
:?(loc.stars <= 10 &
^  old_t ! item.GetTreasureCount()) |
^ (end_t > 1 & isBossScr &
^ !(c.canUsePotion() |
^   c.canUseAbility("staff_fire")))
  a.ldtF("defense armor regen")

// armor regen logic
:?(ai.walking & !s.inRange("socketed_shield")) |
^ (ai.paused | !ai.enabled |
^  player.direction = -1)
  a.ldtF("defense armor regen")

// engaging logic (out of fighting range)
:?ai.walking & player.direction = 1 &
^ s.inRange("socketed_shield") &
^ ((canDash & !s.inRange("dash_max")) |
^  !s.inRange("crossbow"))
  ?armor > a.compoundMaxArmor_boosted |
  ^(c.canEngage() & foe ! "phase")
    a.ldtF("engage")
  :
    a.ldtF("defense armor regen")

// ............... RANGED LOGIC .............. //



// .............. DASHING LOGIC .............. //

:?a.canSmartDash() & canDash
  a.smartDash()

// Knockback farming
:?c.canKbFarm()
  c.smoothCast(a.bS)

/* if in range, get closer while trying to
unmake with repeating crossbow. */
:?canDash &
^ s.betweenRange("triskelion","crossbow") &
^ c.canSmoothCast(true) & foe ! "boss"
  c.smoothCast(a.bR)

// engaging logic (within fighting range)
:?ai.walking & player.direction = 1 &
^foe ! "phase" & canDash & !a.canSmartDash() &
^s.betweenRange("triskelion", "dash_max")
  a.ldtF("engage")

// get in melee range faster
:?c.canStutterStep()
  c.stutterStep()

// ........... SPECIAL ITEMS LOGIC ........... //

// Voidweaver - use it if possible
:?c.canUseAbility("voidweaver") &
^ c.canUnmakeVoidweaver() &
^ foe.count > 1 & s.inRange("dash_min") &
^ !isMinibossScr
  c.devour("voidweaver")

// Calamity - reduce hp
:?c.canUseAbility("wand_aether") &
^(encounter.eliteMod = "berserk"    |
^ encounter.eliteMod = "monarch"    |
^ foe = "monarch"                   |
^ encounter.eliteMod = "blessed"    |
^ foe.buffs.string = "buff_tenacious")
  c.useAbility_OH("wand_aether", a.wAh, hand)

// Reset buff
:?c.canUseAbility("wand_vigor") &
^ foe.buffs.string = "stoic"
  c.useAbility_OH("wand_vigor", a.wVh, hand)

// Reduce damage
:?c.canUseAbility("wand_poison") &
^(encounter.eliteMod = "berserk"    |
^ encounter.eliteMod = "monarch"    |
^ foe = "monarch"                   |
^ encounter.eliteMod = "blessed"    |
^ encounter.eliteMod = "hydra"      |
^ foe.buffs.string = "buff_tenacious")
  c.useAbility_OH("wand_poison", a.wPh, hand)

// Stun enemies
:?c.canUseAbility("wand_ice") &
^ es.canDebuff("Stun") &
^ foe.count > 1 &
^ encounter.eliteMod = "Venom" &
^ s.getTotalHP("player") < c.hihp
  c.useAbility_OH("wand_ice", a.wIh, hand)

/* // extend range
:?c.canUseAbility("staff_aether") &
^ s.inRange("dash_min")
  c.useAbility_TH("staff_aether", a.stAh) */

// ................ AOE LOGIC ................ //

:?foe.GetCount(itemSplash) > 1 &
^!(foe = "boss" | foe = "monarch")
  // Align foes
  ?c.canUseAbility("wand_stone") &
  ^s.inRange("wand")
    c.useAbility_OH("wand_stone", a.wSh, hand)

  // Heavy hammer logic
  :?c.canUseAbility("heavy_hammer") &
  ^ encounter.eliteMod = "plated"
    c.useAbility_TH("heavy_hammer", Mjölnir)

  // Stun/Perma-stun logic
  :?s.inRange("grappling_hook") &
  ^ foe.buffs.string = "stability" &
  // ^ foe = "elite" &
  // don't interrupt bardiche
  ^ !c.isCasting("bardiche") &
  ^ (es.canDebuff("Stun") |
    // stun time (frames) running out
  ^  (stunTime = math.Clamp(stunTime, 1, 11) &
     // not stunned with heavy hammer or Frost Wand
  ^   foe.debuffs.string ! "debuff_armor_fatigue" &
  ^   foe.debuffs.string ! "debuff_frost_stun"))
    a.ldtF("grappling")

  // permapot run
  :?encounter.eliteMod = "blessed" &
  ^ !c.isCasting("bardiche")
    itemSplash = foe.distance + 7

    // Debuff and unmake
    ?tracker.canTrack("Weaken")
      a.ldtF("magic debuff weaken")
    :?tracker.canTrack("Chill")
      a.ldtF("magic debuff chill")
    :?tracker.canTrack("Burn")
      a.ldtF("magic debuff burn")
    :
      itemSplash = 10
      a.ldtF("big sword unmake")

  // berserk potion
  :
    /* ?buffs.string = "ʘ" |
    // split hydras lose the Stable buff
    ^(encounter.eliteMod = "hydra" &
    ^ foe.buffs.string ! "stability") |
    // yellow and below
    ^loc.stars <= 15 |
    // more foes outside range
    ^(foe ! "elite" & screen.i > 1 &
    ^ (foe.count -
    ^ foe.GetCount(itemSplash))
    ^ > 0) */
    itemSplash = 10
    a.ldtF("big sword unmake")
  /*:
    equip Kubikiribocho*/

// ................ BOSS LOGIC ............... //

// Boss fight (Bronze Guardian)
:?foe = "phase"
  ?c.canKillCinderwisp(
  ^   a.cinderwispDmg_Default, 0) |
  // reached ignition cap
  ^c.isCinderwispCap(a.maxIgnitions_Default)
    c.devour("cinderwisp")

  ?c.canUseAbility("bardiche") & nRuns > 1
    c.useAbility_TH("bardiche", Kubikiribocho)
  :
    fight("normal")

// ................ 1V1 LOGIC ................ //

/* Tanky foe, use bardiche ability to lower
its hitpoints */
:?(foe = "monarch" | foe = "boss") &
^ (c.canUseAbility("staff_poison") |
^  c.canUseAbility("bardiche")      )
  ?c.canUseAbility("staff_poison")
    c.useAbility_TH("staff_poison", a.stPh)
  : // c.canUseAbility("bardiche")
    c.useAbility_TH("bardiche", Kubikiribocho)
:
  fight("normal")

//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄▀ END OF "3-Caves" ▄▀▄▀▄▀▄▀▄ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //
