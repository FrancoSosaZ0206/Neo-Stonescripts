// ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓ //
// ┃ ▄▀▄▀▄▀▄▀ START OF "4-Mushroom" ▄▀▄▀▄▀▄ ┃ //
// ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛ //

// ///////////////// IMPORTS //////////////// //

var log = import My/Lib/Error_Logging/public

var c = import My/Lib/Combat
var s = import My/Lib/Stats
var a = import My/Lib/Arsenal

var es = import My/Lib/Effect_Stats/public
var tracker = import My/Lib/Ranged_Debuffing/public

// //////////////// VARIABLES /////////////// //

// file
var _FILE = "Scripts/4-Mushroom"

// flags
var canDash  = true
var hardElite = false

// values
var stunTime = null
var hand     = null

var itemSplash = 17 // bardiche hitbox + splash area

// TODO: implement
var boss_cutscenes = []
var boss_locs = ["fungus_forest_boss",
^                "fungus_forest_boss_harder"]

// screens
var minibossScr = null
var isMinibossScr = (loc.stars > 10)
var lastFoeScr = null
var isLastFoeScr = (loc.id = "fungus_forest")
var bossScr = 0
var isBossScr = (boss_locs.Contains(loc.id))

var bfgInitScr = 1

// counters
// number of runs in this session
var nRuns = 1
/* time when permapot was activated,
   -1 means not activated. */
var permapotActTime = -1
// permapot time bandwidth
var permapotTbw = loc.averageTime -
^                 (loc.averageTime / 10)

var old_t = item.GetTreasureCount()
var end_t = 0

// /////////////// FUNCTIONS //////////////// //

func fight(mode)
  ?mode = "escape"
    canDash = false

    ?c.canUseAbility("mind")
      a.ldtF("mind")
    : // c.canUseAbility("staff_stone")
      c.useAbility_TH("staff_stone", a.stSh)

  :?mode = "evade"
    canDash = false

    ?c.canUseAbility("staff_stone") &
    ^ (foe ! "ranged" |
    ^  (foe = "phase" & s.inRange("dash_min")))
      c.useAbility_TH("staff_stone", a.stSh)
    :?c.canUseAbility("mind") &
    ^ (foe ! "ranged" |
    ^  (foe = "phase" & s.inRange("dash_min")))
      a.ldtF("mind")

    :?c.canUseAbility("staff_ice") &
    ^ s.inRange("dash_min")
      c.useAbility_TH("staff_ice", a.stIh)

    :?!s.inRange("sword")
      fight("normal")

    // Unmake
    :?es.canUnmake()
      a.ldtF("defense unmake")
    :
      a.ldtF("defense")

  :?mode = "normal"
    // ........... DEBUFFING LOGIC .......... //

    // AOE debuffing
    ?(tracker.canTrack("Weaken") |
    ^ tracker.canTrack("Burn")    ) &
    ^(!canDash |
    ^ (foe = "elite" &
    ^  foe.GetCount(itemSplash) > 1))
      itemSplash = foe.distance + 7
      ?tracker.canTrack("Weaken")
        a.ldtF("magic debuff weaken")
      : // tracker.canTrack("Burn")
        a.ldtF("magic debuff burn")

    // 1v1 strong foe weakening
    :?es.canDebuff("Weaken") & canDash &
    ^ (foe = "monarch" | foe = "boss") &
    ^ s.inRange("sword")
      a.ldtF("sword debuff weaken")

    :?es.canMultiDebuff(["Chill"/*,"Burn"*/],"any") &
    ^ canDash & s.inRange("sword") &
    // Permapot 1v1 debuffing
    ^ ((c.permapotSetup & foe = "elite" &
    ^   foe.GetCount(itemSplash) = 1) |
    // 1v1 strong foe debuffing
    ^  (((loc.stars > 15 &
    ^     (foe = "monarch" | foe = "boss") &
    ^     s.getFoeDmg() > 0) |
    ^    nRuns = 1 | buffs.string = "smite")))
      a.ldtF("sword debuff auto")

    // ........... HEALING LOGIC ............ //
    :?!(hp = maxhp |
    ^ foe = "acronian_cultist" |
    ^ foe.name = "Cult Guard"   ) &
    ^ ((canDash &
    ^   foe.GetCount(itemSplash) = 1) |
    ^  foe = "monarch")
      a.ldtF("sword heal")

    // ........... UNMAKING LOGIC ........... //
    :?es.canUnmake()
      ?canDash &
      ^foe.GetCount(itemSplash) = 1
        a.ldtF("sword unmake")
      : // foe.GetCount(itemSplash) > 1
        itemSplash = 10
        a.ldtF("big sword unmake")

    // ............ DAMAGE LOGIC ............ //
    :?foe = "phase2" & canDash
      ?c.canUseAbility("staff_poison") &
      ^!s.inRange("dash")
        c.useAbility_TH("staff_poison", a.stPh)
      
      // reduce their damage
      :?c.canUseAbility("wand_poison")
        c.useAbility_OH("wand_poison",a.wPh,hand)
      :?c.canUseAbility("mask")
        c.useAbility_OH("mask", mask, "r")
      :
        a.ldtF("sword dmg")

    :?canDash &
    ^ (foe.GetCount(itemSplash) = 1 |
    ^  foe = "monarch" | foe = "boss")
      a.ldtF("sword dmg")
    :
      itemSplash = foe.distance + 7
      a.ldtF("magic dmg")

  : // invalid mode
    log.logError(_FILE,"fight",
    ^   "unknown <mode>: "+mode)

// /////////////// PROCEDURE //////////////// //

// .............. SETUP LOGIC ............... //

?loc.loop
  nRuns++

// set up post-treasure metrics
?old_t ! item.GetTreasureCount()
  // reset metrics
  ?loc.loop | !isBossScr
    old_t = item.GetTreasureCount()
    end_t = 0
  : // track time
    end_t++

// set up isBossScr
isBossScr = (boss_locs.Contains(loc.id) &
^            screen.i = bossScr)

// set up lastFoeScr
?!lastFoeScr
  ?loc.stars <= 4
    lastFoeScr = 4
  :?loc.stars = 5 // white
    lastFoeScr = 7
  :?loc.stars <= 15 // cyan and yellow
    lastFoeScr = 5
  :?loc.stars <= 20 // green
    lastFoeScr = 1 + loc.stars - 16
: // set up isLastFoeScr
  isLastFoeScr =
  ^(loc.id = "fungus_forest" & screen.i = lastFoeScr)

// set up minibossScr
?!minibossScr
  // yellow
  ?loc.stars = math.Clamp(loc.stars, 11, 15)
    minibossScr = 15
  // green
  :?loc.stars = math.Clamp(loc.stars, 16, 20)
    minibossScr = 1
: // set up isMinibossScr
  isMinibossScr = (loc.stars > 10 &
  ^loc.id = "fungus_forest" & screen.i = minibossScr)

/* set up permapotSetup:
- from yellow onwards,
- in first run, or
- in green, when in boss screen. */
c.permapotSetup = (nRuns = 1 & loc.stars > 10 &
^(loc.stars <= 15 | (nRuns = 1 &
^(loc.id = "fungus_forest" &
^screen.i = lastFoeScr + 1) | isBossScr)))

// Record time when permapot was activated
?c.permapotSetup & permapotActTime = -1
  permapotActTime = totaltime

// BFG logic
?loc.stars <= 10 | nRuns > 1
  c.screenMngr_DS("blade",
  ^               bfgInitScr,
  ^               lastFoeScr-1)

// set up itemSplash default value
itemSplash = 17 // bardiche's hitbox + itemSplash area

// set up canDash
?isMinibossScr & foe ! "boss"
  canDash = false
:
  canDash = true

// set variables for getFoeDmg
s._berserkerLvl = a.berserkerLvl
s._plagueLvl    = a.plagueLvl
s._maskLvl      = a.maskLvl

// init Combat
c.Init()
// start up Ranged_Debuffing tracker
tracker.track()

// set up stunTime
stunTime = foe.debuffs.GetTime("stun")

// Decide hand for one-handed items
?c.permapotSetup |
^(s.inRange("dash") & canDash & ai.walking)
  hand = "left"
:
  hand = "right"

// Auto brewing
?loc.stars <= 10
  c.potionType = "berserk"
:
  c.potionType = "lucky"
?c.canBrewPotion()
  c.brewPotion()

// track post-treasure time
?old_t ! item.GetTreasureCount()
  end_t++

// ............. MONITOR LOGIC .............. //

// permapot time bandwidth monitor:
?permapotActTime ! -1 & nRuns = 1
  var delay = totaltime + (5 * 30)
  ?totaltime <= delay
    >o-7,6,#magenta,wait until 
    ^@time.FormatDigital(permapotTbw)@

/* // foe distance monitor
?foe.distance ! 9999
  >f0,10,#magenta,dist=@foe.distance@
*/

/* // monitor post-treasure metrics
var treasureCol = #888888
?end_t > 0 | old_t ! item.GetTreasureCount()
  treasureCol = #aaff00
:
  treasureCol = #888888
>`1,14,@treasureCol@,
^treasure:\n
^  old | @old_t@ (t = @end_t@)
>`1,16,
^  cur | @item.GetTreasureCount()@\n
^  cap | @item.GetTreasureLimit()@ */

// bfg ds screens monitor
var bfgDsTargetScr = null
bfgDsTargetScr = c.getScreen_DS("blade",
^                               "TARGET")

?bfgDsTargetScr & !boss_locs.Contains(loc.id)
  var bfgCol = #00ff00

  ?screen.i > bfgDsTargetScr
    bfgCol = #888888
  :?screen.i < bfgDsTargetScr
    bfgCol = #ffff00
  : // screen.i = bfgDsTargetScr
    bfgCol = #00ff00

  >`1,9,@bfgCol@,bfg: @screen.i@/
  ^@bfgDsTargetScr@

/* // item states monitor:
>`1,10,#magenta,ils: @item.left.state@ | 
^irs: @item.right.state@ */

var permapotCol
?c.permapotSetup
  permapotCol = #00ff00
:
  permapotCol = #ff0000
>`1,11,@permapotCol@,permapotSetup

// show effect durations
?buffs.string = "ʘ" | buffs.string = "lucky"
  var potTime
  ?buffs.string = "ʘ"
    potTime = buffs.GetTime("ʘ")
  : // buffs.string = "lucky"
    potTime = buffs.GetTime("lucky")

  >`1,12,#0066ff,@c.potionType@: @potTime@f 
  ^(@time.FormatDigital(potTime, true)@)

// ............ WITHDRAWAL LOGIC ............ //

?(foe ! "boss" &
^ s.getTotalHP("player") <= s.getFoeDmg()) |
^old_t = item.GetTreasureLimit()
  loc.Leave()

// ............... INIT LOGIC ............... //

// Permapot equipment
?c.permapotSetup
  a.ldtF("permapot")

c.AAC("sword *0", "hatchet")

// Initial equipments
?loc.begin
  a.ldtF("default")

// activate potion when in permapot
:?c.canUsePotion() & c.permapotSetup
  activate potion

// ........... POST-TREASURE LOGIC .......... //

:?loc.stars > 10 & end_t > 1 & isBossScr
  // pickup treasure
  ?pickup.distance < 15
    a.ldtF("magnet")

  // activate potion
  :?c.canUsePotion() & end_t > 70
    activate potion

  // speed up
  :?c.canUseAbility("staff_fire") &
  ^ item.potion = "empty" & end_t > 70
    c.useAbility_TH("staff_fire", a.stFh)

  : // regen armor
    a.ldtF("defense armor regen")

// .............. SUMMON LOGIC .............. //

:?c.canSummon("voidweaver") &
^!boss_locs.Contains(loc.id) &
^ screen.i <= lastFoeScr
  c.useAbility_OH("aether_talisman",
  ^   "aether_talisman", hand       )
:?c.canSummon("cinderwisp") & (isBossScr |
^(!boss_locs.Contains(loc.id) &
^ screen.i = lastFoeScr + 1))
  c.useAbility_OH("fire_talisman",
  ^   "fire_talisman", hand       )

// ............. DEFENSIVE LOGIC ............ //

// Angry Shroom's punch attack
:?foe = "phase1" & foe.state = 32
  ?c.canUseAbility("shovel") &
  ^foe.time = math.Clamp(foe.time,40,74)
    c.useAbility_TH("shovel", shovel)
  :?foe.time > 74
    fight("escape")
  :
    fight("normal")

:?foe.id = "mushroom_boss_fat" &
^ foe.state = 32
  ?c.canUseAbility("shovel") &
  ^foe.time = math.Clamp(foe.time,30,65)
    c.useAbility_TH("shovel", shovel)

  :?foe.time > 74
    ?c.canUseAbility("staff_vigor")
      c.useAbility_TH("staff_vigor", a.stVh)
    :?c.canUseAbility("staff_ice") &
    ^buffs.string ! "buff_staff_protection"
      c.useAbility_TH("staff_ice", a.stIh)
    :
      fight("escape")
  :
    fight("normal")

// ............ CUTSCENES LOGIC ............. //



// ......... ENGAGING/WALKING LOGIC ......... //

/* speed up when:
  - speed effect not already present
  - not boss, miniboss nor elite screens. */
:?c.canUseAbility("staff_fire") &
^ buffs.string ! "buff_inf_speed" &
^(loc.stars <= 15 | nRuns > 1) &
^ !boss_locs.Contains(loc.id) &
^ screen.i = lastFoeScr + 1
  c.useAbility_TH("staff_fire", a.stFh)

// mini-dash
:?c.canUseAbility("quarterstaff") &
^ ai.walking & ai.enabled & !ai.paused &
// Cannot dash and in dashing range
^!(canDash & a.canSmartDash())
  c.useAbility_TH("quarterstaff", a.myQ)

// pickup logic
:?pickup.distance < 15 &
// foe is farther than pickup
^(foe.distance > 15 | foe.distance = 9999)
  a.ldtF("magnet")

// armor regen logic
:?(ai.walking & !s.inRange("socketed_shield")) |
^ (ai.paused | !ai.enabled |
^  player.direction = -1)
  /*?!c.canBurst() & !c.permapotSetup
    c.burst(a.burstSwords)
  :*/
  a.ldtF("defense armor regen")

// engaging logic (out of fighting range)
:?ai.walking & player.direction = 1 &
^ s.inRange("socketed_shield") &
^ ((canDash & !s.inRange("dash_max")) |
^  !s.inRange("crossbow"))
  ?armor > a.CmaE |
  ^(c.canEngage() & foe ! "phase")
    a.ldtF("engage")
  :
    a.ldtF("defense armor regen")

// ............... RANGED LOGIC ............. //



// .............. DASHING LOGIC ............. //

:?a.canSmartDash() & canDash
  a.smartDash()

// Knockback farming
:?c.canKbFarm()
  c.smoothCast(a.bS)

/* if in range, get closer while trying to
unmake with repeating crossbow. */
:?c.canSmoothCast(true) & canDash
  c.smoothCast(a.bR)

// engaging logic (within fighting range)
:?ai.walking & player.direction = 1 &
^!boss_locs.Contains(loc.id) &
^canDash & !a.canSmartDash() &
^s.betweenRange("triskelion", "dash_max")
  a.ldtF("engage")

// get in melee range faster
:?c.canStutterStep()
  c.stutterStep()

// ............... BOSS LOGIC ............... //

// Angry shroom, Morel and Enoki
:?foe = "phase"
  ?c.canKillCinderwisp(a.cwpMiN, 0) |
  ^c.isCinderwispCap(a.cwpMiN)
    c.devour("cinderwisp")
  
  ?c.canUsePotion() & foe = "phase1"
    activate potion

  fight("normal")

// ............ SPECIAL FOE LOGIC ........... //

:?isMinibossScr
  ?foe = "explode" // Mr. Puff
    /* FIXME: Eternity Staff ability
    removes buff_staff_protection,
    put it back when the bug is fixed.

    ?c.canUseAbility("staff_ice") &
    ^buffs.string = "buff_staff_protection" &
    ^s.inRange("sword")
      c.useAbility_TH("staff_ice", a.stIh) */

    ?c.canUseAbility("staff_vigor")
      c.useAbility_TH("staff_vigor", a.stVh)

    :?buffs.string = "buff_staff_protection"
      ?!s.inRange("shovel")
        equip shovel

      :?s.getFoeDmg() > armor
        a.ldtF("defense heal")
      :
        fight("normal")
    :
      fight("evade")

  // The snail that's always with Mr. Puff
  :?foe = "snail" & foe = "boss" & foe.count > 1
    equip @a.bS@
  : // normal foes
    fight("normal")

// ................ AOE LOGIC ............... //

:?foe.GetCount(itemSplash) > 1
  // ......... SPECIAL ITEMS LOGIC .......... //
  hardElite = (
  ^(encounter.eliteMod = "berserk"    |
  ^ encounter.eliteMod = "monarch"    |
  ^ foe = "monarch"                   |
  ^ encounter.eliteMod = "blessed"    |
  ^ foe.buffs.string = "buff_tenacious"))

  // Voidweaver - use it if possible
  ?c.canUseAbility("voidweaver") &
  ^c.canUnmakeVoidweaver() &
  ^!isMinibossScr & !encounter.isElite
    c.devour("voidweaver")

  // Calamity - reduce hp
  :?c.canUseAbility("wand_aether") & hardElite
    c.useAbility_OH("wand_aether", a.wAh, hand)

  // Reset buff
  :?c.canUseAbility("wand_vigor") &
  ^ foe.buffs.string = "stoic"
    c.useAbility_OH("wand_vigor", a.wVh, hand)

  // Reduce damage
  :?c.canUseAbility("wand_poison") &
  ^(hardElite | encounter.eliteMod = "hydra")
    c.useAbility_OH("wand_poison", a.wPh, hand)

  // Stun enemies
  :?c.canUseAbility("wand_ice") &
  ^ es.canDebuff("Stun") & foe = "elite"
    c.useAbility_OH("wand_ice", a.wIh, hand)

  /* // FIXME: extend range
  :?c.canUseAbility("staff_aether") &
  ^ s.inRange("dash_min")
    c.useAbility_TH("staff_aether", a.stAh) */

  // Align foes
  :?c.canUseAbility("wand_stone")
    c.useAbility_OH("wand_stone", a.wSh, hand)

  // ........................................ //

  // Heavy hammer logic
  :?c.canUseAbility("heavy_hammer") &
  ^ encounter.eliteMod = "plated"
    c.useAbility_TH("heavy_hammer", Mjölnir)

  // Stun/Perma-stun logic
  :?s.inRange("grappling_hook") &
  ^ foe.buffs.string = "stability" & (
  ^ es.canDebuff("Stun") | (
    // stun time (frames) running out
  ^ stunTime = math.Clamp(stunTime, 1, 11) &
    // not stunned with heavy hammer or Frost Wand
  ^ foe.debuffs.string ! "debuff_armor_fatigue" &
  ^ foe.debuffs.string ! "debuff_frost_stun"))
    itemSplash = foe.distance + 5
    a.ldtF("grappling")

  // Debuff and unmake
  :?encounter.eliteMod = "blessed"
    itemSplash = foe.distance + 7

    ?tracker.canTrack("Weaken")
      a.ldtF("magic debuff weaken")
    :?tracker.canTrack("Chill")
      a.ldtF("magic debuff chill")
    :?tracker.canTrack("Burn")
      a.ldtF("magic debuff burn")
    :
      itemSplash = 10
      a.ldtF("big sword unmake")

  // unmake
  :
    /* ?buffs.string = "ʘ" | // berserk pot
    ^ foe.buffs.string ! "stability" |
    // yellow and below
    ^loc.stars <= 15 |
    // more foes outside range
    ^(foe ! "elite" & screen.i > 1 &
    ^ (foe.count -
    ^ foe.GetCount(itemSplash))
    ^ > 0) */
    itemSplash = 10
    a.ldtF("big sword unmake")
  /*:
    equip Kubikiribocho*/

// ................ 1V1 LOGIC ............... //

:
  fight("normal")

// .......................................... //

?buffs.string = "buff_range"
  itemSplash += 10

// ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓ //
// ┃ ▀▄▀▄▀▄▀▄▀ END OF "4-Mushroom" ▄▀▄▀▄▀▄▀ ┃ //
// ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛ //
