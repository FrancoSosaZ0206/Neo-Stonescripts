//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄▀ START OF "1-Rocky" ▄▀▄▀▄▀▄▀▄▀ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //


// ///////////////// IMPORTS ///////////////// //

var uimkr = import My/Lib/UI_Maker
var u     = import My/Lib/Utilities
var c     = import My/Lib/Combat
var s     = import My/Lib/Stats
var a     = import My/Lib/Arsenal


// //////////////// VARIABLES //////////////// //

var yesButton     = null
var noButton      = null
var choice        = false
var buttonPressed = false
var dontShowAgain = false

var weakenType = a.weaken_default
var potionType = "lucky"
var hand       = null

var margin = 8 // wand left hitbox + splash range


// //////////////// FUNCTIONS //////////////// //

func harvestFunc(button)
  buttonPressed = true
  ?button = yesButton
    choice = true
  : // button = noButton
    choice = false

func fight(mode)
  ?mode = "evade"
    ?c.canUseAbility("mind") & (foe ! "ranged" | foe = "phase")
      a.ldtF("mind")
    :?c.canUseAbility("staff_stone") & (foe ! "ranged" | foe = "phase")
    ^& foe.distance < s.dash_range_min
      c.useAbility_TH(a.staffStoneHidden)
    
    :?c.canUseAbility("staff_ice") &
    ^foe.distance < s.dash_range_min
      c.useAbility_TH(a.staffIceHidden)
    
    :?foe.distance > s.getRange("sword")
      ?foe.count > 1
        fight("magic")
      :
        fight("ranged")

    :?foe ! "boss" & !c.isPermapotSetup &
    ^encounter.eliteMod ! "tenacious" &
    ^encounter.eliteMod ! "monarch" // can unmake
      a.ldtF("defense unmake")
    :
      a.ldtF("defense")

  :
    // 1. debuff, if possible
    ?c.canDebuffAny() & // foe can be debuffed
    ^encounter.eliteMod ! "tenacious" & // foe isn't immune to debuffs
    ^foe.buffs.string ! "buff_protection"
      ?mode = "melee"
        a.ldtF("melee 1v1 debuff")
      :?mode = "magic"
        a.ldtF("magic debuff")
      :?mode = "ranged"
        a.ldtF("magic debuff") // ranged

    :?hp ! maxhp // 2. Heal if needed
      ?mode = "melee"
        a.ldtF("melee 1v1 heal")
      :?mode = "magic"
        a.ldtF("melee heal") // magic
      :?mode = "ranged"
        a.ldtF("melee heal") // ranged

    :?foe ! "boss" & // 3. Unmake
    ^encounter.eliteMod ! "tenacious" &
    ^encounter.eliteMod ! "monarch"
      ?mode = "melee"
        a.ldtF("melee 1v1 unmake")
      :?c.isPermapotSetup
        a.ldtF("magic unmake")
      :
        a.ldtF("ranged unmake")

      /*:?mode = "magic"
        a.ldtF("magic unmake")
      :?mode = "ranged"
        a.ldtF("ranged unmake")*/

    : // 4. Deal max damage
      ?mode = "melee"
        /*?!c.isPermapotSetup & foe ! "phase2" & foe ! "phase3"
          c.doBursting(a.burstingSwords)
        :*/
          a.ldtF("melee 1v1 dmg")
      :?mode = "magic"
        a.ldtF("magic dmg")
      :?mode = "ranged"
        a.ldtF("ranged dmg")


// //////////////// PROCEDURE //////////////// //

/*?foe.distance ! 9999
  >`1,15,#magenta,dist#=#@foe.distance@\n
  ^dist#+#margin#=#@foe.distance + margin@\n
  ^count#=#@foe.count@\n
  ^count#in#dist#=#@foe.GetCount(foe.distance)@\n
  ^count#in#dist#+#margin#=#@foe.GetCount(foe.distance + margin)@\n*/

c.Init()
c.smiteScreen_Target = 2

disable npcDialog

// Auto-harvesting logic
?!buttonPressed
  ?!yesButton
    yesButton = uimkr.mkButton(
    ^-5,9,null,2,null,null, // x, y, w, h, anchor, dock,
    ^"Yes","#green", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvestFunc, // style, pressed,
    ^null,null,null) // down, up, sound

  ?!noButton
    noButton = uimkr.mkButton(
    ^5,9,8,2,null,null, // x, y, w, h, anchor, dock,
    ^"No","red", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvestFunc, // style, pressed,
    ^null,null,null) // down, up, sound


  u.showStrCtr(6,30,#yellow,"Harvest boulders?",false)
:
  ?yesButton
    yesButton.Recycle()
    yesButton = null
  ?noButton
    noButton.Recycle()
    noButton = null

  ?!dontShowAgain
    var aux = totaltime + 3*u.sec
    ?totaltime < aux
      ?choice
        u.showStrCtr(6, 30,#green,
        ^"Harvest boulders is enabled",false)
      :
        u.showStrCtr(6, 30,#red,
        ^"Harvest boulders is disabled",false)
    :
      ?!dontShowAgain
        dontShowAgain = true

?loc.stars > 15
  potionType = "lucky" // "berserk"
  // weakenType = a.weaken_boosted

// Decide hand for one-handed items
?c.isPermapotSetup
  hand = "left"
:
  hand = "right"

c.smiteScreen_Max = 4
c.screen_DS_Change("blade")
c.doAAC(a.hammerFireDefense, a.shieldFireDefense)

// Auto Brewing
?c.canBrewPotion(potionType)
  c.brewPotion(potionType)

?loc.begin // initial equipment
  a.ldtF("default")
:?c.isPermapotSetup
  a.ldtF("permapot")

?c.canUseAbility_DS("blade") & loc.stars > 15
  c.useAbility_DS("blade")

?c.canSummon("aether_talisman") & foe ! "boss"
  c.useAbility_OH("aether_talisman", hand)
:?c.canSummon("fire_talisman") & (foe = "boss" | loc.stars < 15)
  c.useAbility_OH("fire_talisman", hand)


:?c.canUsePotion(potionType) & c.isPermapotSetup
  activate potion
:?c.canUseAbility("staff_fire") &
^buffs.string ! "buff_inf_speed" &
^ai.enabled & !ai.paused
  // speed up
  c.useAbility_TH(a.staffFireHidden)

:?choice & harvest.distance < 5
  a.ldtF("harvest")
:?pickup.distance < 15
  a.ldtF("magnet")


// ABOUT TO ATTACK
:?(foe.state = 32 & (
^ (foe ! "boss" & foe.time > 50) |
^ (foe = "scout" & (
^   (foe.damage <= 5 & foe.time > 50)   |
^   (foe.damage >= 8 & foe.time > 155))) |
^ (foe = "phase1" & foe.time > 60) |
^ (foe = "phase2" & foe.time > 95) |
^ (foe = "phase3" & (
    // normal phase3 attacks
^   (foe.time > 85 & foe.damage ! 1) |
    // putting shield up
^   (foe.time > 65 & foe.damage = 1)) )  )   ) |
    // Dysangelos's massive beam
^(foe = "phase3" & foe.state = 115 & foe.time > 70) |
  // a bit into the attack state
^(foe.state = 33 & foe.time = 0 &
^ (foe = "phase2" | foe = "phase3"))

  ?foe ! "boss"
    ?!(encounter.eliteMod = "tenacious" |
    ^(encounter.eliteMod = "monarch" & foe.count = 1))
      a.ldtF("defense unmake")
    :?armor <= c.getFoeDamage(weakenType, a.mask_feebleCount)
      a.ldtF("defense heal")
    :
      fight("evade")

  :?foe = "phase3" &
  ^(foe.state = 115 | foe.damage = 1)
    ?foe.damage = 1 & // prevent the stun
    ^c.canUseAbility("staff_vigor")
      c.useAbility_TH(a.staffVigorHidden)
    :
      fight("evade")

  :?armor < c.getFoeDamage(weakenType, a.mask_feebleCount)
    ?foe = "phase3" & foe.damage ! 1 &
    ^foe.debuffs.string ! "feeble" &
    ^c.canUseAbility("staff_ice")
      c.useAbility_TH(a.staffIceHidden)

    :?foe ! "phase2" // heal to reduce it even more
      a.ldtF("defense heal")
    : // normal phase2 behaviour
      ?foe.buffs.string = "buff_protection" & // against debuffs
      ^c.canUseAbility("wand_vigor")
        c.useAbility_OH(a.wandVigorHidden, hand)
      :
        a.ldtF("defense")

  : // armor >= c.getFoeDamage(weakenType, a.mask_feebleCount)
    ?buffs.string ! "buff_damage" & !c.isPermapotSetup
      a.ldtF("defense buff empower")

    :?foe = "phase2"
      ?foe.buffs.string = "buff_protection" & // against debuffs
      ^c.canUseAbility("wand_vigor")
        c.useAbility_OH(a.wandVigorHidden, hand)
      :?foe = "fire" & !c.isPermapotSetup // unmake Dysangelos's fire arm
        a.ldtF("defense unmake")

      :
        a.ldtF("defense")


:?c.canUseAbility("quarterstaff") &
^!c.isDashingRange() & ai.walking
  c.useAbility_TH(a.myQuarterstaff)


// AOE strategy
:?foe.GetCount(foe.distance + margin) > 1 &
^foe.distance <= s.getRange("wand") & foe ! "warcaster"
  ?c.canUseAbility("voidweaver")
    c.devour("voidweaver")

  // Hinder regen
  ?encounter.eliteMod = "regen" &
  ^c.canUseAbility("wand_fire")
    c.useAbility_OH(a.wandFireHidden, "l")

  :?c.canUseAbility("heavy_hammer")
    c.useAbility_TH(Mjölnir)

  :?encounter.eliteMod ! "tenacious" &
  ^encounter.isElite &
  ^c.canUseAbility("wand_vigor") // reset buffs
    c.useAbility_OH(a.wandVigorHidden, "l")
  :?!encounter.isElite &
  ^foe.distance <= s.getRange("wand") &
  ^c.canUseAbility("wand_stone") // align foes
    c.useAbility_OH(a.wandStoneHidden, "l")
  :?encounter.eliteMod ! "tenacious" &
  ^c.canUseAbility("wand_aether") // Calamity - reduce hp
    c.useAbility_OH(a.wandAetherHidden, "l")

  :?foe.distance < s.dash_range_min &
  ^c.canUseAbility("mind")
    a.ldtF("mind")
  :?foe.distance < s.dash_range_min &
  ^c.canUseAbility("staff_stone")
    a.ldtF("staff_stone")

  :?c.isPermapotSetup
    ?foe.debuffs.string = "stun"
      equipL @a.wandAetherDebuff@
    :
      equipL grappling
    equipR mask
  :
    fight("ranged")


:?ai.paused |
^(ai.idle & foe.distance > s.dash_range_min) | // if we're waiting
^c.isDead() |
^foe.state = 124 | foe.state = 125 | // dysangelos phase 1 to 2 states
^(foe.state = 107 & foe.damage ! 1) | // dsangelos' flying animation
^foe.state = 108 | // dysangelos phase 2 to 3 states
^foe.state = 116 | // dysangelos phase 3 death animation state
^foe.distance > s.dash_range_max // or we can't engage in a fight
  ?armor > a.compoundMaxArmor_boosted |
  ^(foe ! "boss" & foe.distance < s.shield_rune_range)
    a.ldtF("engage")
  :?a.compoundMaxArmor_default <= armor &
  ^armor <= a.compoundMaxArmor_boosted
    a.ldtF("defense regen max")
  :
    a.ldtF("defense regen fast")


// fighting range
:?foe = "phase" // Dysangelos fight logic
  ?c.canUseAbility("bash")
    a.ldtF("bash")
  :?c.canUseAbility("dash")
    a.ldtF("dash")

  :?foe.debuffs.string = a.maxIgnitions_Default
    activate cinderwisp
  ?c.canUseAbility("staff_poison")
    c.useAbility_TH(a.staffPoisonHidden)

  :?foe ! "phase3"
    ?foe.buffs.string = "buff_protection" & // against debuffs
    ^c.canUseAbility("wand_vigor")
      c.useAbility_OH(a.wandVigorHidden, hand)
    :
      fight("melee")

  : // foe = phase3
    ?c.canUsePotion(potionType)
      activate potion
    :?c.canUseAbility("mask")
      c.useAbility_OH("mask", "r")
    :?c.canUseAbility("wand_poison")
      c.useAbility_OH(a.wandPoisonHidden, "l")
    :?c.canUseAbility("heavy_hammer") & foe.armor > 0 & // reduce armor
    ^!c.isPermapotSetup
      c.useAbility_TH("Guardian's Bane") //'
    :
      fight("melee")

: // 1v1
  ?c.canUseAbility("bash")
    a.ldtF("bash")
  :?c.canUseAbility("dash")
    a.ldtF("dash")
  :
    fight("melee")


//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄▀▄ END OF "1-Rocky" ▀▄▀▄▀▄▀▄▀▄▀ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //
