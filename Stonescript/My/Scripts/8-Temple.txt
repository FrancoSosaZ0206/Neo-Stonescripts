// ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓ //
// ┃ ▄▀▄▀▄▀▄▀ START OF "8-Temple" ▄▀▄▀▄▀▄▀▄ ┃ //
// ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛ //

// //////////////// IMPORTS ///////////////// //

var log = import My/Lib/Error_Logging/public

var u     = import My/Lib/Utilities
var c     = import My/Lib/Combat
var s     = import My/Lib/Stats
var a     = import My/Lib/Arsenal

var es = import My/Lib/Effect_Stats/public
var tracker = import My/Lib/Ranged_Debuffing/public

// /////////////// VARIABLES //////////////// //

// file
var _FILE = "Scripts/8-Temple"

// flags
var canDash  = true

// values
var stunTime = null
var hand     = null

var itemSplash = 17 // bardiche hitbox + splash area

// TODO: implement cutscenes if needed
var boss_cutscenes = []
var boss_locs = ["nagaraja", "nagaraja_harder"]

// screens
var minibossScr = null
var isMinibossScr = (loc.stars > 10)
var lastFoeScr = null
var isLastFoeScr = (loc.id = "temple")
var bossScr = 1
var isBossScr = (boss_locs.Contains(loc.id))

// counters
// number of runs in this session
var nRuns = 1
/* time when permapot was activated,
   -1 means not activated. */
var permapotActTime = -1
// permapot time bandwidth
var permapotTbw = loc.averageTime -
^                 (loc.averageTime / 10)

var old_t = item.GetTreasureCount()
var end_t = 0

// /////////////// FUNCTIONS //////////////// //

func fight(mode)
  ?mode = "escape"
    canDash = false

    ?c.canUseAbility("mind")
      a.ldtF("mind")
    : // c.canUseAbility("staff_stone")
      c.useAbility_TH("staff_stone", a.stSh)

  :?mode = "evade"
    canDash = false

    ?c.canUseAbility("staff_stone") &
    ^ (foe ! "ranged" |
    ^  (foe = "phase" & s.inRange("dash_min")))
      c.useAbility_TH("staff_stone", a.stSh)
    :?c.canUseAbility("mind") &
    ^ (foe ! "ranged" |
    ^  (foe = "phase" & s.inRange("dash_min")))
      a.ldtF("mind")

    :?c.canUseAbility("staff_ice") &
    ^ s.inRange("dash_min")
      c.useAbility_TH("staff_ice", a.stIh)

    :?!s.inRange("sword")
      fight("normal")

    // Unmake
    :?es.canUnmake()
      a.ldtF("defense unmake")
    :
      a.ldtF("defense")

  :?mode = "normal"
    // ........... DEBUFFING LOGIC .......... //

    // AOE debuffing
    ?(tracker.canTrack("Weaken") |
    ^ tracker.canTrack("Burn")    ) &
    ^(!canDash |
    ^ (foe = "elite" &
    ^  foe.GetCount(itemSplash) > 1))
      itemSplash = foe.distance + 7
      ?tracker.canTrack("Weaken")
        a.ldtF("magic debuff weaken")
      : // tracker.canTrack("Burn")
        a.ldtF("magic debuff burn")

    // 1v1 strong foe weakening
    :?es.canDebuff("Weaken") & canDash &
    ^ (foe = "monarch" | foe = "boss") &
    ^ s.inRange("sword")
      a.ldtF("sword debuff weaken")

    // 1v1 debuffing (permapot/strong foes)
    :?es.canMultiDebuff(["Chill"/*,"Burn"*/],"any") &
    ^ canDash & s.inRange("sword") & ((
    ^ c.permapotSetup & foe = "elite" &
    ^ foe.GetCount(itemSplash) = 1) | (((
    ^ loc.stars > 15 & (
    ^ foe = "monarch" | foe = "boss") &
    ^ s.getFoeDmg() > 0) |
    ^ nRuns = 1 | buffs.string = "smite")))
      a.ldtF("sword debuff auto")

    // ............ HEALING LOGIC ............ //
    :?!(hp = maxhp |
    ^ foe = "acronian_cultist" |
    ^ foe.name = "Cult Guard"   ) &
    ^ ((canDash &
    ^   foe.GetCount(itemSplash) = 1) |
    ^  foe = "monarch")
      a.ldtF("sword heal")

    // ........... UNMAKING LOGIC ............ //
    :?es.canUnmake()
      ?canDash &
      ^foe.GetCount(itemSplash) = 1
        a.ldtF("sword unmake")
      : // foe.GetCount(itemSplash) > 1
        itemSplash = 10
        a.ldtF("big sword unmake")

    // ............ DAMAGE LOGIC ............. //
    :?foe = "phase" & canDash
      ?c.canUseAbility("staff_poison") &
      ^!s.inRange("dash")
        c.useAbility_TH("staff_poison", a.stPh)
      :?c.canUsePotion()
        activate potion
      :
        a.ldtF("sword dmg")

    :?canDash &
    ^ (foe.GetCount(itemSplash) = 1 |
    ^  foe = "monarch" | foe = "boss")
      a.ldtF("sword dmg")
    :
      itemSplash = foe.distance + 7
      a.ldtF("magic dmg")

  : // invalid mode
    log.logError(_FILE,"fight",
    ^   "unknown <mode>: "+mode)

// /////////////// PROCEDURE //////////////// //

// .............. SETUP LOGIC ............... //

?loc.loop
  nRuns++

// set up post-treasure metrics
?old_t ! item.GetTreasureCount()
  // reset metrics
  ?loc.loop | !isBossScr
    old_t = item.GetTreasureCount()
    end_t = 0
  : // track time
    end_t++

// set up isBossScr
isBossScr = (boss_locs.Contains(loc.id) &
^            screen.i = bossScr)

// set up lastFoeScr
?!lastFoeScr
  ?loc.stars <= 4
    lastFoeScr = 4
  :?loc.stars = 5 // white
    lastFoeScr = 15
  :?loc.stars <= 10 // cyan
    lastFoeScr = 16
  :?loc.stars <= 15 // yellow
    lastFoeScr = 14
  :?loc.stars <= 20 // green
    lastFoeScr = 1 + loc.stars - 16
: // set up isLastFoeScr
  isLastFoeScr =
  ^(loc.id = "temple" & screen.i = lastFoeScr)

// set up minibossScr
?!minibossScr & loc.stars > 10
  // yellow
  ?loc.stars <= 15
    minibossScr = 15
  // green
  :?loc.stars <= 20
    minibossScr = 1
:?minibossScr // set up isMinibossScr
  isMinibossScr = (loc.id = "temple" &
  ^                screen.i = minibossScr)

/* set up permapotSetup:
- in yellow ONLY, first run. */
c.permapotSetup = (nRuns = 1 &
^loc.stars = math.Clamp(loc.stars,11,15))

// Record time when permapot was activated
?c.permapotSetup & permapotActTime = -1
  permapotActTime = totaltime

// BFG logic
var firstFoeScr = 1
?loc.stars = math.Clamp(loc.stars, 6, 15)
  firstFoeScr = 2

?!(loc.stars > 15 & nRuns = 1)
  ?loc.stars = 16
    c.screenMngr_DS("blade",
    ^   firstFoeScr, lastFoeScr)
  :
    c.screenMngr_DS("blade",
    ^   firstFoeScr, lastFoeScr - 1)

// set up itemSplash default value
// bardiche's hitbox + itemSplash area
itemSplash = 17 // s.getHbxSplash("bardiche", null)

// set up canDash
canDash = true

// set variables for getFoeDmg
s._berserkerLvl = a.berserkerLvl
s._plagueLvl    = a.plagueLvl
s._maskLvl      = a.maskLvl

// init Combat
c.Init()
// start up Ranged_Debuffing tracker
tracker.track()

stunTime = foe.debuffs.GetTime("stun")

// Decide hand for one-handed items
?c.permapotSetup |
^(s.inRange("dash") & canDash & ai.walking)
  hand = "left"
:
  hand = "right"

// Auto brewing
?loc.stars <= 10
  c.potionType = "berserk"
:
  c.potionType = "lucky"
?c.canBrewPotion()
  c.brewPotion()

// ............. MONITOR LOGIC .............. //

// permapot time bandwidth monitor:
?permapotActTime ! -1 & nRuns = 1
  var delay = totaltime + (5 * 30)
  ?totaltime <= delay
    >o-7,4,#magenta,wait until 
    ^@time.FormatDigital(permapotTbw)@

// monitor False-God
?foe = "elite" | encounter.isElite //eliteMod = "False-God"
  >`1,12,#magenta,mod: @encounter.eliteMod@\n
  ^buffs: @foe.buffs.string@

/* // monitor post-treasure metrics
var treasureCol = #888888
?end_t > 0 | old_t ! item.GetTreasureCount()
  treasureCol = #aaff00
:
  treasureCol = #888888
>`1,14,@treasureCol@,
^treasure:\n
^  old | @old_t@ (t = @end_t@)
>`1,16,
^  cur | @item.GetTreasureCount()@\n
^  cap | @item.GetTreasureLimit()@ */

// show any logged errors
?log.hasErrors()
  >`1,@screen.h - 2 - 5@,#ff0000,errorCount: 
  ^@log.getErrorCount()@\n
  >@log.getError()@

// ............ WITHDRAWAL LOGIC ............ //

?s.getTotalHP("player") <= s.getFoeDmg() |
^old_t = item.GetTreasureLimit()
  loc.Leave()

// ............... INIT LOGIC ............... //

// Permapot equipment
?c.permapotSetup
  a.ldtF("permapot")

c.AAC("sword *0 +0", "hatchet")

// Initial equipments
?loc.begin
  a.ldtF("default")

// activate potion when in permapot
:?c.canUsePotion() & c.permapotSetup
  activate potion

// ........... POST-TREASURE LOGIC .......... //

:?loc.stars > 10 & end_t > 1 & isBossScr
  // pickup treasure
  ?pickup.distance < 15
    a.ldtF("magnet")

  // activate potion
  :?c.canUsePotion() & end_t > 27
    activate potion

  // speed up
  :?c.canUseAbility("staff_fire") &
  ^ end_t > 27 & item.potion = "empty"
    c.useAbility_TH("staff_fire", a.stFh)

  : // regen armor
    a.ldtF("defense armor regen")

// .............. SUMMON LOGIC .............. //

:?c.canSummon("voidweaver") &
^ loc.id = "temple" & screen.i < lastFoeScr
  c.useAbility_OH("aether_talisman",
  ^   "aether_talisman", hand       )
:?c.canSummon("cinderwisp") & loc.id = "temple" &
^ screen.i >= lastFoeScr & foe.count <= 1
  c.useAbility_OH("fire_talisman",
  ^   "fire_talisman", hand       )

// ............. DEFENSIVE LOGIC ............ //

// Nagaraja debuffing spit attack
:?foe = "phase" &
^ foe.state = 32 & foe.time >= 45
  // SAC (Shovel Attack Cancelling)
  ?c.canUseAbility("shovel")
    c.useAbility_TH("shovel", "shovel")

  // prevent Weaken
  :?c.canUseAbility("staff_vigor")
    c.useAbility_TH("staff_vigor", a.stVh)
  
  :?buffs.string ! "buff_staff_protection"
    fight("evade")
  :
    fight("normal")

// Nagaraja brick attack
:?foe = "phase" &
^ [112, 102].Contains(foe.state)
  ?foe.time = math.Clamp(foe.time,55,65)
    fight("evade")
  :
    fight("normal")

// ............ CUTSCENES LOGIC ............. //



// ......... ENGAGING/WALKING LOGIC ......... //

/* speed up when:
  - speed effect not already present
  - not boss, miniboss nor elite screens. */
:?c.canUseAbility("staff_fire") &
^ buffs.string ! "buff_inf_speed" &
^ (loc.stars <= 15 | nRuns > 1) &
^ loc.id = "temple" &
^!(isMinibossScr | encounter.isElite)
  c.useAbility_TH("staff_fire", a.stFh)

// mini-dash
:?c.canUseAbility("quarterstaff") &
^ ai.walking & ai.enabled & !ai.paused &
// Cannot dash and in dashing range
^!(canDash & a.canSmartDash())
  c.useAbility_TH("quarterstaff", a.myQ)

// pickup logic
:?pickup.distance < 15 &
// foe is farther than pickup
^(foe.distance > 15 | foe.distance = 9999)
  a.ldtF("magnet")

// armor regen logic
:?(ai.walking & !s.inRange("socketed_shield")) |
^ (ai.paused | !ai.enabled |
^  player.direction = -1)
  /*?!c.canBurst() & !c.permapotSetup
    c.burst(a.burstSwords)
  :*/
  a.ldtF("defense armor regen")

// engaging logic (out of fighting range)
:?ai.walking & player.direction = 1 &
^ s.inRange("socketed_shield") &
^ ((canDash & !s.inRange("dash_max")) |
^  !s.inRange("crossbow"))
  ?armor > a.CmaE |
  ^(c.canEngage() & foe ! "phase")
    a.ldtF("engage")
  :
    a.ldtF("defense armor regen")

// ............... RANGED LOGIC .............. //



// .............. DASHING LOGIC ............. //

:?a.canSmartDash() & canDash
  a.smartDash()

// Knockback farming
:?c.canKbFarm()
  c.smoothCast(a.bS)

/* if in range, get closer while trying to
unmake with repeating crossbow. */
:?canDash & c.canSmoothCast(true)
  c.smoothCast(a.bR)

// engaging logic (within fighting range)
:?ai.walking & player.direction = 1 &
^ loc.id = "temple" &
^ canDash & !a.canSmartDash() &
^ s.betweenRange("triskelion", "dash_max")
  a.ldtF("engage")

// get in melee range faster
:?c.canStutterStep()
  c.stutterStep()

// ............... AOE LOGIC ................ //

:?foe.GetCount(itemSplash) > 1 & foe ! "boss"
  // .......... SPECIAL ITEMS LOGIC ......... //

  // Voidweaver - use it if possible
  ?c.canUseAbility("voidweaver") &
  ^c.canUnmakeVoidweaver() &
  ^!isMinibossScr
    c.devour("voidweaver")

  // Calamity - reduce hp
  :?c.canUseAbility("wand_aether") &
  ^(encounter.eliteMod = "berserk"    |
  ^ encounter.eliteMod = "monarch"    |
  ^ foe = "monarch"                   |
  ^ encounter.eliteMod = "blessed"    |
  ^ foe.buffs.string = "buff_tenacious")
    c.useAbility_OH("wand_aether", a.wAh, hand)

  // Reset buff
  :?c.canUseAbility("wand_vigor") &
  ^ foe.buffs.string = "stoic"
    c.useAbility_OH("wand_vigor", a.wVh, hand)

  // Reduce damage
  :?c.canUseAbility("wand_poison") &
  ^(encounter.eliteMod = "berserk"    |
  ^ encounter.eliteMod = "monarch"    |
  ^ foe = "monarch"                   |
  ^ encounter.eliteMod = "blessed"    |
  ^ encounter.eliteMod = "hydra"      |
  ^ foe.buffs.string = "buff_tenacious")
    c.useAbility_OH("wand_poison", a.wPh, hand)

  // Stun enemies
  :?c.canUseAbility("wand_ice") &
  ^ es.canDebuff("Stun") & foe = "elite"
    c.useAbility_OH("wand_ice", a.wIh, hand)

  /* // FIXME: extend range
  :?c.canUseAbility("staff_aether") &
  ^ !(isMinibossScr | nRuns = 1)
    c.useAbility_TH("staff_aether", a.stAh) */

  // Align foes
  :?c.canUseAbility("wand_stone") &
  ^s.inRange("wand")
    c.useAbility_OH("wand_stone", a.wSh, hand)

  /* Activate Berserk Staff early
  (in case there's barely enough armor,
  to not lose it to the Infernal Staff's
  Burn debuff) for the Scout. */
  :?c.canUseAbility("staff_poison") &
  ^isMinibossScr
    c.useAbility_TH("staff_poison", a.stPh)

  // ........................................ //

  // Heavy hammer logic
  :?c.canUseAbility("heavy_hammer") &
  ^ (encounter.eliteMod = "plated" |
  ^  loc.stars <= 15)
    c.useAbility_TH("heavy_hammer",Mjölnir)
  

  // Stun/Perma-stun logic
  :?s.inRange("grappling_hook") &
  // don't interrupt bardiche
  ^ !c.isCasting("bardiche") &
  ^ foe ! "warcaster" &
  ^ (es.canDebuff("stun") |
    // stun time (frames) running out
  ^  (stunTime = math.Clamp(stunTime, 1, 11) &
     // not stunned with heavy hammer or Frost Wand
  ^   foe.debuffs.string ! "debuff_armor_fatigue" &
  ^   foe.debuffs.string ! "debuff_frost_stun"))
    itemSplash = foe.distance + 5
    a.ldtF("grappling")

  // permapot run
  :?((nRuns = 1 & loc.stars > 15 &
  ^   foe = "elite") |
  ^  encounter.eliteMod = "blessed") &
  ^!(c.isCasting("bardiche") |
  ^  c.isCasting("talisman")  )
    // Debuff and unmake
    itemSplash = foe.distance + 7

    // Debuff and unmake
    ?tracker.canTrack("Weaken")
      a.ldtF("magic debuff weaken")
    :?tracker.canTrack("Chill")
      a.ldtF("magic debuff chill")
    :?tracker.canTrack("Burn")
      a.ldtF("magic debuff burn")
    :
      itemSplash = 10
      a.ldtF("big sword unmake")

  // berserk potion
  :?buffs.string = "ʘ" | foe ! "elite" |
  // split hydras lose the Stable buff
  ^ (encounter.eliteMod = "hydra" &
  ^  foe.buffs.string ! "stability") /* |
  // more foes outside range
  ^ (foe ! "elite" & screen.i > 1 &
  ^  (foe.count -
  ^   foe.GetCount(itemSplash))
  ^  > 0) */
    itemSplash = 10
    a.ldtF("big sword unmake")

  /* // False-God AOE
  :?foe.buffs.string = "immortal"
    ?nRuns <= 1
      a.ldtF("grappling")
    :
      equip @a.bS@ */

  :?buffs.string = "!"
    equip Kubikiribocho
  :
    a.ldtF("big sword unmake")

// ............... BOSS LOGIC ............... //

// Nagaraja fight logic
:?foe = "phase"
  ?c.canKillCinderwisp(a.cwpMiN, 0) |
  ^c.isCinderwispCap(a.cwpMiN)
    c.devour("cinderwisp")

  fight("normal")

// ................ 1V1 LOGIC ............... //

// Tanky foe, use bardiche ability to lower its hitpoints
:?loc.stars > 15 &
^(foe = "boss" | foe = "monarch") &
^(c.canUseAbility("staff_poison") |
^c.canUseAbility("bardiche"))
  ?c.canUseAbility("staff_poison")
    c.useAbility_TH("staff_poison", a.stPh)
  : // c.canUseAbility("bardiche")
    c.useAbility_TH("bardiche", Kubikiribocho)

/* // False-God 1v1
:?foe.buffs.string = "immortal"
  ?nRuns <= 1
    a.ldtF("grappling")
  :
    equip @a.bS@ */

:
  fight("normal")

// .......................................... //

?buffs.string = "buff_range"
  itemSplash += 10

// ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓ //
// ┃ ▄▀▄▀▄▀▄▀▄ END OF "8-Temple" ▀▄▀▄▀▄▀▄▀▄ ┃ //
// ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛ //
