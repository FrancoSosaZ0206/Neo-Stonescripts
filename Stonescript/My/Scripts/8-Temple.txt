// *******************************************************
//                      8-Temple.txt                      
// *******************************************************

// Script for Temple



// IMPORTS

var u = new MyScripts/Lib/Utilities
var uimkr = new MyScripts/Lib/UI_Maker
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat
c.Main()

/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter
var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED
var bosTmr
bosTmr = import MyScripts/Borrowed/BossfightTimer2

var somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)


dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/



// VARIABLES

var smiteScr = 2
var keepMask = true
var keepFTali = false

?foe = "boss"
  keepFTali = true

var canDebI
var canDebP
var canDebF

?loc.loop
  keepMask = false
  keepFTali = false


// FUNCTIONS:

func ldtF(mode) // loadouts

  ?mode = "default"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield2@
  :?mode = "engage"
    equipL triskelion
    ?!keepMask
      equipR @a.ishield@
  :?mode = "escape"
    equipL mind
    ?!keepMask
      equipR @a.ishield@
  :?mode = "magnet"
    equipL star
    ?!keepMask
      equipR triskelion

  :?mode = "meleeDmg"
    equipL Bifrost
    ?!keepMask
      equipR @a.isword@

  :?mode = "extend"
    equipL triskelion
    equipR mask

  :?mode = "magic"
    ?keepMask
      ?canDebI
        equipL Frosty Crust
      :?canDebP
        equipL @a.dPwand@
      :?canDebF
        equipL @a.dFwand@

    :
      /*?totaltime % 2 = 0
        equipL triskelion
        equipR hatchet*/

      ?canDebI
        equip @a.dIstaff@
      :?canDebP
        equip @a.dPstaff@
      :?canDebF
        equip @a.dFstaff@

  :
    >o0,0,#red,ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight1v1()

  /* How to apply debuffs:
  - apply all except the ones the foe is immune to.
  Finish when:
  - all possible debuffs have been applied. */
  canDebI = (foe.debuffs.string ! "chill:6" & foe ! "immune_to_debuff_chill")
  canDebP = (foe.debuffs.string ! "damage" & foe ! "immune_to_debuff_damage")
  canDebF = (foe.debuffs.string ! "dot" & foe ! "immune_to_debuff_dot")

  ?(keepMask | buffs.string = "berserk") & (canDebI | canDebP | canDebF)
    ?keepMask
      ?canDebI
        equipL @a.dIsword@
      :?canDebP
        equipL @a.dPsword@
      :?canDebF
        equipL @a.dFsword@
    :
      //Left hand:
      ?canDebP
        equipL @a.dPsword@
      :?canDebF
        equipL @a.dFsword@
      :?canDebI
        equipL Frosty Crust

      //Right hand:
      ?canDebI
        equipR @a.dIsword@

  :?keepMask
    equipL Bifrost
  :
    ?buffs.string = "berserk"
      c.doMoondial(Bifrost, a.isword, 2)
    :
      c.doMoondial(Bifrost, a.isword, 0)

  return



// PROCEDURE:


c.doAAC("sword 0*", a.cshield1)

>`15,2,#yellow,@foe.debuffs.string@

?hp < maxhp & !keepMask
  loc.Leave()

?loc.begin // AUTO BREWING

  ?res.bronze >= 10
    ?res.stone >= 10
      ?item.potion ! "lucky"
        brew stone + bronze

    :?res.wood >= 10
      ?item.potion ! "berserk"
        brew wood + bronze
    :
      loc.Leave()
  :
    loc.Leave()

?keepMask
  ?item.right ! "bardiche" &
  ^item.right ! "quarterstaff" &
  ^item.right ! "heavy hammer"
    equipR mask

?loc.begin | loc.loop
  equipL triskelion
  equipR @a.cshield1@

:?c.canUseAETali() & !keepFTali
  ?keepMask
    c.useAETali("l")
  :
    equipL triskelion
    c.useAETali("r")

:?c.canUseFTali() & keepFTali
  ?keepMask
    c.useFTali("l")
  :
    equipL triskelion
    c.useFTali("r")

:?c.canUsePotDmg() & keepMask
  activate potion

:?pickup.distance ! 9999
  ?c.canUseQstaff()
    c.useQstaff(a.myqstaff)
  :
    ldtF("magnet")
:

  ?c.canUseBladeDS(smiteScr)
    ?foe.distance <= 33
      c.useBladeDS()

  ?c.canUseQstaff() & ai.enabled
    ?item.right ! "bardiche" & item.right ! Kubikiribocho
    ^& item.right ! "heavy hammer" & (
    ^(!c.canBash() & !c.canDash())
    ^| (foe.distance < 11 | foe.distance > 15))
      c.useQstaff(a.myqstaff)

  :?foe.distance > 35
    ?14 <= armor & armor <= a.cMaxArmor
      ldtF("default")
    :?armor < 14
      ldtF("armorRegen")
    :
      equipL triskelion
      equipR mask

  :?foe.distance > 15
    ldtF("engage")

    var isAbtToDash
    isAbtToDash = (foe.distance <= 17)
    ?keepMask & isAbtToDash
      equipR @a.ishield@

    canDebI = (foe.debuffs.string ! "chill" &
    ^f ! "immune_to_debuff_chill")

    ?!keepMask & canDebI & isAbtToDash &
    ^i.getFoeTotalHP() > i.getBuffInfo("smite", false, 2) * a.smiteMult
      equipR @a.aiShield@

  :
    ?foe ! "nagaraja"
      ?c.canBash()
        c.bash(Bifrost)
      :?c.canDash()
        c.dash(Bifrost, a.dshield)
      :
        ?foe = "acronian_cultist"
          ?c.canUseMask()
            c.useMask()
          :
            fight1v1()  
        :
          ?foe.count >= 7 &
          ^c.canUseHamm() & !keepMask
          ^!c.canUseBlade()
            c.useHamm(a.myHamm)

          ://?foe.count > 1
            ldtF("magic")
          /*:?foe.distance < 7
            fight1v1()*/


    : // foe = "nagaraja"

      ?c.canUseCind() & dpsm.DPS ! "N/A"
        ?foe.debuffs.string = a.maxIgnitions_Default |
        ^c.canKillCind(i.getFoeTotalHP(),
        ^              i.getDebuffLvl("ignition", true),
        ^              a.cindDmg_Default, dpsm.DPS)
          c.useCind()

      ?c.canUsePot()
        activate potion

      ?(foe.state = 112 | foe.state = 102)
      ^& foe.time >= 55 & foe.time <= 65
        ?c.canUseMind()
          ldtF("escape")

        :?keepMask
          equipL @a.altbow@
        :
          equip @a.ibow@

      :?foe.state ! 4 // if it's not dying
        ?c.canBash()
          c.bash(Bifrost)
        :?c.canDash()
          c.dash(Bifrost,a.dshield)

        :?c.canUseBard() & keepMask
          c.useBard(Kubikiribocho)
        :
          fight1v1()



// *******************************************************
//                  END OF "8-Temple.txt"                 
// *******************************************************