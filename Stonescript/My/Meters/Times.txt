//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄▀ START OF "Times" ▀▄▀▄▀▄▀▄▀ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //

// ///////////////// IMPORTS ///////////////// //

var uimkr = import My/Lib/UI_Maker
var u = import My/Lib/Utilities

// ///////////////// PUBLIC ////////////////// //

func main(x, y, colorHex, mode)
  // sanitize mode entries
  mode = string.ToLower(mode)

  ?canUpdate(mode)
    Update(mode)

    ?mode = "ui"
      UiMngr(x,y,colorHex,mode)

    :?mode = "print"
      Print(x,y,colorHex)
    :
      u.showStrCtr(0,30,#red,
      ^            "ERROR\n"+
      ^            "FILE: Times\n"+
      ^            "FUNCTION: main\n"+
      ^            "CAUSE: invalid <mode>:\n\n"+
      ^            mode, false                      )

// ///////////////// PRIVATE ///////////////// //

// ................ Variables ................ //

var tt  = null
var pb  = null
var avg = null

var pnl_t = null
var txt_t = null

// ................ Functions ................ //

func canUpdate(mode)
  ?mode = "speedrun"
    return true
  :
    return loc.begin | (totaltime % u.sec = 0)
func Update(mode)
  ?mode = "speedrun" // update times every frame
    tt    = u.formatSpeedrun(totaltime)

    ?loc.begin | loc.loop
      pb  = u.formatSpeedrun(loc.bestTime)
      avg = u.formatSpeedrun(loc.averageTime)

  : // update times every second (30 frames)
    ?loc.begin | totaltime % u.sec = 0
      tt  = time.formatDigital(totaltime)

    ?loc.begin | loc.loop
      pb  = time.formatDigital(loc.bestTime)
      avg = time.formatDigital(loc.averageTime)

func ToString(size)
  var str = "####SPEED-O-METER####\n" +
  ^         "#·##·##·##·##·##·##·#\n" +
  ^         "#PB##{0}\n" +
  ^         "#AVG#{1}\n" +
  ^         "#CUR#{2}"

  ?size = "slim" | size = "small"
    /* TO DO: implement dynamic column paddings.
    var headers = ["☆", "≈", "▶"]
    var data    = [pb, avg, tt]*/

    str = "☆#{0}#|#≈#{1}#|#▶#{2}"

  :?!(size = "normal" | size = "default")
    str = null
    u.showStrCtr(0,30,#red,"ERROR\nFILE: Times\nFUNCTION: ToString\nCAUSE: invalid <size>:\n\n" + size, false)

  ?str
    str = string.Format(str, pb,avg,tt)

  return str

func Print(x, y, colorHex)
  >`@x@,@y@,@colorHex@,@ToString(mode)@

func UiMngr(x, y, colorHex, size)
  ?totaltime = 1*30 // loc.begin | loc.loop
    // DESTROY — Recycle old components
    // txt_t = uimkr.destroy(txt_t)
    pnl_t = uimkr.destroy(pnl_t)

    // INIT — create UI if needed
    var isValidSize = true

    // Panel settings
    var panelAnchor = bottom_left
    var panelDock   = bottom_left
    var panelStyle  = -2

    // Textbox settings
    var str = ToString(size)
    var txtW = u.getLargestStr(string.Split(str, "\n"))
    var txtH = string.Split(str,"\n").Count()

    // default size: "normal"/"default"
    ?size = "slim"      | size = "small"
      panelStyle = -1
      txtW = string.Size(str)
      txtH = 1

    :?!(size = "normal" | size = "default")
      isValidSize = false
      u.showStrCtr(0,30,#red,
      ^            "ERROR\n"+
      ^            "FILE: Times\n"+
      ^            "FUNCTION: UiMngr\n"+
      ^            "CAUSE: invalid <size>:\n\n"+
      ^            size, false                     )

    ?isValidSize
      txt_t = ui.AddText()
      txt_t.text = str
      txt_t.x = 1
      txt_t.y = 1
      txt_t.w = txtW
      txt_t.h = txtH
      txt_t.anchor = "top_left"
      txt_t.dock = "top_left"
      txt_t.color = colorHex

      pnl_t = ui.AddPanel()
      pnl_t.Add(txt_t)
      pnl_t.x = x
      pnl_t.y = y
      pnl_t.w = txt_t.w + 2
      pnl_t.h = txt_t.h + 2
      pnl_t.anchor = panelAnchor
      pnl_t.dock = panelDock
      pnl_t.color = colorHex
      pnl_t.style = panelStyle


  // UPDATE
  ?pnl_t & txt_t
    pnl_t.visible = true
    txt_t.visible = true

    // Update content
    txt_t.text = ToString(size)

//  ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓  //
//  ┃ ▄▀▄▀▄▀▄▀▄▀▄ END OF "Times" ▀▄▀▄▀▄▀▄▀▄ ┃  //
//  ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛  //