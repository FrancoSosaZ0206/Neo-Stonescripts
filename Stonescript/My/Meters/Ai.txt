/* For info about this file, see Ai_README.md */

// ==============================================
// PUBLIC
// ==============================================

func main()
  ?canInit()
    init()

  ?canUpdate()
    update()

// ==============================================
// PRIVATE
// ==============================================

var txt_Ai = null

// True on first frame of every run/loop
func canInit()
  return loc.begin | loc.loop

// UI construction
func init()
  ?txt_Ai // recycle old component(s)
    txt_Ai.Recycle()
    txt_Ai = null
  :
    // create with placeholder string
    txt_Ai = ui.AddText("AI Display")

    // define properties
    txt_Ai.x = 1
    txt_Ai.y = 0
    txt_Ai.w = string.Size("AI || E | P | I | W ||")
    txt_Ai.anchor = "center_left"
    txt_Ai.dock = "center_left"
    txt_Ai.color = "#ffffff"

// Update condition
func canUpdate()
  return txt_Ai

// Update display
func update()
  // Ensure visibility
  txt_Ai.visible = true

  // Collect AI values
  var values = [ai.enabled, ai.paused, ai.idle, ai.walking]
  var labels = ["E", "P", "I", "W"]
  var output = "AI#||#"

  var n = values.Count() - 1
  for i = 0 .. n

    // add color metadata based on flag state
    output += "[color=#"
    ?values[i]
      output += "00ff00" // rGb -> green
    :
      output += "ff0000" // Rgb -> red

    // add label
    output += "]" + labels[i] + "[/color]"

    // add separator (with transparent characters '#')
    ?i < n
      output += "#|#"
    :
      output += "#||"

  // assign and parse output into ui textbox
  txt_Ai.text = output
