/* For info about this file, see Ai_README.md */

// ==============================================
// PUBLIC
// ==============================================

func main()
  ?canDestroy()
    destroy()

  ?canConstruct()
    construct()

  ?canUpdate()
    update()

// ==============================================
// PRIVATE
// ==============================================

var txt = null

// True on first frame of every run/loop
func canConstruct()
  return loc.begin | loc.loop

// True if cleanup is needed
func canDestroy()
  return canConstruct() & txt

// Cleanup
func destroy()
  txt.Recycle()
  txt = null

// UI construction
func construct()
  // create with placeholder string
  txt = ui.AddText("AI Display")

  // define properties
  txt.x = 1
  txt.y = 0
  txt.w = string.Size("AI || E | P | I | W ||")
  txt.anchor = "center_left"
  txt.dock = "center_left"
  txt.color = "#ffffff"

// Update condition
func canUpdate()
  return txt

// Update display
func update()
  // Ensure visibility
  txt.visible = true

  // Collect AI values
  var values = [ai.enabled, ai.paused, ai.idle, ai.walking]
  var labels = ["E", "P", "I", "W"]
  var output = "AI#||#"

  var n = values.Count() - 1
  for i = 0 .. n

    // add color metadata based on flag state
    output += "[color=#"
    ?values[i]
      output += "00ff00" // rGb -> green
    :
      output += "ff0000" // Rgb -> red

    // add label
    output += "]" + labels[i] + "[/color]"

    // add separator (with transparent characters '#')
    ?i < n
      output += "#|#"
    :
      output += "#||"

  // assign and parse output into ui textbox
  txt.text = output
